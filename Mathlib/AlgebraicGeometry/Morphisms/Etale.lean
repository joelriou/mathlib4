/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Smooth
public import Mathlib.AlgebraicGeometry.Morphisms.FormallyUnramified
public import Mathlib.CategoryTheory.MorphismProperty.Comma
public import Mathlib.RingTheory.Smooth.StandardSmoothCotangent
public import Mathlib.CategoryTheory.Limits.MorphismProperty

/-!

# √âtale morphisms

A morphism of schemes `f : X ‚ü∂ Y` is √©tale if for each affine `U ‚äÜ Y`
and `V ‚äÜ f ‚Åª¬π' U`, the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is √©tale.

## Main results

- `AlgebraicGeometry.Etale.iff_smoothOfRelativeDimension_zero`: Etale is equivalent to
  smooth of relative dimension `0`.

-/

@[expose] public section

universe t u

universe u‚ÇÇ u‚ÇÅ v‚ÇÇ v‚ÇÅ

open CategoryTheory MorphismProperty Limits

namespace AlgebraicGeometry

/-- A morphism of schemes `f : X ‚ü∂ Y` is √©tale if for each affine `U ‚äÜ Y` and
`V ‚äÜ f ‚Åª¬π' U`, The induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is √©tale. -/
@[mk_iff]
class Etale {X Y : Scheme.{u}} (f : X ‚ü∂ Y) : Prop where
  etale_appLE (f) :
    ‚àÄ {U : Y.Opens} (_ : IsAffineOpen U) {V : X.Opens} (_ : IsAffineOpen V) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      (f.appLE U V e).hom.Etale

alias Scheme.Hom.etale_appLE := Etale.etale_appLE

@[deprecated (since := "2026-02-09")] alias IsEtale := Etale

namespace Etale

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/-- The property of scheme morphisms `Etale` is associated with the ring
homomorphism property `Etale`. -/
instance : HasRingHomProperty @Etale RingHom.Etale where
  isLocal_ringHomProperty := RingHom.Etale.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    rw [etale_iff, affineLocally_iff_forall_isAffineOpen]

/-- Being √©tale is multiplicative. -/
instance : MorphismProperty.IsMultiplicative @Etale :=
  HasRingHomProperty.isMultiplicative RingHom.Etale.stableUnderComposition
    RingHom.Etale.containsIdentities

/-- The composition of √©tale morphisms is √©tale. -/
instance etale_comp {Z : Scheme.{u}} (g : Y ‚ü∂ Z) [Etale f] [Etale g] :
    Etale (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g ‚ÄπEtale f‚Ä∫ ‚ÄπEtale g‚Ä∫

/-- Etale is stable under base change. -/
instance etale_isStableUnderBaseChange : MorphismProperty.IsStableUnderBaseChange @Etale :=
  HasRingHomProperty.isStableUnderBaseChange RingHom.Etale.isStableUnderBaseChange

/-- Open immersions are √©tale. -/
instance (priority := 900) [IsOpenImmersion f] : Etale f :=
  HasRingHomProperty.of_isOpenImmersion RingHom.Etale.containsIdentities

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [Etale g] :
    Etale (pullback.fst f g) :=
  MorphismProperty.pullback_fst f g inferInstance

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [Etale f] :
    Etale (pullback.snd f g) :=
  MorphismProperty.pullback_snd f g inferInstance

instance (f : X ‚ü∂ Y) (V : Y.Opens) [Etale f] : Etale (f ‚à£_ V) :=
  IsZariskiLocalAtTarget.restrict ‚Äπ_‚Ä∫ V

instance (f : X ‚ü∂ Y) (U : X.Opens) (V : Y.Opens) (e) [Etale f] :
    Etale (f.resLE V U e) := by
  delta Scheme.Hom.resLE; infer_instance

lemma eq_smoothOfRelativeDimension_zero : @Etale = @SmoothOfRelativeDimension 0 := by
  apply HasRingHomProperty.ext
  introv
  have : @RingHom.Etale = @RingHom.IsStandardSmoothOfRelativeDimension 0 := by
    ext; apply RingHom.etale_iff_isStandardSmoothOfRelativeDimension_zero
  rw [‚Üê this, RingHom.locally_iff_of_localizationSpanTarget]
  ¬∑ exact RingHom.Etale.respectsIso
  ¬∑ exact RingHom.Etale.ofLocalizationSpanTarget

lemma iff_smoothOfRelativeDimension_zero : Etale f ‚Üî SmoothOfRelativeDimension 0 f := by
  rw [eq_smoothOfRelativeDimension_zero]

instance [Etale f] : SmoothOfRelativeDimension 0 f := by
  rwa [‚Üê iff_smoothOfRelativeDimension_zero]

instance (priority := low) [Etale f] : Smooth f :=
  SmoothOfRelativeDimension.smooth 0 f

open RingHom in
instance (priority := 900) [Etale f] : FormallyUnramified f where
  formallyUnramified_appLE {_} hU {_} hV e :=
    (f.etale_appLE hU hV e).formallyUnramified

instance : MorphismProperty.HasOfPostcompProperty
    @Etale (@LocallyOfFiniteType ‚äì @FormallyUnramified) := by
  rw [MorphismProperty.hasOfPostcompProperty_iff_le_diagonal]
  intro X Y f ‚ü®hft, hfu‚ü©
  exact inferInstanceAs <| Etale (pullback.diagonal f)

/-- If `f ‚â´ g` is √©tale and `g` unramified, then `f` is √©tale. -/
lemma of_comp {Z : Scheme.{u}} (g : Y ‚ü∂ Z) [Etale (f ‚â´ g)] [LocallyOfFiniteType g]
    [FormallyUnramified g] : Etale f :=
  of_postcomp _ (W' := @LocallyOfFiniteType ‚äì @FormallyUnramified) f g ‚ü®‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫‚ü© ‚Äπ_‚Ä∫

instance : MorphismProperty.HasOfPostcompProperty @Etale @Etale := by
  apply MorphismProperty.HasOfPostcompProperty.of_le (W := @Etale)
    (Q := (@LocallyOfFiniteType ‚äì @FormallyUnramified))
  intro X Y f hf
  constructor <;> infer_instance

end Etale

namespace Scheme

/-- The category `Etale X` is the category of schemes √©tale over `X`. -/
protected def Etale (X : Scheme.{u}) : Type _ := MorphismProperty.Over @Etale ‚ä§ X

variable (X : Scheme.{u})

instance : Category X.Etale :=
  inferInstanceAs <| Category (MorphismProperty.Over @Etale ‚ä§ X)

/-- The forgetful functor from schemes √©tale over `X` to schemes over `X`. -/
def Etale.forget : X.Etale ‚•§ Over X :=
  MorphismProperty.Over.forget @Etale ‚ä§ X

/-- The forgetful functor from schemes √©tale over `X` to schemes over `X` is fully faithful. -/
def Etale.forgetFullyFaithful : (Etale.forget X).FullyFaithful :=
  MorphismProperty.Comma.forgetFullyFaithful _ _ _

instance : (Etale.forget X).Full :=
  inferInstanceAs <| (MorphismProperty.Comma.forget _ _ _ _ _).Full
instance : (Etale.forget X).Faithful :=
  inferInstanceAs <| (MorphismProperty.Comma.forget _ _ _ _ _).Faithful

instance : HasPullbacks X.Etale := by
  unfold Scheme.Etale
  infer_instance

variable {X} in
abbrev Etale.mk (Y : Scheme.{u}) [OverClass Y X] [Etale (Y ‚Üò X)] : X.Etale :=
  MorphismProperty.Over.mk _ (Y ‚Üò X) inferInstance

variable {X} in
@[simp]
lemma Etale.forget_mk (Y : Scheme.{u}) [OverClass Y X] [Etale (Y ‚Üò X)] :
    (Etale.forget X).obj (.mk Y) = Over.mk (Y ‚Üò X) := rfl

@[elab_as_elim, cases_eliminator, induction_eliminator]
def Etale.rec {motive : X.Etale ‚Üí Sort*}
    (mk : ‚àÄ (Y : Scheme.{u}) (_ : OverClass Y X) (_ : Etale (Y ‚Üò X)), motive (Etale.mk Y))
    (T : X.Etale) :
    motive T :=
  mk T.left inferInstance T.prop

/-- Let `X` be a scheme. Then, `X` considered as an object of `X.Etale` is
a terminal object. -/
def Etale.isTerminal :
    IsTerminal (MorphismProperty.Over.mk ‚ä§ (ùüô X) inferInstance : X.Etale) :=
  .ofUnique _

instance : HasTerminal X.Etale := (Etale.isTerminal X).hasTerminal

instance : HasFiniteLimits X.Etale :=
  hasFiniteLimits_of_hasTerminal_and_pullbacks

instance : PreservesLimitsOfShape WalkingCospan (Etale.forget X) := by
  sorry

instance : PreservesLimit (Functor.empty.{0} X.Etale) (Etale.forget X) :=
  preservesLimit_of_preserves_limit_cone (Etale.isTerminal X) (by
    refine (IsLimit.equivOfNatIsoOfIso (Functor.emptyExt _ _) _ _ ?_).1
      CostructuredArrow.mkIdTerminal
    exact Cones.ext (Iso.refl _))

instance : PreservesLimitsOfShape (Discrete PEmpty.{1}) (Etale.forget X) := by
  exact preservesLimitsOfShape_pempty_of_preservesTerminal _

instance : PreservesFiniteLimits (Etale.forget X) :=
  preservesFiniteLimits_of_preservesTerminal_and_pullbacks _

end Scheme

end AlgebraicGeometry
