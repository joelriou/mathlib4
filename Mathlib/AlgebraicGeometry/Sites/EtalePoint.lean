/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Jo√´l Riou
-/
module

public import Mathlib.AlgebraicGeometry.Sites.AffineEtale
public import Mathlib.CategoryTheory.Functor.TypeValuedFlat
public import Mathlib.CategoryTheory.Limits.Elements
public import Mathlib.CategoryTheory.Sites.Point.Category

/-!

# Points of the √©tale site

In this file, we show that a morphism `Spec (.of Œ©) ‚ü∂ S` where `Œ©` is
a separably closed field defined a point on the small √©tale site of `S`.

-/

@[expose] public section

universe u

open CategoryTheory Opposite

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}} {Œ© : Type u} [Field Œ©] [IsSepClosed Œ©]
  (s : Spec (.of Œ©) ‚ü∂ S)

instance : IsCofiltered (Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s))).Elements :=
  Functor.isCofiltered_elements _

/-- A morphism `s : Spec (.of Œ©) ‚ü∂ S` where `Œ©` is a separably closed field
defines a point for the small √©tale site of `S`. -/
@[simps -isSimp]
noncomputable def pointSmallEtale : (smallEtaleTopology S).Point where
  fiber := Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s))
  initiallySmall :=
    initiallySmall_of_essentiallySmall_weakly_initial_objectProperty
      (Functor.Elements.precomp (AffineEtale.Spec S)
        (Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s)))).essImage (by
      rintro ‚ü®X, x‚ü©
      induction X with | _ Y f
      obtain ‚ü®y, hy, rfl‚ü© := Over.homMk_surjective x
      dsimp at y hy
      obtain ‚ü®R, j, _, y', rfl‚ü© : ‚àÉ (R : CommRingCat) (j : Spec (.of R) ‚ü∂ Y)
          (_ : IsOpenImmersion j) (y' : _ ‚ü∂ _), y' ‚â´ j = y := by
        obtain ‚ü®R, j, _, hj, _‚ü© := exists_affine_mem_range_and_range_subset
          (x := y.base default) (U := ‚ä§) (by simp)
        refine ‚ü®R, j, inferInstance, _, IsOpenImmersion.lift_fac j y ?_‚ü©
        rintro _ ‚ü®a, rfl‚ü©
        rwa [Subsingleton.elim a default]
      exact ‚ü®_,
        ‚ü®Functor.elementsMk _ (AffineEtale.mk (j ‚â´ f)) (Over.homMk y'), ‚ü®Iso.refl _‚ü©‚ü©,
        ‚ü®‚ü®MorphismProperty.Over.homMk j rfl (by simp), by cat_disch‚ü©‚ü©‚ü©)
  jointly_surjective {X} R hR œÜ := by
    induction X with | _ X f
    obtain ‚ü®œÜ : Spec (.of Œ©) ‚ü∂ X, rfl : œÜ ‚â´ f = s, rfl‚ü© := Over.homMk_surjective œÜ
    obtain ‚ü®ùí∞, h, _, le‚ü© := (mem_smallGrothendieckTopology _ _).1 hR
    obtain ‚ü®‚ü®x, a‚ü©, rfl‚ü© := (Scheme.SpecToEquivOfField Œ© X).symm.surjective œÜ
    obtain ‚ü®i, y, rfl‚ü© := ùí∞.exists_eq x
    have hf : ùí∞.f i ‚â´ f = ùí∞.X i ‚Üò S := (h.isOver_map i).comp_over
    let m := ((ùí∞.f i).residueFieldMap y).hom
    dsimp at m
    algebraize [m, a.hom]
    let b : (ùí∞.X i).residueField y ‚Üí‚Çê[X.residueField (ùí∞.f i y)] Œ© :=
      IsSepClosed.lift
    have fac : Spec.map (CommRingCat.ofHom b.toRingHom) ‚â´
          (ùí∞.X i).fromSpecResidueField y ‚â´ ùí∞.f i =
        (SpecToEquivOfField Œ© X).symm ‚ü®(ùí∞.f i) y, a‚ü© := by
      have : (ùí∞.f i).residueFieldMap y ‚â´ CommRingCat.ofHom b.toRingHom = a := by
        ext1; exact b.comp_algebraMap
      simp [SpecToEquivOfField, ‚Üê this]
    dsimp at fac
    exact ‚ü®(ùí∞.X i).asOverProp S inferInstance,
      MorphismProperty.Over.homMk (ùí∞.f i), le _ ‚ü®i‚ü©,
      Over.homMk (Spec.map (CommRingCat.ofHom b.toRingHom) ‚â´
        (ùí∞.X i).fromSpecResidueField y) (by simp [Etale.forget, ‚Üê fac, hf]), by cat_disch‚ü©

variable {s‚ÇÄ : S} (hs‚ÇÄ : s default = s‚ÇÄ)

@[simps]
def pointSmallEtaleFiberObjToPreimage {X : S.Etale}
    (t : (pointSmallEtale s).fiber.obj X) :
    X.hom ‚Åª¬π' {s‚ÇÄ} :=
  ‚ü®t.left (default : Spec (.of Œ©)), by
    have := Over.w t
    dsimp at this
    rw [‚Üê this] at hs‚ÇÄ
    simpa‚ü©

lemma pointSmallEtaleFiberObjToPreimage_surjective (X : S.Etale) :
    Function.Surjective
      (pointSmallEtaleFiberObjToPreimage s hs‚ÇÄ (X := X)) := sorry

-- The following will have to wait for #35175
variable {Œπ : Type*} {S : Scheme.{u}}
  {Œ© : Œπ ‚Üí Type u} [‚àÄ i, Field (Œ© i)] [‚àÄ i, IsSepClosed (Œ© i)]
  (s : ‚àÄ i, Spec (.of (Œ© i)) ‚ü∂ S)
  (hs : ‚ãÉ i, Set.range (s i) = .univ)

include hs in
lemma isConservative_aux {X : S.Etale} {Œ± : Type*} {Y : Œ± ‚Üí S.Etale} (f : ‚àÄ a, Y a ‚ü∂ X)
    (hf : ‚àÄ (i : Œπ) (x : (pointSmallEtale (s i)).fiber.obj X),
      ‚àÉ (a : Œ±) (y : (pointSmallEtale (s i)).fiber.obj (Y a)),
        (pointSmallEtale (s i)).fiber.map (f a) y = x) :
    Sieve.ofArrows _ f ‚àà smallEtaleTopology _ _ := by
  rw [ofArrows_mem_smallEtaleTopology_iff]
  ext x
  simp only [Set.mem_iUnion, Set.mem_range, Set.mem_univ, iff_true]
  obtain ‚ü®i, hi‚ü© : ‚àÉ i, s i default = X.hom x := by
    have := Set.mem_univ (X.hom x)
    simp only [‚Üê hs, Functor.const_obj_obj, Functor.id_obj, Set.mem_iUnion,
      Set.mem_range] at this
    obtain ‚ü®i, y, hy‚ü© := this
    obtain rfl := Subsingleton.elim y default
    exact ‚ü®i, hy‚ü©
  obtain ‚ü®x', hx'‚ü© :=pointSmallEtaleFiberObjToPreimage_surjective (s i) hi X ‚ü®x, by simp‚ü©
  rw [Subtype.ext_iff] at hx'
  dsimp at hx'
  obtain ‚ü®a, y, hy‚ü© := hf i x'
  exact ‚ü®a, (pointSmallEtaleFiberObjToPreimage (s i) hi y).1, by aesop‚ü©

end AlgebraicGeometry.Scheme
