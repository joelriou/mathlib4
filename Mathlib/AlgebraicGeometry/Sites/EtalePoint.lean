/-
Copyright (c) 2026 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, JoÃ«l Riou
-/
module

public import Mathlib.AlgebraicGeometry.Sites.AffineEtale
public import Mathlib.CategoryTheory.Functor.TypeValuedFlat
public import Mathlib.CategoryTheory.Limits.Elements
public import Mathlib.CategoryTheory.Sites.Point.Basic

/-!

# Points of the Ã©tale site

In this file, we show that a morphism `Spec (.of Î©) âŸ¶ S` where `Î©` is
a separably closed field defined a point on the small Ã©tale site of `S`.

-/

@[expose] public section

universe u

open CategoryTheory Opposite

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}} {Î© : Type u} [Field Î©] [IsSepClosed Î©]
  (s : Spec (.of Î©) âŸ¶ S)

instance : IsCofiltered (Etale.forget S â‹™ coyoneda.obj (op (Over.mk s))).Elements :=
  Functor.isCofiltered_elements _

/-- A morphism `s : Spec (.of Î©) âŸ¶ S` where `Î©` is a separably closed field
defines a point for the small Ã©tale site of `S`. -/
noncomputable def pointSmallEtale : (smallEtaleTopology S).Point where
  fiber := Etale.forget S â‹™ coyoneda.obj (op (Over.mk s))
  initiallySmall :=
    initiallySmall_of_essentiallySmall_weakly_initial_objectProperty
      (Functor.Elements.precomp (AffineEtale.Spec S)
        (Etale.forget S â‹™ coyoneda.obj (op (Over.mk s)))).essImage (by
      rintro âŸ¨X, xâŸ©
      induction X with | _ Y f
      obtain âŸ¨y, hy, rflâŸ© := Over.homMk_surjective x
      dsimp at y hy
      obtain âŸ¨R, j, _, y', rflâŸ© : âˆƒ (R : CommRingCat) (j : Spec (.of R) âŸ¶ Y)
          (_ : IsOpenImmersion j) (y' : _ âŸ¶ _), y' â‰« j = y := by
        obtain âŸ¨R, j, _, hj, _âŸ© := exists_affine_mem_range_and_range_subset
          (x := y.base default) (U := âŠ¤) (by simp)
        refine âŸ¨R, j, inferInstance, _, IsOpenImmersion.lift_fac j y ?_âŸ©
        rintro _ âŸ¨a, rflâŸ©
        rwa [Subsingleton.elim a default]
      exact âŸ¨_,
        âŸ¨Functor.elementsMk _ (AffineEtale.mk (j â‰« f)) (Over.homMk y'), âŸ¨Iso.refl _âŸ©âŸ©,
        âŸ¨âŸ¨MorphismProperty.Over.homMk j rfl (by simp), by cat_dischâŸ©âŸ©âŸ©)
  jointly_surjective {X} R hR Ï† := by
    induction X with | _ X f
    obtain âŸ¨Ï† : Spec (.of Î©) âŸ¶ X, rfl : Ï† â‰« f = s, rflâŸ© := Over.homMk_surjective Ï†
    obtain âŸ¨ğ’°, h, _, leâŸ© := (mem_smallGrothendieckTopology _ _).1 hR
    obtain âŸ¨âŸ¨x, aâŸ©, rflâŸ© := (Scheme.SpecToEquivOfField Î© X).symm.surjective Ï†
    obtain âŸ¨i, y, rflâŸ© := ğ’°.exists_eq x
    have hf : ğ’°.f i â‰« f = ğ’°.X i â†˜ S := (h.isOver_map i).comp_over
    let m := ((ğ’°.f i).residueFieldMap y).hom
    dsimp at m
    algebraize [m, a.hom]
    let b : (ğ’°.X i).residueField y â†’â‚[X.residueField (ğ’°.f i y)] Î© :=
      IsSepClosed.lift
    have fac : Spec.map (CommRingCat.ofHom b.toRingHom) â‰«
          (ğ’°.X i).fromSpecResidueField y â‰« ğ’°.f i =
        (SpecToEquivOfField Î© X).symm âŸ¨(ğ’°.f i) y, aâŸ© := by
      have : (ğ’°.f i).residueFieldMap y â‰« CommRingCat.ofHom b.toRingHom = a := by
        ext1; exact b.comp_algebraMap
      simp [SpecToEquivOfField, â† this]
    dsimp at fac
    exact âŸ¨(ğ’°.X i).asOverProp S inferInstance,
      MorphismProperty.Over.homMk (ğ’°.f i), le _ âŸ¨iâŸ©,
      Over.homMk (Spec.map (CommRingCat.ofHom b.toRingHom) â‰«
        (ğ’°.X i).fromSpecResidueField y) (by simp [Etale.forget, â† fac, hf]), by cat_dischâŸ©

end AlgebraicGeometry.Scheme
