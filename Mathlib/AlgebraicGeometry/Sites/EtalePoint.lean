/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Jo√´l Riou
-/
module

public import Mathlib.AlgebraicGeometry.Fiber
public import Mathlib.AlgebraicGeometry.Sites.AffineEtale
public import Mathlib.CategoryTheory.Functor.TypeValuedFlat
public import Mathlib.CategoryTheory.Limits.Elements
public import Mathlib.CategoryTheory.Sites.Point.Conservative

public import Mathlib.FieldTheory.SeparableClosure

/-!

# Points of the √©tale site

In this file, we show that a morphism `Spec (.of Œ©) ‚ü∂ S` where `Œ©` is
a separably closed field defined a point on the small √©tale site of `S`.
We show that these points form a conservative family.

-/

@[expose] public section

universe u

open CategoryTheory Opposite

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}} {Œ© : Type u} [Field Œ©] [IsSepClosed Œ©]
  (s : Spec (.of Œ©) ‚ü∂ S)

lemma exists_fac_of_etale_of_isSepClosed {X S : Scheme.{u}} (f : X ‚ü∂ S) [Etale f]
    {Œ© : Type u} [Field Œ©] [IsSepClosed Œ©] (s : Spec (.of Œ©) ‚ü∂ S)
    (x : X) (hx : f x = s default) :
    ‚àÉ (l : Spec (.of Œ©) ‚ü∂ X), l ‚â´ f = s ‚àß l default = x := by
  obtain ‚ü®‚ü®s, a‚ü©, rfl‚ü© := (SpecToEquivOfField Œ© S).symm.surjective s
  obtain rfl : f x = s := by simp [hx, SpecToEquivOfField]
  let m := (f.residueFieldMap x).hom
  dsimp at m
  algebraize [m, a.hom]
  let b : X.residueField x ‚Üí‚Çê[S.residueField (f x)] Œ© :=
    IsSepClosed.lift
  have : f.residueFieldMap x ‚â´ CommRingCat.ofHom b.toRingHom = a := by
    ext1; exact b.comp_algebraMap
  refine ‚ü®Spec.map (CommRingCat.ofHom b.toRingHom) ‚â´ X.fromSpecResidueField x, ?_, ?_‚ü©
  ¬∑ simp [SpecToEquivOfField, ‚Üê this]
  ¬∑ dsimp
    apply fromSpecResidueField_apply

instance : IsCofiltered (Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s))).Elements :=
  Functor.isCofiltered_elements _

/-- A morphism `s : Spec (.of Œ©) ‚ü∂ S` where `Œ©` is a separably closed field
defines a point for the small √©tale site of `S`. -/
@[simps]
noncomputable def pointSmallEtale : (smallEtaleTopology S).Point where
  fiber := Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s))
  initiallySmall :=
    initiallySmall_of_essentiallySmall_weakly_initial_objectProperty
      (Functor.Elements.precomp (AffineEtale.Spec S)
        (Etale.forget S ‚ãô coyoneda.obj (op (Over.mk s)))).essImage (by
      rintro ‚ü®X, x‚ü©
      induction X with | _ Y f
      obtain ‚ü®y, hy, rfl‚ü© := Over.homMk_surjective x
      dsimp at y hy
      obtain ‚ü®R, j, _, y', rfl‚ü© : ‚àÉ (R : CommRingCat) (j : Spec (.of R) ‚ü∂ Y)
          (_ : IsOpenImmersion j) (y' : _ ‚ü∂ _), y' ‚â´ j = y := by
        obtain ‚ü®R, j, _, hj, _‚ü© := exists_affine_mem_range_and_range_subset
          (x := y.base default) (U := ‚ä§) (by simp)
        refine ‚ü®R, j, inferInstance, _, IsOpenImmersion.lift_fac j y ?_‚ü©
        rintro _ ‚ü®a, rfl‚ü©
        rwa [Subsingleton.elim a default]
      exact ‚ü®_,
        ‚ü®Functor.elementsMk _ (AffineEtale.mk (j ‚â´ f)) (Over.homMk y'), ‚ü®Iso.refl _‚ü©‚ü©,
        ‚ü®‚ü®MorphismProperty.Over.homMk j rfl (by simp), by cat_disch‚ü©‚ü©‚ü©)
  jointly_surjective {X} R hR œÜ := by
    induction X with | _ X f
    obtain ‚ü®œÜ : Spec (.of Œ©) ‚ü∂ X, rfl : œÜ ‚â´ f = s, rfl‚ü© := Over.homMk_surjective œÜ
    obtain ‚ü®ùí∞, h, _, le‚ü© := (mem_smallGrothendieckTopology _ _).1 hR
    obtain ‚ü®i, y, hy‚ü© := ùí∞.exists_eq (œÜ default)
    obtain ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü© := exists_fac_of_etale_of_isSepClosed (ùí∞.f i) œÜ _ hy
    have : ùí∞.f i ‚â´ f = ùí∞.X i ‚Üò S := HomIsOver.comp_over (f := ùí∞.f i) (S := S)
    exact ‚ü®(ùí∞.X i).asOverProp S inferInstance,
      MorphismProperty.Over.homMk (ùí∞.f i), le _ ‚ü®i‚ü©, Over.homMk l, by cat_disch‚ü©

variable {s‚ÇÄ : S} (hs‚ÇÄ : s default = s‚ÇÄ)

@[simps]
def pointSmallEtaleFiberObjToPreimage {X : S.Etale}
    (t : (pointSmallEtale s).fiber.obj X) :
    X.hom ‚Åª¬π' {s‚ÇÄ} :=
  ‚ü®t.left (default : Spec (.of Œ©)), by
    have := Over.w t
    dsimp at this
    rw [‚Üê this] at hs‚ÇÄ
    simpa‚ü©

instance {Y X : Scheme.{u}} (f : Y ‚ü∂ X) [Etale f] (x : X) :
    Etale (f.fiberToSpecResidueField x) := by
  dsimp [Hom.fiberToSpecResidueField]
  infer_instance

lemma pointSmallEtaleFiberObjToPreimage_surjective (X : S.Etale) :
    Function.Surjective (pointSmallEtaleFiberObjToPreimage s hs‚ÇÄ (X := X)) := by
  intro y
  obtain ‚ü®y, rfl‚ü© := (X.hom.fiberHomeo s‚ÇÄ).surjective y
  obtain ‚ü®‚ü®t, a‚ü©, rfl‚ü© := (Scheme.SpecToEquivOfField Œ© _).symm.surjective s
  obtain rfl : t = s‚ÇÄ := by simp [SpecToEquivOfField, ‚Üê hs‚ÇÄ]
  obtain ‚ü®l, hl, rfl‚ü© := exists_fac_of_etale_of_isSepClosed
    (X.hom.fiberToSpecResidueField _) (Spec.map a) y (by subsingleton)
  refine ‚ü®Over.homMk (l ‚â´ X.hom.fiberŒπ t) ?_, rfl‚ü©
  dsimp
  rw [Category.assoc, X.hom.fiber_fac, reassoc_of% hl]
  cat_disch

lemma isConservative_pointSmallEtale
    {Œπ : Type*} {S : Scheme.{u}}
    {Œ© : Œπ ‚Üí Type u} [‚àÄ i, Field (Œ© i)] [‚àÄ i, IsSepClosed (Œ© i)]
    (s : ‚àÄ i, Spec (.of (Œ© i)) ‚ü∂ S)
    (hs : ‚ãÉ i, Set.range (s i) = .univ) :
    (ObjectProperty.ofObj (fun i ‚Ü¶ pointSmallEtale (s i))).IsConservativeFamilyOfPoints :=
  .mk' (fun X R hR ‚Ü¶ by
    obtain ‚ü®Œ±, T, f, rfl‚ü© := R.exists_eq_ofArrows
    rw [ofArrows_mem_smallEtaleTopology_iff]
    ext x
    simp only [Set.mem_iUnion, Set.mem_range, Set.mem_univ, iff_true]
    obtain ‚ü®i, hi‚ü© : ‚àÉ i, s i default = X.hom x := by
      have := Set.mem_univ (X.hom x)
      simp only [‚Üê hs, Functor.const_obj_obj, Functor.id_obj, Set.mem_iUnion,
        Set.mem_range] at this
      obtain ‚ü®i, y, hy‚ü© := this
      obtain rfl := Subsingleton.elim y default
      exact ‚ü®i, hy‚ü©
    obtain ‚ü®x', hx'‚ü© := pointSmallEtaleFiberObjToPreimage_surjective (s i) hi X ‚ü®x, by simp‚ü©
    rw [Subtype.ext_iff] at hx'
    dsimp at hx'
    subst hx'
    obtain ‚ü®W, g, ‚ü®Z, p, _, ‚ü®a‚ü©, rfl‚ü©, y, rfl‚ü© := hR ‚ü®_, ‚ü®i‚ü©‚ü© x'
    exact ‚ü®a, (pointSmallEtaleFiberObjToPreimage (s i) hi (y ‚â´ p.hom)).1, rfl‚ü©)

lemma isConservative_pointSmallEtale' (S : Scheme.{u}) :
    (ObjectProperty.ofObj (fun (s : S) ‚Ü¶ pointSmallEtale
      ((SpecToEquivOfField (SeparableClosure (S.residueField s)) _).2
        ‚ü®s, CommRingCat.ofHom
          (algebraMap (S.residueField s) _)‚ü©))).IsConservativeFamilyOfPoints :=
  isConservative_pointSmallEtale _ (by
    ext s
    simp only [Equiv.invFun_as_coe, Set.mem_iUnion, Set.mem_range, Set.mem_univ, iff_true]
    exact ‚ü®s, default, by simp [SpecToEquivOfField]‚ü©)

instance : GrothendieckTopology.HasEnoughPoints.{u} (smallEtaleTopology S) where
  exists_objectProperty :=
    ‚ü®_, inferInstance, isConservative_pointSmallEtale' S‚ü©

end AlgebraicGeometry.Scheme
