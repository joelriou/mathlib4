/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Etale
public import Mathlib.AlgebraicGeometry.Sites.BigZariski
public import Mathlib.AlgebraicGeometry.Sites.Small
public import Mathlib.CategoryTheory.Functor.TypeValuedFlat
public import Mathlib.CategoryTheory.Limits.Elements
public import Mathlib.CategoryTheory.Sites.Point.Basic

/-!

# The √©tale site

In this file we define the big √©tale site, i.e. the √©tale topology as a Grothendieck topology
on the category of schemes.

-/

@[expose] public section

universe v u

open CategoryTheory MorphismProperty Limits Opposite

namespace AlgebraicGeometry.Scheme

/-- Big √©tale site: the √©tale precoverage on the category of schemes. -/
def etalePrecoverage : Precoverage Scheme.{u} :=
  precoverage @Etale

/-- Big √©tale site: the √©tale pretopology on the category of schemes. -/
def etalePretopology : Pretopology Scheme.{u} :=
  pretopology @Etale

/-- Big √©tale site: the √©tale topology on the category of schemes. -/
abbrev etaleTopology : GrothendieckTopology Scheme.{u} :=
  grothendieckTopology @Etale

lemma zariskiTopology_le_etaleTopology : zariskiTopology ‚â§ etaleTopology := by
  apply grothendieckTopology_monotone
  intro X Y f hf
  infer_instance

/-- The small √©tale site of a scheme is the Grothendieck topology on the
category of schemes √©tale over `X` induced from the √©tale topology on `Scheme.{u}`. -/
def smallEtaleTopology (X : Scheme.{u}) : GrothendieckTopology X.Etale :=
  X.smallGrothendieckTopology (P := @Etale)

/-- The pretopology generating the small √©tale site. -/
def smallEtalePretopology (X : Scheme.{u}) : Pretopology X.Etale :=
  X.smallPretopology (Q := @Etale) (P := @Etale)

lemma ofArrows_mem_smallEtaleTopology_iff
    {X : Scheme.{u}} {W : X.Etale} {Œπ : Type*}
    {Z : Œπ ‚Üí X.Etale} (f : ‚àÄ i, Z i ‚ü∂ W) :
    Sieve.ofArrows _ f ‚àà smallEtaleTopology _ _ ‚Üî
      ‚ãÉ i, Set.range (f i).left = .univ := by
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ (mem_smallGrothendieckTopology _ _).2 ?_‚ü©
  ¬∑ obtain ‚ü®U, _, _, hU‚ü© := (mem_smallGrothendieckTopology _ _).1 hf
    ext y
    simp only [Set.mem_iUnion, Set.mem_range, Set.mem_univ, iff_true]
    obtain ‚ü®i, ‚ü®u, rfl‚ü©‚ü© := ((ofArrows_mem_precoverage_iff _).1 U.mem‚ÇÄ).1 y
    obtain ‚ü®_, b, _, ‚ü®j‚ü©, fac‚ü© := hU _ ‚ü®i‚ü©
    replace fac : b.left ‚â´ (f j).left = U.f i :=
      (Etale.forget _ ‚ãô CategoryTheory.Over.forget _).congr_map fac
    exact ‚ü®j, b.left u, by simp [‚Üê fac]‚ü©
  ¬∑ have (w : W.left) : ‚àÉ (i : Œπ), w ‚àà Set.range (f i).left := by
      have := Set.mem_univ w
      simp [‚Üê hf] at this
      tauto
    choose i z hz using this
    let V : Cover (precoverage @Etale) W.left :=
      Cover.mkOfCovers W.left (fun w ‚Ü¶ (Z (i w)).left)
        (fun w ‚Ü¶ (f (i w)).left) (fun w ‚Ü¶ ‚ü®_, _, hz w‚ü©) inferInstance
    letI : Cover.Over X V :=
      { over w := ‚ü®(Z (i w)).hom‚ü©
        isOver_map w := by cat_disch }
    have (w : W.left) : Etale (V.X w ‚Üò X) :=
      inferInstanceAs (Etale (Z (i w)).hom)
    refine ‚ü®V, inferInstance, inferInstance, ?_‚ü©
    rintro _ _ ‚ü®w‚ü©
    refine ‚ü®_, ùüô _, _, ‚ü®i w‚ü©, by cat_disch‚ü©

instance {S : Scheme.{u}} (ùí∞ : S.Cover (precoverage @Etale)) (i : ùí∞.I‚ÇÄ) : Etale (ùí∞.f i) :=
  ùí∞.map_prop i

end AlgebraicGeometry.Scheme
