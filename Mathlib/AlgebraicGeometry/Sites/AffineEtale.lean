/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Category.Grp.Abelian
public import Mathlib.AlgebraicGeometry.Sites.Etale
public import Mathlib.CategoryTheory.Sites.Abelian
public import Mathlib.CategoryTheory.Sites.DenseSubsite.OneHypercoverDense

/-!
# Affine Ã©tale site

In this file we define the small affine Ã©tale site of a scheme `S`. The underlying
category is the category of commutative rings `R` equipped with an Ã©tale structure
morphism `Spec R âŸ¶ S`.

## Main results
- `AlgebraicGeometry.Scheme.AffineEtale.sheafEquiv`: The category of sheaves on the
  small affine Ã©tale site is equivalent to the category of schemes on the small Ã©tale site.
-/

@[expose] public section

universe u v u'

open CategoryTheory Opposite Limits MorphismProperty

-- to be moved
/-- The equivalence of rings between two equals subrings. -/
@[simps!]
def Subring.equivOfEq {R : Type u} [Ring R] {s t : Subring R} (h : s = t) :
    s â‰ƒ+* t where
  toEquiv := (Equiv.refl _).subtypeEquiv (by simp [h])
  map_mul' := by simp
  map_add' := by simp

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}}

section

/-- Construct an object of affine `P`-schemes over `S` by giving a morphism `Spec R âŸ¶ S`. -/
@[simps! hom left]
def affineOverMk {P : MorphismProperty Scheme.{u}} {R : CommRingCat.{u}}
    (f : Spec R âŸ¶ S) (hf : P f) :
    P.CostructuredArrow âŠ¤ Scheme.Spec S :=
  .mk âŠ¤ f hf

/-- The `Spec` functor from affine `P`-schemes over `S` to `P`-schemes over `S` is dense
if `P` is local at the source. -/
instance isCoverDense_toOver_Spec (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsZariskiLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    (CostructuredArrow.toOver P Scheme.Spec S).IsCoverDense
      (smallGrothendieckTopology P) where
  is_cover U := by
    rw [Scheme.mem_smallGrothendieckTopology]
    let ğ’° : Cover.{u} (precoverage P) U.left :=
      U.left.affineCover.changeProp
      (fun _ â†¦ IsZariskiLocalAtSource.of_isOpenImmersion _)
    let _ (i : ğ’°.Iâ‚€) : (ğ’°.X i).Over S := âŸ¨ğ’°.f i â‰« U.homâŸ©
    refine âŸ¨ğ’°, âŸ¨fun i â†¦ inferInstance, fun i â†¦ âŸ¨rflâŸ©âŸ©, ?_, ?_âŸ©
    Â· intro i
      exact P.comp_mem _ _ (ğ’°.map_prop i) U.prop
    Â· rintro X f âŸ¨iâŸ©
      rw [Sieve.coverByImage]
      refine âŸ¨âŸ¨affineOverMk (ğ’°.f i â‰« U.hom) (P.comp_mem _ _ (ğ’°.map_prop i) U.prop), ?_, ?_, ?_âŸ©âŸ©
      Â· exact CostructuredArrow.homMk (ğŸ™ _) âŸ¨âŸ© rfl
      Â· exact Over.homMk (ğ’°.f i) (by simp) trivial
      Â· ext
        simp

instance isOneHypercoverDense_toOver_Spec
    (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsZariskiLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    Functor.IsOneHypercoverDense.{u} (CostructuredArrow.toOver P Scheme.Spec S)
    ((CostructuredArrow.toOver P Scheme.Spec S).inducedTopology (smallGrothendieckTopology P))
    (smallGrothendieckTopology P) :=
  Functor.IsOneHypercoverDense.of_hasPullbacks (fun X â†¦ by
    let ğ’° := affineOpenCover X.left
    refine âŸ¨ğ’°.Iâ‚€, fun i â†¦ affineOverMk (ğ’°.f i â‰« X.hom)
      (P.comp_mem _ _ (IsZariskiLocalAtSource.of_isOpenImmersion (ğ’°.f i)) X.prop),
      fun i â†¦ CostructuredArrow.homMk (ğ’°.f i) (by simp), ?_âŸ©
    rw [Scheme.mem_smallGrothendieckTopology]
    let ğ’± : Cover (precoverage P) X.left :=
      ğ’°.openCover.changeProp (fun _ â†¦ IsZariskiLocalAtSource.of_isOpenImmersion _)
    let _ (i : ğ’±.Iâ‚€) : (ğ’±.X i).Over S := âŸ¨ğ’°.f i â‰« X.homâŸ©
    let : Cover.Over S ğ’± := { isOver_map _ := by cat_disch }
    refine âŸ¨ğ’±, inferInstance, fun i â†¦ P.comp_mem _ _ (ğ’±.map_prop i) X.prop, ?_âŸ©
    rintro _ _ âŸ¨iâŸ©
    exact (Sieve.mem_ofArrows_iff ..).2 âŸ¨i, ğŸ™ _, by cat_dischâŸ©)

variable (S) in
structure FinitelyPresentedOverAffineOpen : Type u where
  U : Opens S
  hU : IsAffineOpen U
  g : â„•
  r : â„•
  rel (x : Fin r) : MvPolynomial (Fin g) Î“(S, U)

namespace FinitelyPresentedOverAffineOpen

variable (P : S.FinitelyPresentedOverAffineOpen)

abbrev R : Type u :=
  MvPolynomial (Fin P.g) Î“(S, P.U) â§¸ Ideal.span (Set.range P.rel)

noncomputable abbrev scheme : Scheme.{u} := Spec (.of P.R)

noncomputable def Ï€ : P.scheme âŸ¶ P.U :=
  Spec.map (CommRingCat.ofHom (algebraMap _ _)) â‰« P.hU.isoSpec.inv

noncomputable def a : P.scheme âŸ¶ S := P.Ï€ â‰« P.U.Î¹

@[reassoc (attr := simp)]
lemma fac : P.Ï€ â‰« P.U.Î¹ = P.a := rfl

lemma exists_nhd {X : Scheme.{u}} (f : X âŸ¶ S) [LocallyOfFinitePresentation f] (x : X) :
    âˆƒ (U : Opens X) (_ : x âˆˆ U) (P : S.FinitelyPresentedOverAffineOpen),
      Nonempty (U.toScheme â‰… P.scheme) := by
  obtain âŸ¨U, V, hx, hUVâŸ© :
      âˆƒ (U : X.affineOpens) (V : S.affineOpens), x âˆˆ U.val âˆ§ U â‰¤ f.base â»Â¹' V := by
    obtain âŸ¨U, hâ‚, hâ‚‚, _âŸ© := exists_isAffineOpen_mem_and_subset (x := f.base x) (U := âŠ¤) (by simp)
    obtain âŸ¨V, hâ‚ƒ, hâ‚„, hâ‚…âŸ© := exists_isAffineOpen_mem_and_subset (x := x)
      (U := âŸ¨_, IsOpen.preimage f.continuous U.2âŸ©) (by simpa)
    exact âŸ¨âŸ¨V, hâ‚ƒâŸ©, âŸ¨U, hâ‚âŸ©, hâ‚„, hâ‚…âŸ©
  letI := (f.appLE V U hUV).hom.toAlgebra
  obtain âŸ¨n, Ï†, hâ‚, hâ‚‚âŸ© :=
    (LocallyOfFinitePresentation.finitePresentation_appLE f V.prop U.prop hUV).out
  obtain âŸ¨r, Ï, hÏâŸ© : âˆƒ (r : â„•) (Î³ : Fin r â†’ MvPolynomial (Fin n) Î“(S, V)),
      Ideal.span (Set.range Î³) = RingHom.ker Ï†.toRingHom := by
    obtain âŸ¨s, hsâŸ© := hâ‚‚
    exact âŸ¨s.card, Subtype.val âˆ˜ s.equivFin.symm, by rw [â† hs]; simpâŸ©
  let P : S.FinitelyPresentedOverAffineOpen :=
    { U := V.1
      hU := V.prop
      g := n
      r := r
      rel := Ï }
  let e : P.R â‰ƒ+* Î“(X, U.1) :=
    (Ideal.quotEquivOfEq hÏ).trans (Ï†.toRingHom.quotientKerEquivRange.trans
      ((Subring.equivOfEq (RingHom.range_eq_top_of_surjective _ hâ‚)).trans Subring.topEquiv))
  exact âŸ¨U, hx, P, âŸ¨asIso (toSpecÎ“ U) â‰ªâ‰« Scheme.Spec.mapIso U.1.topIso.op.symm â‰ªâ‰«
    Scheme.Spec.mapIso e.toCommRingCatIso.opâŸ©âŸ©

lemma exists_subring
    {A : CommRingCat.{u}} (f : Spec (.of A) âŸ¶ S) [LocallyOfFinitePresentation f] :
    âˆƒ (n : â„•) (P : Fin n â†’ S.FinitelyPresentedOverAffineOpen)
      (Râ‚€ : Subring (âˆ€ i, (P i).R)), Nonempty (A â‰… CommRingCat.of Râ‚€) := by
  choose U hU P e using exists_nhd f
  let iso (x) := (e x).some
  obtain âŸ¨n, Î±, hÎ±âŸ© : âˆƒ (n : â„•) (Î± : Fin n â†’ Spec (.of A)),
    â‹ƒ (i : Fin n), (U (Î± i) : Set (Spec (.of A))) = Set.univ := by
      obtain âŸ¨s, hsâŸ© := CompactSpace.isCompact_univ.elim_finite_subcover _
        (fun x â†¦ (U x).isOpen) (fun x _ â†¦ Set.mem_iUnion_of_mem x (hU x))
      refine âŸ¨s.card, Subtype.val âˆ˜ (Finset.equivFin s).symm,
        subset_antisymm (by simp) (hs.trans ?_)âŸ©
      simp only [Function.comp_apply, Set.iUnion_subset_iff]
      exact fun i hi _ _ â†¦ Set.mem_iUnion_of_mem ((Finset.equivFin s) âŸ¨i, hiâŸ©) (by simpa)
  have (i : Fin n) := (U (Î± i)).Î¹
  let Î² (i : Fin n) : A â†’+* ((P âˆ˜ Î±) i).R := (Spec.preimage ((iso (Î± i)).inv â‰« (U (Î± i)).Î¹)).hom
  let Ï† : A â†’+* âˆ€ i, ((P âˆ˜ Î±) i).R :=
    { toFun a i := Î² i a
      map_zero' := by ext; simp
      map_add' _ _ := by ext; simp
      map_one' := by ext; simp
      map_mul' _ _ := by ext; simp }
  have hÏ† : Function.Injective Ï† := by
    suffices âˆ€ a, Ï† a = 0 â†’ a = 0 from
      fun a b h â†¦ by
        rw [â† sub_eq_zero] at h âŠ¢
        exact this _ (by simpa)
    intro a ha
    replace ha (i : Fin n) : Î² i a = 0 := congr_fun ha i
    obtain âŸ¨a, rflâŸ© := (Î“SpecIso A).commRingCatIsoToRingEquiv.surjective a
    simp only [EmbeddingLike.map_eq_zero_iff]
    refine (openCoverOfIsOpenCover _ (U âˆ˜ Î±) (.mk (by aesop))).ext_elem _ _ (fun i â†¦ ?_)
    dsimp at i âŠ¢
    replace ha : (Î“SpecIso _).hom (((iso (Î± i)).inv â‰« (U (Î± i)).Î¹).appTop a) = 0 := by
      simpa [â† ha] using (ConcreteCategory.congr_hom (Î“SpecIso_naturality
        (Spec.preimage ((iso (Î± i)).inv â‰« (U (Î± i)).Î¹))) a)
    apply (asIso (iso (Î± i)).inv.appTop â‰ªâ‰«
      Î“SpecIso (.of (P (Î± i)).R)).commRingCatIsoToRingEquiv.injective
    simpa [-EmbeddingLike.map_eq_zero_iff] using ha
  exact âŸ¨n, P âˆ˜ Î±, RingHom.range Ï†, âŸ¨RingEquiv.toCommRingCatIso
    (RingEquiv.ofBijective Ï†.rangeRestrict
      âŸ¨(Function.Injective.of_comp_iff Subtype.val_injective _).1 hÏ†,
        RingHom.rangeRestrict_surjective Ï†âŸ©)âŸ©âŸ©

end FinitelyPresentedOverAffineOpen

lemma essentiallySmall_costructuredArrow_Spec
    (P : MorphismProperty Scheme.{u}) (hP : P â‰¤ @LocallyOfFinitePresentation) [P.RespectsIso] :
    EssentiallySmall.{u} (P.CostructuredArrow âŠ¤ Scheme.Spec S) := by
  suffices âˆƒ (Î¹ : Type u) (R : Î¹ â†’ CommRingCat.{u}),
      âˆ€ (Z : P.CostructuredArrow âŠ¤ Scheme.Spec S),
        âˆƒ (i : Î¹), Nonempty (R i â‰… Z.left.unop) by
    rw [essentiallySmall_iff_objectPropertyEssentiallySmall_top]
    obtain âŸ¨Î¹, R, hRâŸ© := this
    let Pâ‚€ : ObjectProperty (P.CostructuredArrow âŠ¤ Scheme.Spec S) :=
      .ofObj (fun (t : Î£ (i : Î¹) (f : Scheme.Spec.obj (Opposite.op (R i)) âŸ¶ S), PLift (P f)) â†¦
        .mk (A := op (R t.1)) _ t.2.1 t.2.2.down)
    refine ObjectProperty.EssentiallySmall.of_le (Q := Pâ‚€.isoClosure) (fun Z _ â†¦ ?_)
    obtain âŸ¨i, âŸ¨eâŸ©âŸ© := hR Z
    refine âŸ¨_, âŸ¨i, Spec.map e.inv â‰« Z.hom, âŸ¨RespectsIso.precomp _ _ _ Z.propâŸ©âŸ©, âŸ¨?_âŸ©âŸ©
    exact MorphismProperty.CostructuredArrow.isoMk e.op (by simp) (by simp)
      (by simp [â† Spec.map_comp_assoc, e.inv_hom_id])
  refine âŸ¨Î£ (n : â„•) (P : Fin n â†’ S.FinitelyPresentedOverAffineOpen), Subring (âˆ€ i, (P i).R),
    fun âŸ¨n, P, Râ‚€âŸ© â†¦ .of Râ‚€, fun Z â†¦ ?_âŸ©
  have : LocallyOfFinitePresentation Z.hom := hP _ Z.prop
  obtain âŸ¨n, P, Râ‚€, âŸ¨eâŸ©âŸ© := FinitelyPresentedOverAffineOpen.exists_subring Z.hom
  exact âŸ¨âŸ¨n, P, Râ‚€âŸ©, âŸ¨e.symmâŸ©âŸ©

variable {P : MorphismProperty Scheme.{u}} [IsZariskiLocalAtSource P]

instance IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete
    {Î¹ : Type*} [Small.{u} Î¹] {C : Type*} [Category C] [HasColimitsOfShape (Discrete Î¹) C]
    (L : C â¥¤ Scheme.{u}) [PreservesColimitsOfShape (Discrete Î¹) L] (X : Scheme.{u}) :
    (P.costructuredArrowObj L (X := X)).IsClosedUnderColimitsOfShape (Discrete Î¹) := by
  refine CostructuredArrow.isClosedUnderColimitsOfShape ?_ ?_ ?_ _
  Â· intro D _
    exact Sigma.cocone _
  Â· intro D
    exact coproductIsCoproduct' _
  Â· intro D _ X s h
    exact IsZariskiLocalAtSource.sigmaDesc (h âŸ¨Â·âŸ©)

variable [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] [P.IsMultiplicative]

instance : HasFiniteCoproducts (P.CostructuredArrow âŠ¤ Scheme.Spec S) where
  out n := by
    have : (MorphismProperty.commaObj Scheme.Spec (.fromPUnit S) P).IsClosedUnderColimitsOfShape
        (Discrete (Fin n)) :=
      IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete _ _
    apply MorphismProperty.Comma.hasColimitsOfShape_of_closedUnderColimitsOfShape

end

/-- The small affine Ã©tale site: The category of affine schemes Ã©tale over `S`, whose objects are
commutative rings `R` with an Ã©tale structure morphism `Spec R âŸ¶ S`. -/
def AffineEtale (S : Scheme.{u}) : Type (u + 1) :=
  MorphismProperty.CostructuredArrow @Etale.{u} âŠ¤ Scheme.Spec.{u} S

namespace AffineEtale

/-- Construct an object of the small affine Ã©tale site. -/
@[simps!]
protected def mk {R : CommRingCat.{u}} (f : Spec R âŸ¶ S) [Etale f] : AffineEtale S :=
  MorphismProperty.CostructuredArrow.mk âŠ¤ f â€¹_â€º

instance : Category S.AffineEtale :=
  inferInstanceAs <| Category (MorphismProperty.CostructuredArrow _ _ _ _)

/-- The `Spec` functor from the small affine Ã©tale site of `S` to the small Ã©tale site of `S`. -/
@[simps! obj_left obj_hom map_left]
protected noncomputable def Spec (S : Scheme.{u}) : S.AffineEtale â¥¤ S.Etale :=
  MorphismProperty.CostructuredArrow.toOver _ _ _

instance : (AffineEtale.Spec S).Faithful :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Faithful

instance : (AffineEtale.Spec S).Full :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Full

instance : (AffineEtale.Spec S).IsCoverDense S.smallEtaleTopology :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).IsCoverDense
    (smallGrothendieckTopology _)

instance : HasPullbacks S.AffineEtale :=
  inferInstanceAs <| HasPullbacks (MorphismProperty.CostructuredArrow _ _ _ _)

variable (S) in
/-- The topology on the small affine Ã©tale site is the topology induced by `Spec` from
the small Ã©tale site. -/
def topology : GrothendieckTopology S.AffineEtale :=
  (AffineEtale.Spec S).inducedTopology (smallEtaleTopology S)

instance : Functor.IsDenseSubsite (topology S) (S.smallEtaleTopology) (AffineEtale.Spec S) := by
  dsimp [topology]
  infer_instance

instance : Functor.IsOneHypercoverDense.{u} (AffineEtale.Spec S)
    (topology S) (S.smallEtaleTopology) :=
  isOneHypercoverDense_toOver_Spec _

instance : EssentiallySmall.{u} S.AffineEtale :=
  essentiallySmall_costructuredArrow_Spec _ (fun _ _ _ _ â†¦ inferInstance)

section

variable {A : Type u'} [Category.{u} A]
  {FA : A â†’ A â†’ Type*} {CD : A â†’ Type u}
  [âˆ€ X Y, FunLike (FA X Y) (CD X) (CD Y)] [ConcreteCategory.{u} A FA]
  [PreservesLimits (CategoryTheory.forget A)] [HasColimits A] [HasLimits A]
  [(CategoryTheory.forget A).ReflectsIsomorphisms]
  [PreservesFilteredColimitsOfSize.{u, u} (CategoryTheory.forget A)]

instance : HasSheafify (topology S) A := hasSheafifyEssentiallySmallSite.{u} _ _

example : HasSheafify (topology S) (Type u) := by
  infer_instance

example : Abelian (Sheaf (topology S) AddCommGrpCat.{u}) := by
  infer_instance

end

/-- The category of sheafs on the small affine Ã©tale site is equivalent to the category of
sheafs on the small Ã©tale site. -/
noncomputable def sheafEquiv (A : Type*) [Category A]
    [âˆ€ (X : S.Etaleáµ’áµ–), Limits.HasLimitsOfShape (StructuredArrow X (AffineEtale.Spec S).op) A] :
    Sheaf (AffineEtale.topology S) A â‰Œ Sheaf (smallEtaleTopology S) A :=
  (AffineEtale.Spec S).sheafInducedTopologyEquivOfIsCoverDense _ _

end AlgebraicGeometry.Scheme.AffineEtale
