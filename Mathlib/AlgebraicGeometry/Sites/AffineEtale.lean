/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Jo√´l Riou
-/
module

public import Mathlib.Algebra.Category.Grp.Abelian
public import Mathlib.AlgebraicGeometry.Sites.Etale
public import Mathlib.CategoryTheory.Sites.Abelian
public import Mathlib.CategoryTheory.Sites.DenseSubsite.OneHypercoverDense

/-!
# Affine √©tale site

In this file we define the small affine √©tale site of a scheme `S`. The underlying
category is the category of commutative rings `R` equipped with an √©tale structure
morphism `Spec R ‚ü∂ S`. We show that this category is essentially small,
that it is a dense subsite of the small √©tale site, and that it is `1`-hypercover
dense, which allows to show that if `S : Scheme.{u}`, then we can sheafify
√©tale presheaves with values in `Type u`, `AddCommGrpCat.{u}`, etc (TODO).

## Main results
- `AlgebraicGeometry.Scheme.AffineEtale.sheafEquiv`: The category of sheaves on the
  small affine √©tale site is equivalent to the category of schemes on the small √©tale site.
-/

@[expose] public section

universe u v u'

open CategoryTheory Opposite Limits MorphismProperty

-- to be moved
/-- The equivalence of rings between two equals subrings. -/
@[simps!]
def Subring.equivOfEq {R : Type u} [Ring R] {s t : Subring R} (h : s = t) :
    s ‚âÉ+* t where
  toEquiv := (Equiv.refl _).subtypeEquiv (by simp [h])
  map_mul' := by simp
  map_add' := by simp

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}}

section

/-- Construct an object of affine `P`-schemes over `S` by giving a morphism `Spec R ‚ü∂ S`. -/
@[simps! hom left]
def affineOverMk {P : MorphismProperty Scheme.{u}} {R : CommRingCat.{u}}
    (f : Spec R ‚ü∂ S) (hf : P f) :
    P.CostructuredArrow ‚ä§ Scheme.Spec S :=
  .mk ‚ä§ f hf

/-- The `Spec` functor from affine `P`-schemes over `S` to `P`-schemes over `S` is dense
if `P` is local at the source. -/
instance isCoverDense_toOver_Spec (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsZariskiLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    (CostructuredArrow.toOver P Scheme.Spec S).IsCoverDense
      (smallGrothendieckTopology P) where
  is_cover U := by
    rw [Scheme.mem_smallGrothendieckTopology]
    let ùí∞ : Cover.{u} (precoverage P) U.left :=
      U.left.affineCover.changeProp
      (fun _ ‚Ü¶ IsZariskiLocalAtSource.of_isOpenImmersion _)
    let _ (i : ùí∞.I‚ÇÄ) : (ùí∞.X i).Over S := ‚ü®ùí∞.f i ‚â´ U.hom‚ü©
    let _ : Cover.Over S ùí∞ := { isOver_map _ := ‚ü®rfl‚ü© }
    refine ‚ü®ùí∞, inferInstance,
      fun i ‚Ü¶ P.comp_mem _ _ (ùí∞.map_prop i) U.prop, fun X f ‚ü®i‚ü© ‚Ü¶ ?_‚ü©
    rw [Sieve.coverByImage]
    exact ‚ü®‚ü®affineOverMk (ùí∞.f i ‚â´ U.hom) (P.comp_mem _ _ (ùí∞.map_prop i) U.prop),
      CostructuredArrow.homMk (ùüô _) ‚ü®‚ü© rfl, Over.homMk (ùí∞.f i) (by simp) trivial,
      by cat_disch‚ü©‚ü©

instance isOneHypercoverDense_toOver_Spec
    (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsZariskiLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    Functor.IsOneHypercoverDense.{u} (CostructuredArrow.toOver P Scheme.Spec S)
    ((CostructuredArrow.toOver P Scheme.Spec S).inducedTopology (smallGrothendieckTopology P))
    (smallGrothendieckTopology P) :=
  Functor.IsOneHypercoverDense.of_hasPullbacks (fun X ‚Ü¶ by
    let ùí∞ := affineOpenCover X.left
    let ùí± : Cover (precoverage P) X.left :=
      ùí∞.openCover.changeProp (fun _ ‚Ü¶ IsZariskiLocalAtSource.of_isOpenImmersion _)
    let _ (i : ùí±.I‚ÇÄ) : (ùí±.X i).Over S := ‚ü®ùí∞.f i ‚â´ X.hom‚ü©
    let : Cover.Over S ùí± := { isOver_map _ := ‚ü®rfl‚ü© }
    refine ‚ü®ùí∞.I‚ÇÄ, fun i ‚Ü¶ affineOverMk (ùí∞.f i ‚â´ X.hom)
      (P.comp_mem _ _ (IsZariskiLocalAtSource.of_isOpenImmersion (ùí∞.f i)) X.prop),
      fun i ‚Ü¶ CostructuredArrow.homMk (ùí∞.f i) (by simp), ?_‚ü©
    rw [Scheme.mem_smallGrothendieckTopology]
    exact ‚ü®ùí±, inferInstance, fun i ‚Ü¶ P.comp_mem _ _ (ùí±.map_prop i) X.prop,
      fun _ _ ‚ü®i‚ü© ‚Ü¶ (Sieve.mem_ofArrows_iff ..).2 ‚ü®i, ùüô _, by cat_disch‚ü©‚ü©)

variable (S) in
/-- Given a scheme `S`, this is a structure which describes
an `S`-scheme that is finitely presented over an affine open
subset of `S`. This is used in the proof of the lemma
`essentiallySmall_costructuredArrow_Spec`. -/
structure FinitelyPresentedOverAffineOpen : Type u where
  /-- the (affine) open subset in the base scheme -/
  U : Opens S
  hU : IsAffineOpen U
  /-- the numbers of generators -/
  g : ‚Ñï
  /-- the numbers of relations -/
  r : ‚Ñï
  /-- the relations -/
  rel (x : Fin r) : MvPolynomial (Fin g) Œì(S, U)

namespace FinitelyPresentedOverAffineOpen

variable (P : S.FinitelyPresentedOverAffineOpen)

/-- The ring defined by the given presentation by generators and relations. -/
protected abbrev Ring : Type u :=
  MvPolynomial (Fin P.g) Œì(S, P.U) ‚ß∏ Ideal.span (Set.range P.rel)

lemma exists_nhd {X : Scheme.{u}} (f : X ‚ü∂ S) [LocallyOfFinitePresentation f] (x : X) :
    ‚àÉ (U : Opens X) (_ : x ‚àà U) (P : S.FinitelyPresentedOverAffineOpen),
      Nonempty (U.toScheme ‚âÖ Spec (.of P.Ring)) := by
  obtain ‚ü®U, V, hx, hUV‚ü© :
      ‚àÉ (U : X.affineOpens) (V : S.affineOpens), x ‚àà U.val ‚àß U ‚â§ f.base ‚Åª¬π' V := by
    obtain ‚ü®U, h‚ÇÅ, h‚ÇÇ, _‚ü© := exists_isAffineOpen_mem_and_subset (x := f.base x) (U := ‚ä§) (by simp)
    obtain ‚ü®V, h‚ÇÉ, h‚ÇÑ, h‚ÇÖ‚ü© := exists_isAffineOpen_mem_and_subset (x := x)
      (U := ‚ü®_, IsOpen.preimage f.continuous U.2‚ü©) (by simpa)
    exact ‚ü®‚ü®V, h‚ÇÉ‚ü©, ‚ü®U, h‚ÇÅ‚ü©, h‚ÇÑ, h‚ÇÖ‚ü©
  letI := (f.appLE V U hUV).hom.toAlgebra
  obtain ‚ü®n, œÜ, h‚ÇÅ, h‚ÇÇ‚ü© :=
    (LocallyOfFinitePresentation.finitePresentation_appLE f V.prop U.prop hUV).out
  obtain ‚ü®r, œÅ, hœÅ‚ü© : ‚àÉ (r : ‚Ñï) (Œ≥ : Fin r ‚Üí MvPolynomial (Fin n) Œì(S, V)),
      Ideal.span (Set.range Œ≥) = RingHom.ker œÜ.toRingHom := by
    obtain ‚ü®s, hs‚ü© := h‚ÇÇ
    exact ‚ü®s.card, Subtype.val ‚àò s.equivFin.symm, by rw [‚Üê hs]; simp‚ü©
  let P : S.FinitelyPresentedOverAffineOpen :=
    { U := V.1, hU := V.prop, g := n, r := r, rel := œÅ }
  let e : P.Ring ‚âÉ+* Œì(X, U.1) :=
    (Ideal.quotEquivOfEq hœÅ).trans (œÜ.toRingHom.quotientKerEquivRange.trans
      ((Subring.equivOfEq (RingHom.range_eq_top_of_surjective _ h‚ÇÅ)).trans Subring.topEquiv))
  exact ‚ü®U, hx, P, ‚ü®asIso (toSpecŒì U) ‚â™‚â´ Scheme.Spec.mapIso U.1.topIso.op.symm ‚â™‚â´
    Scheme.Spec.mapIso e.toCommRingCatIso.op‚ü©‚ü©

lemma exists_subring
    {A : CommRingCat.{u}} (f : Spec (.of A) ‚ü∂ S) [LocallyOfFinitePresentation f] :
    ‚àÉ (n : ‚Ñï) (P : Fin n ‚Üí S.FinitelyPresentedOverAffineOpen)
      (R‚ÇÄ : Subring (‚àÄ i, (P i).Ring)), Nonempty (A ‚âÖ .of R‚ÇÄ) := by
  /- We find a finite open covering `(i : Fin n) ‚Ü¶ U (Œ± i)` of `Spec A`
  consisting of finitely presented schemes over an affine open subset of `S`. -/
  choose U hU P e using exists_nhd f
  let iso (x) := (e x).some
  obtain ‚ü®n, Œ±, hŒ±‚ü© : ‚àÉ (n : ‚Ñï) (Œ± : Fin n ‚Üí Spec (.of A)),
    ‚ãÉ (i : Fin n), (U (Œ± i) : Set (Spec (.of A))) = Set.univ := by
      obtain ‚ü®s, hs‚ü© := CompactSpace.isCompact_univ.elim_finite_subcover _
        (fun x ‚Ü¶ (U x).isOpen) (fun x _ ‚Ü¶ Set.mem_iUnion_of_mem x (hU x))
      refine ‚ü®s.card, Subtype.val ‚àò (Finset.equivFin s).symm,
        subset_antisymm (by simp) (hs.trans ?_)‚ü©
      simp only [Function.comp_apply, Set.iUnion_subset_iff]
      exact fun i hi _ _ ‚Ü¶ Set.mem_iUnion_of_mem ((Finset.equivFin s) ‚ü®i, hi‚ü©) (by simpa)
  /- We consider the map `œÜ : A ‚Üí+* ‚àÄ i, ((P ‚àò Œ±) i).Ring` that is essentially given by
  the restriction to the opens `U (Œ± i)`, and show that it is injective by
  using the sheaf property of the structure sheaf. -/
  have (i : Fin n) := (U (Œ± i)).Œπ
  let Œ≤ (i : Fin n) : A ‚Üí+* ((P ‚àò Œ±) i).Ring :=
    (Spec.preimage ((iso (Œ± i)).inv ‚â´ (U (Œ± i)).Œπ)).hom
  let œÜ : A ‚Üí+* ‚àÄ i, ((P ‚àò Œ±) i).Ring := Pi.ringHom Œ≤
  have hœÜ : Function.Injective œÜ := by
    refine (AddMonoidHom.ker_eq_bot_iff œÜ.toAddMonoidHom).1 ?_
    ext a
    refine ‚ü®fun ha ‚Ü¶ ?_, by rintro rfl; simp‚ü©
    replace ha (i : Fin n) : Œ≤ i a = 0 := congr_fun ha i
    obtain ‚ü®a, rfl‚ü© := (ŒìSpecIso A).commRingCatIsoToRingEquiv.surjective a
    simp only [AddSubgroup.mem_bot, EmbeddingLike.map_eq_zero_iff]
    refine (openCoverOfIsOpenCover _ (U ‚àò Œ±) (.mk (by aesop))).ext_elem _ _ (fun i ‚Ü¶ ?_)
    replace ha : (ŒìSpecIso _).hom (((iso (Œ± i)).inv ‚â´ (U (Œ± i)).Œπ).appTop a) = 0 := by
      simpa [‚Üê ha] using ConcreteCategory.congr_hom (ŒìSpecIso_naturality
        (Spec.preimage ((iso (Œ± i)).inv ‚â´ (U (Œ± i)).Œπ))) a
    apply (asIso (iso (Œ± i)).inv.appTop ‚â™‚â´
      ŒìSpecIso _).commRingCatIsoToRingEquiv.injective
    simpa [-EmbeddingLike.map_eq_zero_iff] using ha
  /- The expected subring is given by the range of `œÜ`. -/
  exact ‚ü®n, P ‚àò Œ±, RingHom.range œÜ, ‚ü®RingEquiv.toCommRingCatIso
    (RingEquiv.ofBijective œÜ.rangeRestrict
      ‚ü®(Function.Injective.of_comp_iff Subtype.val_injective _).1 hœÜ,
        RingHom.rangeRestrict_surjective œÜ‚ü©)‚ü©‚ü©

end FinitelyPresentedOverAffineOpen

lemma essentiallySmall_costructuredArrow_Spec
    (P : MorphismProperty Scheme.{u}) (hP : P ‚â§ @LocallyOfFinitePresentation) [P.RespectsIso] :
    EssentiallySmall.{u} (P.CostructuredArrow ‚ä§ Scheme.Spec S) := by
  /- It suffices to show that there is a `u`-small type which, up to
  isomorphisms, parametrizes all the the possible rings `Z.left.unop` for
  `Z : P.CostructuredArrow ‚ä§ Scheme.Spec S`. -/
  suffices ‚àÉ (Œπ : Type u) (R : Œπ ‚Üí CommRingCat.{u}),
      ‚àÄ (Z : P.CostructuredArrow ‚ä§ Scheme.Spec S),
        ‚àÉ (i : Œπ), Nonempty (R i ‚âÖ Z.left.unop) by
    rw [essentiallySmall_iff_objectPropertyEssentiallySmall_top]
    obtain ‚ü®Œπ, R, hR‚ü© := this
    let P‚ÇÄ : ObjectProperty (P.CostructuredArrow ‚ä§ Scheme.Spec S) :=
      .ofObj (fun (t : Œ£ (i : Œπ) (f : Scheme.Spec.obj (Opposite.op (R i)) ‚ü∂ S), PLift (P f)) ‚Ü¶
        .mk (A := op (R t.1)) _ t.2.1 t.2.2.down)
    refine ObjectProperty.EssentiallySmall.of_le (Q := P‚ÇÄ.isoClosure) (fun Z _ ‚Ü¶ ?_)
    obtain ‚ü®i, ‚ü®e‚ü©‚ü© := hR Z
    refine ‚ü®_, ‚ü®i, Spec.map e.inv ‚â´ Z.hom, ‚ü®RespectsIso.precomp _ _ _ Z.prop‚ü©‚ü©, ‚ü®?_‚ü©‚ü©
    exact MorphismProperty.CostructuredArrow.isoMk e.op (by simp) (by simp)
      (by simp [‚Üê Spec.map_comp_assoc, e.inv_hom_id])
  /- We use the family given by all the subrings of the finite products
  of finitely presented algebras over the rings of sections of an affine
  open subset of `S`. -/
  refine ‚ü®Œ£ (n : ‚Ñï) (P : Fin n ‚Üí S.FinitelyPresentedOverAffineOpen),
    Subring (‚àÄ i, (P i).Ring), fun ‚ü®n, P, R‚ÇÄ‚ü© ‚Ü¶ .of R‚ÇÄ, fun Z ‚Ü¶ ?_‚ü©
  have : LocallyOfFinitePresentation Z.hom := hP _ Z.prop
  obtain ‚ü®n, P, R‚ÇÄ, ‚ü®e‚ü©‚ü© := FinitelyPresentedOverAffineOpen.exists_subring Z.hom
  exact ‚ü®‚ü®n, P, R‚ÇÄ‚ü©, ‚ü®e.symm‚ü©‚ü©

variable {P : MorphismProperty Scheme.{u}} [IsZariskiLocalAtSource P]

instance IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete
    {Œπ : Type*} [Small.{u} Œπ] {C : Type*} [Category C] [HasColimitsOfShape (Discrete Œπ) C]
    (L : C ‚•§ Scheme.{u}) [PreservesColimitsOfShape (Discrete Œπ) L] (X : Scheme.{u}) :
    (P.costructuredArrowObj L (X := X)).IsClosedUnderColimitsOfShape (Discrete Œπ) :=
  CostructuredArrow.isClosedUnderColimitsOfShape _ (fun _ ‚Ü¶ coproductIsCoproduct' _)
    (fun _ _ _ _ h ‚Ü¶ IsZariskiLocalAtSource.sigmaDesc (h ‚ü®¬∑‚ü©)) _

variable [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] [P.IsMultiplicative]

instance : HasFiniteCoproducts (P.CostructuredArrow ‚ä§ Scheme.Spec S) where
  out n := by
    have : (MorphismProperty.commaObj Scheme.Spec (.fromPUnit S) P).IsClosedUnderColimitsOfShape
        (Discrete (Fin n)) :=
      IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete _ _
    apply MorphismProperty.Comma.hasColimitsOfShape_of_closedUnderColimitsOfShape

end

/-- The small affine √©tale site: The category of affine schemes √©tale over `S`, whose objects are
commutative rings `R` with an √©tale structure morphism `Spec R ‚ü∂ S`. -/
def AffineEtale (S : Scheme.{u}) : Type (u + 1) :=
  MorphismProperty.CostructuredArrow @Etale.{u} ‚ä§ Scheme.Spec.{u} S

namespace AffineEtale

/-- Construct an object of the small affine √©tale site. -/
@[simps!]
protected def mk {R : CommRingCat.{u}} (f : Spec R ‚ü∂ S) [Etale f] : AffineEtale S :=
  MorphismProperty.CostructuredArrow.mk ‚ä§ f ‚Äπ_‚Ä∫

instance : Category S.AffineEtale :=
  inferInstanceAs <| Category (MorphismProperty.CostructuredArrow _ _ _ _)

/-- The `Spec` functor from the small affine √©tale site of `S` to the small √©tale site of `S`. -/
@[simps! obj_left obj_hom map_left]
protected noncomputable def Spec (S : Scheme.{u}) : S.AffineEtale ‚•§ S.Etale :=
  MorphismProperty.CostructuredArrow.toOver _ _ _

instance : (AffineEtale.Spec S).Faithful :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Faithful

instance : (AffineEtale.Spec S).Full :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Full

instance : (AffineEtale.Spec S).IsCoverDense S.smallEtaleTopology :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).IsCoverDense
    (smallGrothendieckTopology _)

instance : HasPullbacks S.AffineEtale :=
  inferInstanceAs <| HasPullbacks (MorphismProperty.CostructuredArrow _ _ _ _)

variable (S) in
/-- The topology on the small affine √©tale site is the topology induced by `Spec` from
the small √©tale site. -/
def topology : GrothendieckTopology S.AffineEtale :=
  (AffineEtale.Spec S).inducedTopology (smallEtaleTopology S)

instance : Functor.IsDenseSubsite (topology S) (S.smallEtaleTopology) (AffineEtale.Spec S) := by
  dsimp [topology]
  infer_instance

instance : Functor.IsOneHypercoverDense.{u} (AffineEtale.Spec S)
    (topology S) (S.smallEtaleTopology) :=
  isOneHypercoverDense_toOver_Spec _

instance : EssentiallySmall.{u} S.AffineEtale :=
  essentiallySmall_costructuredArrow_Spec _ (fun _ _ _ _ ‚Ü¶ inferInstance)

section

variable {A : Type u'} [Category.{u} A]
  {FA : A ‚Üí A ‚Üí Type*} {CD : A ‚Üí Type u}
  [‚àÄ X Y, FunLike (FA X Y) (CD X) (CD Y)] [ConcreteCategory.{u} A FA]
  [PreservesLimits (CategoryTheory.forget A)] [HasColimits A] [HasLimits A]
  [(CategoryTheory.forget A).ReflectsIsomorphisms]
  [PreservesFilteredColimitsOfSize.{u, u} (CategoryTheory.forget A)]

instance : HasSheafify (topology S) A := hasSheafifyEssentiallySmallSite.{u} _ _

example : HasSheafify (topology S) (Type u) := by
  infer_instance

example : Abelian (Sheaf (topology S) AddCommGrpCat.{u}) := by
  infer_instance

end

/-- The category of sheafs on the small affine √©tale site is equivalent to the category of
sheafs on the small √©tale site. -/
noncomputable def sheafEquiv (A : Type*) [Category A]
    [‚àÄ (X : S.Etale·µí·µñ), Limits.HasLimitsOfShape (StructuredArrow X (AffineEtale.Spec S).op) A] :
    Sheaf (AffineEtale.topology S) A ‚âå Sheaf (smallEtaleTopology S) A :=
  (AffineEtale.Spec S).sheafInducedTopologyEquivOfIsCoverDense _ _

end AlgebraicGeometry.Scheme.AffineEtale
