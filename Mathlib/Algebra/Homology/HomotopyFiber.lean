/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.HomotopyCofiber
public import Mathlib.Algebra.Homology.Opposite

/-! The homotopy fiber of a morphism of homological complexes

-/

@[expose] public section


open CategoryTheory Category Limits Preadditive Opposite

variable {C : Type*} [Category* C] [Preadditive C]

namespace Homotopy

variable {Œ± : Type*} {c : ComplexShape Œ±}

open HomologicalComplex

@[simps]
def unop {F G : HomologicalComplex C·µí·µñ c} {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G}
    (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) :
      Homotopy ((unopFunctor C c).map œÜ‚ÇÅ.op) ((unopFunctor C c).map œÜ‚ÇÇ.op) where
  hom i j := (h.hom j i).unop
  zero i j hij := Quiver.Hom.op_inj (h.zero _ _ hij)
  comm n := Quiver.Hom.op_inj (by
    dsimp
    rw [h.comm n]
    dsimp
    nth_rw 2 [add_comm]
    rfl)

@[simps]
def op {F G : HomologicalComplex C c} {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G}
    (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) :
      Homotopy ((opFunctor C c).map œÜ‚ÇÅ.op) ((opFunctor C c).map œÜ‚ÇÇ.op) where
  hom i j := (h.hom j i).op
  zero i j hij := Quiver.Hom.unop_inj (h.zero _ _ hij)
  comm n := Quiver.Hom.unop_inj (by
    dsimp
    rw [h.comm n]
    dsimp
    nth_rw 2 [add_comm]
    rfl)

end Homotopy

def ComplexShape.decidableRelSymm {Œ± : Type*} (c : ComplexShape Œ±)
    [DecidableRel c.Rel] :
    DecidableRel c.symm.Rel :=
  fun a b ‚Ü¶ decidable_of_iff (c.Rel b a) Iff.rfl

namespace HomologicalComplex

attribute [local instance] ComplexShape.decidableRelSymm

variable {Œ± : Type*} {c : ComplexShape Œ±} {F G K : HomologicalComplex C c} (œÜ : F ‚ü∂ G)

variable [DecidableRel c.Rel]

section

/-- A morphism of homological complexes `œÜ : F ‚ü∂ G` has a homotopy fiber if for all
indices `i` and `j` such that `c.Rel i j`, the binary biproduct `F.X i ‚äû G.X j` exists. -/
class HasHomotopyFiber (œÜ : F ‚ü∂ G) : Prop where
  hasBinaryBiproduct (œÜ) (i j : Œ±) (hij : c.Rel i j) : HasBinaryBiproduct (G.X i) (F.X j)

instance [HasBinaryBiproducts C] : HasHomotopyFiber œÜ where
  hasBinaryBiproduct _ _ _ := inferInstance

variable [HasHomotopyFiber œÜ] [DecidableRel c.Rel]

instance : HasHomotopyCofiber ((opFunctor C c).map œÜ.op) where
  hasBinaryBiproduct i j hij := by
    have := HasHomotopyFiber.hasBinaryBiproduct œÜ j i hij
    dsimp
    infer_instance

noncomputable def homotopyFiber : HomologicalComplex C c :=
  (unopFunctor C c.symm).obj (op (homotopyCofiber ((opFunctor C c).map œÜ.op)))

end

variable (K)
variable [‚àÄ i, HasBinaryBiproduct (K.X i) (K.X i)]

instance (i : Œ±) : HasBinaryBiproduct (K.op.X i) (K.op.X i) := by
  dsimp; infer_instance

abbrev HasPathObject := HasHomotopyCofiber (biprod.lift (ùüô K.op) (-ùüô K.op))

variable [K.HasPathObject]

noncomputable def pathObject := (unopFunctor C c.symm).obj (op K.op.cylinder)

namespace pathObject

noncomputable def œÄ‚ÇÄ : K.pathObject ‚ü∂ K :=
  (unopFunctor C c.symm).map (cylinder.Œπ‚ÇÄ K.op).op

noncomputable def œÄ‚ÇÅ : K.pathObject ‚ü∂ K :=
  (unopFunctor C c.symm).map (cylinder.Œπ‚ÇÅ K.op).op

noncomputable def Œπ : K ‚ü∂ K.pathObject :=
  (unopFunctor C c.symm).map (cylinder.œÄ K.op).op

@[reassoc (attr := simp)]
lemma œÄ‚ÇÄ_Œπ : Œπ K ‚â´ œÄ‚ÇÄ K = ùüô K :=
  Quiver.Hom.op_inj ((opFunctor C c).map_injective (cylinder.Œπ‚ÇÄ_œÄ K.op))

@[reassoc (attr := simp)]
lemma œÄ‚ÇÅ_Œπ : Œπ K ‚â´ œÄ‚ÇÅ K = ùüô K :=
  Quiver.Hom.op_inj ((opFunctor C c).map_injective (cylinder.Œπ‚ÇÅ_œÄ K.op))

noncomputable def homotopy‚ÇÄ‚ÇÅ (hc : ‚àÄ (i : Œ±), ‚àÉ j, c.Rel i j) : Homotopy (œÄ‚ÇÄ K) (œÄ‚ÇÅ K) :=
  (cylinder.homotopy‚ÇÄ‚ÇÅ K.op hc).unop

/-- The homotopy equivalence between `K` and `K.pathObject`. -/
noncomputable def homotopyEquiv (hc : ‚àÄ (i : Œ±), ‚àÉ j, c.Rel i j) :
    HomotopyEquiv K K.pathObject where
  hom := Œπ K
  inv := œÄ‚ÇÄ K
  homotopyHomInvId := Homotopy.ofEq (by simp)
  homotopyInvHomId := (cylinder.œÄCompŒπ‚ÇÄHomotopy K.op hc).unop

section

variable {K} (œÜ‚ÇÄ œÜ‚ÇÅ : F ‚ü∂ K) (h : Homotopy œÜ‚ÇÄ œÜ‚ÇÅ)

noncomputable def lift : F ‚ü∂ K.pathObject := by
  letI œÜ : K.op.cylinder ‚ü∂ (opFunctor C c).obj (op F) :=
    cylinder.desc ((opFunctor C c).map œÜ‚ÇÄ.op)
      ((opFunctor C c).map œÜ‚ÇÅ.op) h.op
  exact (unopFunctor C c.symm).map œÜ.op

@[reassoc (attr := simp)]
lemma lift_œÄ‚ÇÄ : lift œÜ‚ÇÄ œÜ‚ÇÅ h ‚â´ œÄ‚ÇÄ K = œÜ‚ÇÄ :=
  Quiver.Hom.op_inj ((opFunctor C c).map_injective (cylinder.Œπ‚ÇÄ_desc _ _ _))

@[reassoc (attr := simp)]
lemma lift_œÄ‚ÇÅ : lift œÜ‚ÇÄ œÜ‚ÇÅ h ‚â´ œÄ‚ÇÅ K = œÜ‚ÇÅ :=
  Quiver.Hom.op_inj ((opFunctor C c).map_injective (cylinder.Œπ‚ÇÅ_desc _ _ _))

end

variable (F) {D : Type*} [Category* D] [Preadditive D] (H : C ‚•§ D) [H.Additive]
  [‚àÄ (i : Œ±), HasBinaryBiproduct (((H.mapHomologicalComplex c).obj K).X i)
    (((H.mapHomologicalComplex c).obj K).X i)]
  [((H.mapHomologicalComplex c).obj K).HasPathObject]
  [‚àÄ (i : Œ±),
    HasBinaryBiproduct (((H.op.mapHomologicalComplex c.symm).obj K.op).X i)
      (((H.op.mapHomologicalComplex c.symm).obj K.op).X i)]
  [HasHomotopyCofiber
    (biprod.lift (ùüô ((H.op.mapHomologicalComplex c.symm).obj K.op))
    (-ùüô ((H.op.mapHomologicalComplex c.symm).obj K.op)))]
  [HasHomotopyCofiber ((H.op.mapHomologicalComplex c.symm).map (biprod.lift (ùüô K.op) (-ùüô K.op)))]
  (hc : ‚àÄ (i : Œ±), ‚àÉ j, c.Rel i j)

noncomputable def mapHomologicalComplexObjIso :
    (H.mapHomologicalComplex c).obj (K.pathObject) ‚âÖ
      pathObject ((H.mapHomologicalComplex c).obj K) :=
  (unopFunctor _ _).mapIso
    (cylinder.mapHomologicalComplexObjIso K.op H.op hc).op.symm

@[reassoc (attr := simp)]
lemma mapHomologicalComplexObjIso_hom_map_œÄ‚ÇÄ :
    (mapHomologicalComplexObjIso K H hc).inv ‚â´ (H.mapHomologicalComplex c).map (œÄ‚ÇÄ K) =
      œÄ‚ÇÄ _ :=
  Quiver.Hom.op_inj ((opFunctor _ _).map_injective
    (cylinder.map_Œπ‚ÇÄ_mapHomologicalComplexObjIso_hom K.op H.op hc))

@[reassoc (attr := simp)]
lemma mapHomologicalComplexObjIso_hom_map_œÄ‚ÇÅ :
    (mapHomologicalComplexObjIso K H hc).inv ‚â´ (H.mapHomologicalComplex c).map (œÄ‚ÇÅ K) =
      œÄ‚ÇÅ _ :=
  Quiver.Hom.op_inj ((opFunctor _ _).map_injective
    (cylinder.map_Œπ‚ÇÅ_mapHomologicalComplexObjIso_hom K.op H.op hc))

end pathObject

end HomologicalComplex
