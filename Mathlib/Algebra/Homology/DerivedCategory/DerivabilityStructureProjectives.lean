/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.DerivedCategory.Minus
public import Mathlib.Algebra.Homology.DerivedCategory.KProjective
public import Mathlib.Algebra.Homology.ModelCategory.Projective
public import Mathlib.AlgebraicTopology.ModelCategory.DerivabilityStructureCofibrant
public import Mathlib.CategoryTheory.GuitartExact.Quotient
public import Mathlib.CategoryTheory.Localization.DerivabilityStructure.OfLocalizedEquivalences

/-!
# The projective derivability structure

-/

@[expose] public section

universe w‚ÇÅ w‚ÇÇ

open HomotopicalAlgebra CategoryTheory Limits ZeroObject Category

variable (C : Type*) [Category C] [Abelian C]
  {H : Type*} [Category H]

namespace HomologicalComplex

@[simps X d]
def liftObjectProperty {Œπ : Type*} {c : ComplexShape Œπ}
    {V : Type*} [Category* V] [Preadditive V] (P : ObjectProperty V)
    (K : HomologicalComplex V c) (hK : ‚àÄ (n : Œπ), P (K.X n)) :
    HomologicalComplex P.FullSubcategory c where
  X n := ‚ü®_, hK n‚ü©
  d i j := ObjectProperty.homMk (K.d i j)

@[simps]
def liftFunctorObjectProperty {D : Type*} [Category* D] {Œπ : Type*} {c : ComplexShape Œπ}
    {V : Type*} [Category* V] [Preadditive V] (P : ObjectProperty V)
    (F : D ‚•§ HomologicalComplex V c) (hF : ‚àÄ (X : D) (n : Œπ), P ((F.obj X).X n)) :
    D ‚•§ HomologicalComplex P.FullSubcategory c where
  obj X := liftObjectProperty _ (F.obj X) (hF X)
  map f := { f n := ObjectProperty.homMk ((F.map f).f n) }

end HomologicalComplex

namespace CategoryTheory

abbrev Projectives := ObjectProperty.FullSubcategory (fun (X : C) => Projective X)

namespace Projectives

instance closedUnderLimitsOfShapeDiscrete (J : Type*) :
    ObjectProperty.IsClosedUnderColimitsOfShape (fun (X : C) => Projective X) (Discrete J) where
  colimitsOfShape_le := by
    rintro Y ‚ü®p‚ü©
    have : HasColimit p.diag := ‚ü®_, p.isColimit‚ü©
    let X := fun j => p.diag.obj ‚ü®j‚ü©
    let e := Discrete.natIsoFunctor (F := p.diag)
    have : HasCoproduct X := hasColimit_of_iso e.symm
    have : HasColimit (Discrete.functor (p.diag.obj ‚àò Discrete.mk)) := by
      change HasCoproduct X
      infer_instance
    have : ‚àÄ j, Projective (X j) := fun j => p.prop_diag_obj ‚ü®j‚ü©
    have e' : ‚àê X ‚âÖ Y := IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
      ((IsColimit.precomposeInvEquiv e _).symm p.isColimit)
    exact Projective.of_iso e' inferInstance

instance : HasFiniteCoproducts (Projectives C) where
  out n := by infer_instance

instance : HasFiniteBiproducts (Projectives C) :=
  HasFiniteBiproducts.of_hasFiniteCoproducts

instance : HasBinaryBiproducts (Projectives C) := hasBinaryBiproducts_of_finite_biproducts _

instance : HasZeroObject (Projectives C) where
  zero := by
    refine ‚ü®‚ü®0, inferInstance‚ü©, ?_‚ü©
    rw [IsZero.iff_id_eq_zero]
    ext : 1
    apply id_zero

abbrev Œπ : Projectives C ‚•§ C := ObjectProperty.Œπ _

instance (X : Projectives C) : Projective ((Œπ C).obj X) := X.2

instance (X : Projectives C) : Projective X.obj := X.2

instance (X : HomotopyCategory.Plus (Projectives C)) (n : ‚Ñ§) :
    Projective (((Œπ C).mapHomotopyCategoryPlus.obj X).obj.as.X n) := by
  change Projective ((Œπ C).obj (X.obj.as.X n))
  infer_instance

variable {C}

def liftHomotopyCategoryPlusOfProjective (K : HomotopyCategory.Plus C)
  [‚àÄ (n : ‚Ñ§), Projective (K.obj.as.X n)] : HomotopyCategory.Plus (Projectives C) :=
    { obj :=
       ‚ü®{ X n := ‚ü®K.obj.as.X n, inferInstance‚ü©
          d i j := ObjectProperty.homMk (K.obj.as.d i j)
          shape i j hij := by ext : 1; exact K.obj.as.shape i j hij
          d_comp_d' i j k _ _ := by ext : 1; exact K.obj.as.d_comp_d i j k }‚ü©
      property := by
        obtain ‚ü®n, hn‚ü© := K.2
        refine ‚ü®n, ?_‚ü©
        rw [CochainComplex.isStrictlyGE_iff]
        intro i hi
        have := CochainComplex.isZero_of_isStrictlyGE K.obj.as n i hi
        rw [IsZero.iff_id_eq_zero] at this ‚ä¢
        ext : 1
        exact this }

def isoMapHomotopyCategoryPlusŒπObj (K : HomotopyCategory.Plus C)
    [‚àÄ (n : ‚Ñ§), Projective (K.obj.as.X n)] :
    (Œπ C).mapHomotopyCategoryPlus.obj (liftHomotopyCategoryPlusOfProjective K) ‚âÖ K := Iso.refl _

lemma mem_essImage_mapHomotopyCategoryPlus_Œπ_of_projective (K : HomotopyCategory.Plus C)
    [‚àÄ (n : ‚Ñ§), Projective (K.obj.as.X n)] :
    (Œπ C).mapHomotopyCategoryPlus.essImage K :=
  ‚ü®_, ‚ü®isoMapHomotopyCategoryPlusŒπObj K‚ü©‚ü©

instance (K : CochainComplex.Minus (Projectives C)) :
    CochainComplex.IsKProjective
      (((Projectives.Œπ C).mapHomologicalComplex (.up ‚Ñ§)).obj K.obj) := by
  obtain ‚ü®K, n, hn‚ü© := K
  let L := ((Projectives.Œπ C).mapHomologicalComplex (.up ‚Ñ§)).obj K
  have (n : ‚Ñ§) : Projective (L.X n) := by dsimp [L]; infer_instance
  exact CochainComplex.isKProjective_of_projective L n

end Projectives

end CategoryTheory

namespace CochainComplex.Minus

@[simps]
def localizerMorphism :
    LocalizerMorphism ((quasiIso C).inverseImage (Projectives.Œπ C).mapCochainComplexMinus)
      (quasiIso C) where
  functor := (Projectives.Œπ C).mapCochainComplexMinus
  map := by rfl

instance : (localizerMorphism C).IsInduced where
  inverseImage_eq := rfl

instance (K : Minus (Projectives C)) (n : ‚Ñ§) :
    Projective (K.obj.X n).obj :=
  (K.obj.X n).property

variable [EnoughProjectives C]

open modelCategoryQuillen

instance (K : CofibrantObject (Minus C)) (n : ‚Ñ§) :
    Projective (K.obj.obj.X n) := by
  obtain ‚ü®K, hK‚ü© := K
  rw [cofibrantObjects, modelCategoryQuillen.isCofibrant_iff] at hK
  dsimp
  infer_instance

def cofibrantObjectEquivalence :
    Minus (Projectives C) ‚âå CofibrantObject (Minus C) where
  functor := ObjectProperty.lift _ (Projectives.Œπ C).mapCochainComplexMinus (fun K ‚Ü¶ by
    dsimp [cofibrantObjects]
    rw [modelCategoryQuillen.isCofibrant_iff]
    intro n
    dsimp
    infer_instance)
  inverse := ObjectProperty.lift _
    (HomologicalComplex.liftFunctorObjectProperty _ (CofibrantObject.Œπ ‚ãô Minus.Œπ C)
      (fun K n ‚Ü¶ by dsimp; infer_instance)) (by
        rintro ‚ü®‚ü®K, n, hn‚ü©, _‚ü©
        refine ‚ü®n, ?_‚ü©
        rw [isStrictlyLE_iff]
        intro i hi
        rw [IsZero.iff_id_eq_zero]
        ext
        apply (K.isZero_of_isStrictlyLE n i hi).eq_of_src)
  unitIso := Iso.refl _
  counitIso := Iso.refl _

@[simps]
def cofibrantObjectLocalizerMorphism :
    LocalizerMorphism ((quasiIso C).inverseImage (Projectives.Œπ C).mapCochainComplexMinus)
      (weakEquivalences (CofibrantObject (Minus C))) where
  functor := (cofibrantObjectEquivalence C).functor
  map := by rfl

instance : (cofibrantObjectLocalizerMorphism C).IsInduced where
  inverseImage_eq := rfl

instance : (cofibrantObjectLocalizerMorphism C).functor.IsEquivalence := by
  dsimp; infer_instance

instance : (localizerMorphism C).IsLeftDerivabilityStructure := by
  rw [LocalizerMorphism.isLeftDerivabilityStructure_iff_of_equivalences
    (T := localizerMorphism C) (B := (CofibrantObject.localizerMorphism (Minus C)))
    (R := .id _) (L := cofibrantObjectLocalizerMorphism C) (Iso.refl _)]
  infer_instance

end CochainComplex.Minus

namespace HomotopyCategory.Minus

def localizerMorphism : LocalizerMorphism
  (MorphismProperty.isomorphisms (HomotopyCategory.Minus (Projectives C)))
    (HomotopyCategory.Minus.quasiIso C) where
  functor := (Projectives.Œπ C).mapHomotopyCategoryMinus
  map K L f (hf : IsIso f) := by
    dsimp only [MorphismProperty.inverseImage, HomotopyCategory.Minus.quasiIso]
    rw [HomotopyCategory.mem_quasiIso_iff]
    intro n
    infer_instance

variable {C} in
lemma isIso_quotient_map
    {K L : CochainComplex.Minus (Projectives C)} (f : K ‚ü∂ L) :
    IsIso ((quotient _).map f) ‚Üî
    CochainComplex.Minus.quasiIso C ((Projectives.Œπ C).mapCochainComplexMinus.map f) := by
  rw [‚Üê isIso_iff_of_reflects_iso _ (HomotopyCategory.Minus.Œπ (Projectives C)),
    ‚Üê isIso_iff_of_reflects_iso _ (Functor.mapHomotopyCategory (Projectives.Œπ C) (.up ‚Ñ§))]
  dsimp
  rw [CochainComplex.IsKProjective.isIso_quotient_map_iff_quasiIso]
  rfl

namespace isLeftDerivabilityStructure

open MorphismProperty

@[simps]
def L : LocalizerMorphism
  ((CochainComplex.Minus.quasiIso C).inverseImage (Projectives.Œπ C).mapCochainComplexMinus)
      (isomorphisms (Minus (Projectives C))) where
  functor := HomotopyCategory.Minus.quotient (Projectives C)
  map _ _ f hf := (isIso_quotient_map f).2 hf

instance : (L C).IsInduced where
  inverseImage_eq := by ext; apply isIso_quotient_map

@[simps]
def R : LocalizerMorphism (CochainComplex.Minus.quasiIso C) (quasiIso C) where
  functor := HomotopyCategory.Minus.quotient C
  map := by
    intro X Y f hf
    simpa [quasiIso, quotient_map_mem_quasiIso_iff]

instance : (R C).IsInduced where
  inverseImage_eq := by ext; apply quotient_map_mem_quasiIso_iff

open HomologicalComplex in
lemma inverseImage_quasiIso_mapCochainComplexMinus_projectivesŒπ :
    (CochainComplex.Minus.quasiIso C).inverseImage (Projectives.Œπ C).mapCochainComplexMinus =
    (homotopyEquivalences (Projectives C) (ComplexShape.up ‚Ñ§)).inverseImage
      (CochainComplex.Minus.Œπ (Projectives C)) := by
  ext K L f
  simp [CochainComplex.Minus.quasiIso, Functor.mapCochainComplexMinus,
    ‚Üê CochainComplex.IsKProjective.isIso_quotient_map_iff_quasiIso,
    ‚Üê isIso_quotient_map_iff_homotopyEquivalences,
    ‚Üê isIso_iff_of_reflects_iso _ ((Projectives.Œπ C).mapHomotopyCategory (.up ‚Ñ§))]

instance : (HomotopyCategory.Minus.quotient (Projectives C)).IsLocalization
      ((CochainComplex.Minus.quasiIso C).inverseImage
      (Projectives.Œπ C).mapCochainComplexMinus) := by
  rw [inverseImage_quasiIso_mapCochainComplexMinus_projectivesŒπ]
  infer_instance

instance : (L C).IsLocalizedEquivalence := by
  have :
      ((L C).functor ‚ãô ùü≠ (Minus (Projectives C))).IsLocalization
        ((CochainComplex.Minus.quasiIso C).inverseImage (Projectives.Œπ C).mapCochainComplexMinus) :=
    inferInstanceAs ((HomotopyCategory.Minus.quotient (Projectives C)).IsLocalization _)
  exact LocalizerMorphism.IsLocalizedEquivalence.of_isLocalization_of_isLocalization (L C) (ùü≠ _)

open HomologicalComplex in
instance {D : Type*} [Category* D] (L : Minus C ‚•§ D) [L.IsLocalization (quasiIso C)] :
    (quotient C ‚ãô L).IsLocalization (CochainComplex.Minus.quasiIso C) := by
  refine Functor.IsLocalization.comp _ _
    ((homotopyEquivalences C (.up ‚Ñ§)).inverseImage (CochainComplex.Minus.Œπ C))
    (quasiIso C) _ ?_ ?_ ?_
  ¬∑ intro _ _ f hf
    refine Localization.inverts L (quasiIso C) _ ?_
    simpa [quasiIso, quotient_map_mem_quasiIso_iff]
  ¬∑ intro K L f hf
    exact homotopyEquivalences_le_quasiIso _ _ _ hf
  ¬∑ rintro K L f hf
    obtain ‚ü®K, rfl‚ü© := Minus.quotient_obj_surjective K
    obtain ‚ü®L, rfl‚ü© := Minus.quotient_obj_surjective L
    obtain ‚ü®f, rfl‚ü© := (Minus.quotient C).map_surjective f
    apply MorphismProperty.map_mem_map
    simpa [quasiIso, HomotopyCategory.quotient_map_mem_quasiIso_iff] using hf

instance {D : Type*} [Category* D] (L : Minus C ‚•§ D) [L.IsLocalization (quasiIso C)] :
    ((R C).functor ‚ãô L).IsLocalization (CochainComplex.Minus.quasiIso C) := by
  dsimp; infer_instance

instance : (R C).IsLocalizedEquivalence :=
  LocalizerMorphism.IsLocalizedEquivalence.of_isLocalization_of_isLocalization
    (R C) ((quasiIso C).Q)

instance : (L C).functor.Full := by dsimp; infer_instance
instance : (R C).functor.Full := by dsimp; infer_instance
instance : (L C).functor.EssSurj := by dsimp; infer_instance
instance : (R C).functor.EssSurj := by dsimp; infer_instance

def iso : (CochainComplex.Minus.localizerMorphism C).functor ‚ãô
  (R C).functor ‚âÖ (L C).functor ‚ãô (localizerMorphism C).functor := Iso.refl _

open HomologicalComplex in
instance : TwoSquare.GuitartExact (iso C).inv :=
  TwoSquare.GuitartExact.quotient (iso C).symm (by
    rintro ‚ü®K‚ÇÅ, n‚ÇÅ, hn‚ÇÅ‚ü© ‚ü®K‚ÇÇ, n‚ÇÇ, hn‚ÇÇ‚ü© f‚ÇÄ f‚ÇÅ hf
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÄ
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÅ
    dsimp [Functor.mapCochainComplexMinus] at f‚ÇÄ f‚ÇÅ
    refine ‚ü®‚ü®K‚ÇÅ.cylinder, CochainComplex.minus_cylinder _ ‚ü®_, hn‚ÇÅ‚ü©‚ü©,
      ObjectProperty.homMk (cylinder.Œπ‚ÇÄ _),
      ObjectProperty.homMk (cylinder.Œπ‚ÇÅ _), ?_,
      ObjectProperty.homMk ?_, ?_, ?_‚ü©
    ¬∑ ext : 1
      exact eq_of_homotopy _ _ (cylinder.homotopy‚ÇÄ‚ÇÅ _ (fun n ‚Ü¶ ‚ü®n - 1, by simp‚ü©))
    ¬∑ exact (cylinder.mapHomologicalComplexObjIso K‚ÇÅ (Projectives.Œπ C)
          (fun n ‚Ü¶ ‚ü®n - 1, by simp‚ü©)).hom ‚â´
        cylinder.desc f‚ÇÄ f‚ÇÅ (homotopyOfEq _ _ ((HomotopyCategory.Minus.Œπ C).congr_map hf))
    ¬∑ dsimp [Functor.mapCochainComplexMinus]
      cat_disch
    ¬∑ dsimp [Functor.mapCochainComplexMinus]
      cat_disch)

end isLeftDerivabilityStructure

variable [EnoughProjectives C]

instance isLeftDerivabilityStructure : (localizerMorphism C).IsLeftDerivabilityStructure :=
  LocalizerMorphism.isLeftDerivabilityStructure_of_isLocalizedEquivalence
    (isLeftDerivabilityStructure.iso C)

end HomotopyCategory.Minus
