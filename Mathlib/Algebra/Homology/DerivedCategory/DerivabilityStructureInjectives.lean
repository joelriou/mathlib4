/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.DerivedCategory.Plus
public import Mathlib.Algebra.Homology.DerivedCategory.KInjective
public import Mathlib.Algebra.Homology.HomotopyFiber
public import Mathlib.Algebra.Homology.ModelCategory.Injective
public import Mathlib.AlgebraicTopology.ModelCategory.DerivabilityStructureFibrant
public import Mathlib.CategoryTheory.GuitartExact.Quotient
public import Mathlib.CategoryTheory.ObjectProperty.HomologicalComplex
public import Mathlib.CategoryTheory.Localization.DerivabilityStructure.OfLocalizedEquivalences
public import Mathlib.CategoryTheory.Localization.DerivabilityStructure.Derives

/-!
# The projective derivability structure

-/

@[expose] public section

universe w‚ÇÅ w‚ÇÇ

open HomotopicalAlgebra CategoryTheory Limits ZeroObject Category

variable (C : Type*) [Category C] [Abelian C]
  {H : Type*} [Category H]

namespace CategoryTheory

abbrev InjectiveObject := ObjectProperty.FullSubcategory (fun (X : C) => Injective X)

namespace InjectiveObject

instance closedUnderLimitsOfShapeDiscrete (J : Type*) :
    ObjectProperty.IsClosedUnderLimitsOfShape (fun (X : C) => Injective X) (Discrete J) where
  limitsOfShape_le := by
    rintro Y ‚ü®p‚ü©
    have : HasLimit p.diag := ‚ü®_, p.isLimit‚ü©
    let X := fun j => p.diag.obj ‚ü®j‚ü©
    let e := Discrete.natIsoFunctor (F := p.diag)
    have : HasProduct X := hasLimit_of_iso e
    have : HasLimit (Discrete.functor (p.diag.obj ‚àò Discrete.mk)) := by
      change HasProduct X
      infer_instance
    have : ‚àÄ j, Injective (X j) := fun j => p.prop_diag_obj ‚ü®j‚ü©
    have e' : ‚àè·∂ú X ‚âÖ Y := IsLimit.conePointUniqueUpToIso (limit.isLimit _)
      ((IsLimit.postcomposeHomEquiv e _).symm p.isLimit)
    exact Injective.of_iso e' inferInstance

instance : HasFiniteProducts (InjectiveObject C) where
  out n := by infer_instance

instance : HasFiniteBiproducts (InjectiveObject C) :=
  HasFiniteBiproducts.of_hasFiniteProducts

instance : HasBinaryBiproducts (InjectiveObject C) := hasBinaryBiproducts_of_finite_biproducts _

instance : HasZeroObject (InjectiveObject C) where
  zero := by
    refine ‚ü®‚ü®0, inferInstance‚ü©, ?_‚ü©
    rw [IsZero.iff_id_eq_zero]
    ext : 1
    apply id_zero

abbrev Œπ : InjectiveObject C ‚•§ C := ObjectProperty.Œπ _

instance (X : InjectiveObject C) : Injective ((Œπ C).obj X) := X.2

instance (X : InjectiveObject C) : Injective X.obj := X.2

instance (X : HomotopyCategory.Plus (InjectiveObject C)) (n : ‚Ñ§) :
    Injective (((Œπ C).mapHomotopyCategoryPlus.obj X).obj.as.X n) := by
  change Injective ((Œπ C).obj (X.obj.as.X n))
  infer_instance

instance (K : CochainComplex.Plus (InjectiveObject C)) :
    CochainComplex.IsKInjective
      (((InjectiveObject.Œπ C).mapHomologicalComplex (.up ‚Ñ§)).obj K.obj) := by
  obtain ‚ü®K, n, hn‚ü© := K
  let L := ((InjectiveObject.Œπ C).mapHomologicalComplex (.up ‚Ñ§)).obj K
  have (n : ‚Ñ§) : Injective (L.X n) := by dsimp [L]; infer_instance
  exact CochainComplex.isKInjective_of_injective L n

end InjectiveObject

end CategoryTheory

namespace CochainComplex.Plus

variable {C}

lemma exists_injective_resolution [EnoughInjectives C]
    (K : CochainComplex.Plus C) (n : ‚Ñ§) [K.obj.IsStrictlyGE n] :
    ‚àÉ (L : CochainComplex.Plus (InjectiveObject C)) (_ : L.obj.IsStrictlyGE n)
      (i : K ‚ü∂ (InjectiveObject.Œπ C).mapCochainComplexPlus.obj L),
        quasiIso C i := by
  obtain ‚ü®L, i, _, _, _‚ü© := CochainComplex.exists_injective_resolution K.obj n
  let L' : CochainComplex (InjectiveObject C) ‚Ñ§ :=
    HomologicalComplex.liftObjectProperty _ L inferInstance
  have hL' : L'.IsStrictlyGE n := by
    rw [CochainComplex.isStrictlyGE_iff]
    intro i hi
    rw [IsZero.iff_id_eq_zero]
    ext
    exact (L.isZero_of_isStrictlyGE n i).eq_of_src _ _
  exact ‚ü®‚ü®L', n, hL'‚ü©, hL', ObjectProperty.homMk i, by assumption‚ü©

end CochainComplex.Plus

namespace DerivedCategory.Plus

variable {C} [HasDerivedCategory C]

lemma exists_injective_resolution [EnoughInjectives C] (K : DerivedCategory.Plus C)
    (n : ‚Ñ§) [K.IsGE n] :
    ‚àÉ (L : CochainComplex.Plus (InjectiveObject C)) (_ : L.obj.IsStrictlyGE n),
      Nonempty (DerivedCategory.Plus.Q.obj
        ((InjectiveObject.Œπ C).mapCochainComplexPlus.obj L) ‚âÖ K) := by
  have : K.obj.IsGE n := (K.isGE_Œπ_obj_iff n).2 (by assumption)
  obtain ‚ü®L, _, ‚ü®e‚ü©‚ü© := DerivedCategory.exists_iso_Q_obj_of_isGE K.obj n
  obtain ‚ü®M, _, i, hi‚ü© :=
    CochainComplex.Plus.exists_injective_resolution ‚ü®L, ‚ü®n, inferInstance‚ü©‚ü© n
  have : QuasiIso i.hom := by assumption
  exact ‚ü®M, inferInstance,
    ‚ü®DerivedCategory.Plus.Œπ.preimageIso ((asIso (DerivedCategory.Q.map i.hom)).symm ‚â™‚â´ e.symm)‚ü©‚ü©

end DerivedCategory.Plus

namespace CochainComplex.Plus

@[simps]
def localizerMorphism :
    LocalizerMorphism ((quasiIso C).inverseImage (InjectiveObject.Œπ C).mapCochainComplexPlus)
      (quasiIso C) where
  functor := (InjectiveObject.Œπ C).mapCochainComplexPlus
  map := by rfl

instance : (localizerMorphism C).IsInduced where
  inverseImage_eq := rfl

instance (K : Plus (InjectiveObject C)) (n : ‚Ñ§) :
    Injective (K.obj.X n).obj :=
  (K.obj.X n).property

variable [EnoughInjectives C]

open modelCategoryQuillen

instance (K : FibrantObject (Plus C)) (n : ‚Ñ§) :
    Injective (K.obj.obj.X n) := by
  obtain ‚ü®K, hK‚ü© := K
  rw [fibrantObjects, modelCategoryQuillen.isFibrant_iff] at hK
  dsimp
  infer_instance

def fibrantObjectEquivalence :
    Plus (InjectiveObject C) ‚âå FibrantObject (Plus C) where
  functor := ObjectProperty.lift _ (InjectiveObject.Œπ C).mapCochainComplexPlus (fun K ‚Ü¶ by
    dsimp [fibrantObjects]
    rw [modelCategoryQuillen.isFibrant_iff]
    intro n
    dsimp
    infer_instance)
  inverse := ObjectProperty.lift _
    (HomologicalComplex.liftFunctorObjectProperty _ (FibrantObject.Œπ ‚ãô Plus.Œπ C)
      (fun K n ‚Ü¶ by dsimp; infer_instance)) (by
        rintro ‚ü®‚ü®K, n, hn‚ü©, _‚ü©
        refine ‚ü®n, ?_‚ü©
        rw [isStrictlyGE_iff]
        intro i hi
        rw [IsZero.iff_id_eq_zero]
        ext
        apply (K.isZero_of_isStrictlyGE n i hi).eq_of_tgt)
  unitIso := Iso.refl _
  counitIso := Iso.refl _

@[simps]
def fibrantObjectLocalizerMorphism :
    LocalizerMorphism ((quasiIso C).inverseImage (InjectiveObject.Œπ C).mapCochainComplexPlus)
      (weakEquivalences (FibrantObject (Plus C))) where
  functor := (fibrantObjectEquivalence C).functor
  map := by rfl

instance : (fibrantObjectLocalizerMorphism C).IsInduced where
  inverseImage_eq := rfl

instance : (fibrantObjectLocalizerMorphism C).functor.IsEquivalence := by
  dsimp; infer_instance

instance : (localizerMorphism C).IsRightDerivabilityStructure := by
  rw [LocalizerMorphism.isRightDerivabilityStructure_iff_of_equivalences
    (T := localizerMorphism C) (B := (FibrantObject.localizerMorphism (Plus C)))
    (R := .id _) (L := fibrantObjectLocalizerMorphism C) (Iso.refl _)]
  infer_instance

end CochainComplex.Plus

namespace HomotopyCategory.Plus

@[simps]
def localizerMorphism : LocalizerMorphism
  (MorphismProperty.isomorphisms (HomotopyCategory.Plus (InjectiveObject C)))
    (HomotopyCategory.Plus.quasiIso C) where
  functor := (InjectiveObject.Œπ C).mapHomotopyCategoryPlus
  map K L f (hf : IsIso f) := by
    dsimp only [MorphismProperty.inverseImage, HomotopyCategory.Plus.quasiIso]
    rw [HomotopyCategory.mem_quasiIso_iff]
    intro n
    infer_instance

variable {C} in
lemma isIso_quotient_map
    {K L : CochainComplex.Plus (InjectiveObject C)} (f : K ‚ü∂ L) :
    IsIso ((quotient _).map f) ‚Üî
    CochainComplex.Plus.quasiIso C ((InjectiveObject.Œπ C).mapCochainComplexPlus.map f) := by
  rw [‚Üê isIso_iff_of_reflects_iso _ (HomotopyCategory.Plus.Œπ (InjectiveObject C)),
    ‚Üê isIso_iff_of_reflects_iso _ (Functor.mapHomotopyCategory (InjectiveObject.Œπ C) (.up ‚Ñ§))]
  dsimp
  rw [CochainComplex.IsKInjective.isIso_quotient_map_iff_quasiIso]
  rfl

namespace isRightDerivabilityStructure

open MorphismProperty

@[simps]
def L : LocalizerMorphism
  ((CochainComplex.Plus.quasiIso C).inverseImage (InjectiveObject.Œπ C).mapCochainComplexPlus)
      (isomorphisms (Plus (InjectiveObject C))) where
  functor := HomotopyCategory.Plus.quotient (InjectiveObject C)
  map _ _ f hf := (isIso_quotient_map f).2 hf

instance : (L C).IsInduced where
  inverseImage_eq := by ext; apply isIso_quotient_map

@[simps]
def R : LocalizerMorphism (CochainComplex.Plus.quasiIso C) (quasiIso C) where
  functor := HomotopyCategory.Plus.quotient C
  map := by
    intro X Y f hf
    simpa [quasiIso, quotient_map_mem_quasiIso_iff]

instance : (R C).IsInduced where
  inverseImage_eq := by ext; apply quotient_map_mem_quasiIso_iff

open HomologicalComplex in
lemma inverseImage_quasiIso_mapCochainComplexPlus_injectivesŒπ :
    (CochainComplex.Plus.quasiIso C).inverseImage (InjectiveObject.Œπ C).mapCochainComplexPlus =
    (homotopyEquivalences (InjectiveObject C) (ComplexShape.up ‚Ñ§)).inverseImage
      (CochainComplex.Plus.Œπ (InjectiveObject C)) := by
  ext K L f
  simp [CochainComplex.Plus.quasiIso, Functor.mapCochainComplexPlus,
    ‚Üê CochainComplex.IsKInjective.isIso_quotient_map_iff_quasiIso,
    ‚Üê isIso_quotient_map_iff_homotopyEquivalences,
    ‚Üê isIso_iff_of_reflects_iso _ ((InjectiveObject.Œπ C).mapHomotopyCategory (.up ‚Ñ§))]

instance : (HomotopyCategory.Plus.quotient (InjectiveObject C)).IsLocalization
      ((CochainComplex.Plus.quasiIso C).inverseImage
      (InjectiveObject.Œπ C).mapCochainComplexPlus) := by
  rw [inverseImage_quasiIso_mapCochainComplexPlus_injectivesŒπ]
  infer_instance

instance : (L C).IsLocalizedEquivalence := by
  have :
      ((L C).functor ‚ãô ùü≠ (Plus (InjectiveObject C))).IsLocalization
        ((CochainComplex.Plus.quasiIso C).inverseImage
          (InjectiveObject.Œπ C).mapCochainComplexPlus) :=
    inferInstanceAs ((HomotopyCategory.Plus.quotient (InjectiveObject C)).IsLocalization _)
  exact LocalizerMorphism.IsLocalizedEquivalence.of_isLocalization_of_isLocalization (L C) (ùü≠ _)

open HomologicalComplex in
instance {D : Type*} [Category* D] (L : Plus C ‚•§ D) [L.IsLocalization (quasiIso C)] :
    (quotient C ‚ãô L).IsLocalization (CochainComplex.Plus.quasiIso C) := by
  refine Functor.IsLocalization.comp _ _
    ((homotopyEquivalences C (.up ‚Ñ§)).inverseImage (CochainComplex.Plus.Œπ C))
    (quasiIso C) _ ?_ ?_ ?_
  ¬∑ intro _ _ f hf
    refine Localization.inverts L (quasiIso C) _ ?_
    simpa [quasiIso, quotient_map_mem_quasiIso_iff]
  ¬∑ intro K L f hf
    exact homotopyEquivalences_le_quasiIso _ _ _ hf
  ¬∑ rintro K L f hf
    obtain ‚ü®K, rfl‚ü© := Plus.quotient_obj_surjective K
    obtain ‚ü®L, rfl‚ü© := Plus.quotient_obj_surjective L
    obtain ‚ü®f, rfl‚ü© := (Plus.quotient C).map_surjective f
    apply MorphismProperty.map_mem_map
    simpa [quasiIso, HomotopyCategory.quotient_map_mem_quasiIso_iff] using hf

instance {D : Type*} [Category* D] (L : Plus C ‚•§ D) [L.IsLocalization (quasiIso C)] :
    ((R C).functor ‚ãô L).IsLocalization (CochainComplex.Plus.quasiIso C) := by
  dsimp; infer_instance

instance : (R C).IsLocalizedEquivalence :=
  LocalizerMorphism.IsLocalizedEquivalence.of_isLocalization_of_isLocalization
    (R C) ((quasiIso C).Q)

instance : (L C).functor.Full := by dsimp; infer_instance
instance : (R C).functor.Full := by dsimp; infer_instance
instance : (L C).functor.EssSurj := by dsimp; infer_instance
instance : (R C).functor.EssSurj := by dsimp; infer_instance

def iso : (CochainComplex.Plus.localizerMorphism C).functor ‚ãô
  (R C).functor ‚âÖ (L C).functor ‚ãô (localizerMorphism C).functor := Iso.refl _

open HomologicalComplex CochainComplex in
instance : TwoSquare.GuitartExact (iso C).hom :=
  TwoSquare.GuitartExact.quotient' (iso C).symm (by
    rintro ‚ü®K‚ÇÅ, n‚ÇÅ, hn‚ÇÅ‚ü© ‚ü®K‚ÇÇ, n‚ÇÇ, hn‚ÇÇ‚ü© f‚ÇÄ f‚ÇÅ hf
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÄ
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÅ
    dsimp [Functor.mapCochainComplexPlus] at f‚ÇÄ f‚ÇÅ
    refine ‚ü®‚ü®K‚ÇÇ.pathObject, CochainComplex.plus_pathObject K‚ÇÇ ‚ü®n‚ÇÇ, hn‚ÇÇ‚ü©‚ü©,
      ObjectProperty.homMk (pathObject.œÄ‚ÇÄ K‚ÇÇ),
      ObjectProperty.homMk (pathObject.œÄ‚ÇÅ K‚ÇÇ),
      ?_, ObjectProperty.homMk ?_, ?_‚ü©
    ¬∑ ext : 1
      exact eq_of_homotopy _ _ (pathObject.homotopy‚ÇÄ‚ÇÅ _ (fun n ‚Ü¶ ‚ü®n + 1, by simp‚ü©))
    ¬∑ exact pathObject.lift f‚ÇÄ f‚ÇÅ (homotopyOfEq _ _ ((HomotopyCategory.Plus.Œπ C).congr_map hf)) ‚â´
        (pathObject.mapHomologicalComplexObjIso K‚ÇÇ (InjectiveObject.Œπ C)
          (fun n ‚Ü¶ ‚ü®n + 1, by simp‚ü©)).inv
    ¬∑ dsimp [Functor.mapCochainComplexPlus]
      cat_disch)

end isRightDerivabilityStructure

variable [EnoughInjectives C]

instance isRightDerivabilityStructure : (localizerMorphism C).IsRightDerivabilityStructure :=
  LocalizerMorphism.isRightDerivabilityStructure_of_isLocalizedEquivalence
    (isRightDerivabilityStructure.iso C)

instance : (HomotopyCategory.Plus.localizerMorphism C).arrow.HasRightResolutions := by
  intro f
  obtain ‚ü®K‚ÇÅ, K‚ÇÇ, f, rfl‚ü© := Arrow.mk_surjective f
  let r‚ÇÅ : (HomotopyCategory.Plus.localizerMorphism C).RightResolution K‚ÇÅ :=
    Classical.arbitrary _
  let r‚ÇÇ : (HomotopyCategory.Plus.localizerMorphism C).RightResolution K‚ÇÇ :=
    Classical.arbitrary _
  obtain ‚ü®œÜ, hœÜ‚ü© : ‚àÉ œÜ, r‚ÇÅ.w ‚â´ œÜ = f ‚â´ r‚ÇÇ.w := by
    obtain ‚ü®Œ±, hŒ±‚ü© :=
      (HomotopyCategory.quotient _ _).map_surjective ((HomotopyCategory.Plus.Œπ C).map r‚ÇÅ.w)
    let L := ((InjectiveObject.Œπ C).mapHomologicalComplex _).obj r‚ÇÇ.X‚ÇÅ.obj.as
    have : QuasiIso Œ± := by
      change HomologicalComplex.quasiIso C _ Œ±
      rw [‚Üê quotient_map_mem_quasiIso_iff, hŒ±]
      exact r‚ÇÅ.hw
    have (n : ‚Ñ§) : Injective (L.X n) := (r‚ÇÇ.X‚ÇÅ.obj.as.X n).property
    obtain ‚ü®d, hd‚ü© := r‚ÇÇ.X‚ÇÅ.property
    have := CochainComplex.isKInjective_of_injective L d
    obtain ‚ü®Œ≤, hŒ≤‚ü© := (CochainComplex.IsKInjective.bijective_precomp Œ± L).2
      ((HomotopyCategory.Plus.Œπ C).map (f ‚â´ r‚ÇÇ.w))
    exact ‚ü®ObjectProperty.homMk Œ≤, (Œπ C).map_injective (by rw [‚Üê hŒ≤, hŒ±]; dsimp)‚ü©
  obtain ‚ü®œÜ, rfl‚ü© := (InjectiveObject.Œπ C).mapHomotopyCategoryPlus.map_surjective œÜ
  exact ‚ü®{
    X‚ÇÅ := Arrow.mk œÜ
    w := Arrow.homMk r‚ÇÅ.w r‚ÇÇ.w hœÜ
    hw := ‚ü®r‚ÇÅ.hw, r‚ÇÇ.hw‚ü©
  }‚ü©

noncomputable instance : (HomotopyCategory.Plus.localizerMorphism C).functor.CommShift ‚Ñ§ := by
  dsimp; infer_instance

instance : (HomotopyCategory.Plus.localizerMorphism C).functor.IsTriangulated := by
  dsimp; infer_instance

instance [HasDerivedCategory C] :
    ((InjectiveObject.Œπ C).mapHomotopyCategoryPlus ‚ãô DerivedCategory.Plus.Qh).EssSurj where
  mem_essImage K := by
    let r : (HomotopyCategory.Plus.localizerMorphism C).RightResolution
        (DerivedCategory.Plus.Qh.objPreimage K) := Classical.arbitrary _
    have := Localization.inverts DerivedCategory.Plus.Qh _ _ r.hw
    exact ‚ü®r.X‚ÇÅ, ‚ü®(asIso (DerivedCategory.Plus.Qh.map r.w)).symm ‚â™‚â´
      DerivedCategory.Plus.Qh.objObjPreimageIso K‚ü©‚ü©

section

variable (F : HomotopyCategory.Plus C ‚•§ H)

variable {C} in
omit [EnoughInjectives C] in
lemma localizerMorphism_derives : (localizerMorphism C).Derives F :=
  MorphismProperty.isomorphisms_isInvertedBy _

/-- Any functor the homotopy category `K^+` has a right derived functor with respect
to quasi-isomorphisms. -/
instance : F.HasPointwiseRightDerivedFunctor (HomotopyCategory.Plus.quasiIso C) :=
  (localizerMorphism_derives F).hasPointwiseRightDerivedFunctor

variable [HasDerivedCategory C]
variable (F' : DerivedCategory.Plus C ‚•§ H) (Œ± : F ‚ü∂ DerivedCategory.Plus.Qh ‚ãô F')
  [F'.IsRightDerivedFunctor Œ± (HomotopyCategory.Plus.quasiIso C)]

instance (K : HomotopyCategory.Plus C) [(‚àÄ (n : ‚Ñ§), Injective (K.obj.as.X n))] :
    IsIso (Œ±.app K) := by
  have (Y : HomotopyCategory.Plus (InjectiveObject C)) :
      IsIso (Œ±.app ((InjectiveObject.Œπ C).mapHomotopyCategoryPlus.obj Y)) :=
    (localizerMorphism_derives F).isIso_of_isRightDerivedFunctor _ _
  obtain ‚ü®Y, ‚ü®e‚ü©‚ü© : (InjectiveObject.Œπ C).mapHomotopyCategoryPlus.essImage K :=
    ‚ü®(quotient _).obj
      ((CochainComplex.Plus.fibrantObjectEquivalence C).inverse.obj ‚ü®‚ü®_, K.property‚ü©, by
          dsimp [fibrantObjects]
          rw [CochainComplex.Plus.modelCategoryQuillen.isFibrant_iff]
          infer_instance‚ü©), ‚ü®Iso.refl _‚ü©‚ü©
  rw [‚Üê NatTrans.isIso_app_iff_of_iso Œ± e]
  infer_instance

example (X : HomotopyCategory.Plus (InjectiveObject C)) :
    IsIso ((F.totalRightDerivedUnit DerivedCategory.Plus.Qh
      (HomotopyCategory.Plus.quasiIso C)).app
        ((InjectiveObject.Œπ C).mapHomotopyCategoryPlus.obj X)) := by
  infer_instance

end

end HomotopyCategory.Plus
