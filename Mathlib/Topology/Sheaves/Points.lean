/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Sites.Point.Conservative
public import Mathlib.Topology.Sheaves.Sheaf

/-!
# The standard conservative families of points for the site attached to a topological space

If `X` is a topological space, any `x : X` defined a point of the site
attached to `X`.

## TODO

* Redefine the stalks functors in `Mathlib/Topology/Sheaves/Stalks.lean`
using `GrothendieckTopology.Point.presheafFiber`.

-/

@[expose] public section

universe u

namespace Opens

open CategoryTheory GrothendieckTopology

variable (X : Type u) [TopologicalSpace X] (x : X)

variable {X} in
/-- Given a topological space `X` and `x : X`, this is the point of the site
`(Opens X, Opens.grothendieckTopology X)` corresponding to `x`. -/
def pointGrothendieckTopology : Point.{u} (grothendieckTopology X) where
  fiber.obj U := ULift.{u} (PLift (x ‚àà U))
  fiber.map f h := ‚ü®‚ü®leOfHom f h.down.down‚ü©‚ü©
  isCofiltered :=
    { nonempty := ‚ü®‚ä§, ‚ü®‚ü®by simp‚ü©‚ü©‚ü©
      cone_objs := by
        rintro ‚ü®U, ‚ü®‚ü®hU‚ü©‚ü©‚ü© ‚ü®V, ‚ü®‚ü®hV‚ü©‚ü©‚ü©
        exact ‚ü®‚ü®U ‚äì V, ‚ü®‚ü®‚ü®hU, hV‚ü©‚ü©‚ü©‚ü©, ‚ü®homOfLE (by simp), rfl‚ü©,
          ‚ü®homOfLE (by simp), rfl‚ü©, ‚ü®‚ü©‚ü©
      cone_maps _ _ _ _ := ‚ü®_, ùüô _, rfl‚ü© }
  initiallySmall := initiallySmall_of_essentiallySmall _
  jointly_surjective := by
    rintro U R hR ‚ü®‚ü®hU‚ü©‚ü©
    obtain ‚ü®V, f, hf, hV‚ü© := hR x hU
    exact ‚ü®_, _, hf, ‚ü®‚ü®hV‚ü©‚ü©, rfl‚ü©

/-- When `X` is a topological space, this is the obvious conservative family of
points on the site `(Opens X, Opens.grothendieckTopology X)`. -/
def pointsGrothendieckTopology : ObjectProperty (Point.{u} (grothendieckTopology X)) :=
  ObjectProperty.ofObj pointGrothendieckTopology

lemma isConservative_pointsGrothendieckTopology :
    (pointsGrothendieckTopology X).IsConservativeFamilyOfPoints :=
  .mk' (fun U S hS x hx ‚Ü¶ by
    obtain ‚ü®V, f, hf, ‚ü®‚ü®hV‚ü©‚ü©, _‚ü© := hS ‚ü®_, ‚ü®x‚ü©‚ü© ‚ü®‚ü®hx‚ü©‚ü©
    exact ‚ü®V, f, hf, hV‚ü©)

end Opens
