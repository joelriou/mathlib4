/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.EssentiallySmall
import Mathlib.SetTheory.Cardinal.Basic

/-!
# Representatives of essentially small categories

-/

universe w v u

namespace CategoryTheory

variable (Œ© : Type w)

/-- Structure which allows to construct a category whose types
of objects and morphisms are subtype of a fixed type `Œ©`. -/
structure SmallCategoryOfSet where
  obj : Set Œ©
  hom (X Y : obj) : Set Œ©
  id (X : obj) : hom X X
  comp {X Y Z : obj} (f : hom X Y) (g : hom Y Z) : hom X Z
  id_comp {X Y : obj} (f : hom X Y) : comp (id _) f = f := by cat_disch
  comp_id {X Y : obj} (f : hom X Y) : comp f (id _) = f := by cat_disch
  assoc {X Y Z T : obj} (f : hom X Y) (g : hom Y Z) (h : hom Z T) :
      comp (comp f g) h = comp f (comp g h) := by cat_disch

namespace SmallCategoryOfSet

attribute [simp] id_comp comp_id assoc

@[simps]
instance (S : SmallCategoryOfSet Œ©) : SmallCategory S.obj where
  Hom X Y := S.hom X Y
  id := S.id
  comp := S.comp

abbrev categoryFamily : SmallCategoryOfSet Œ© ‚Üí Type w := fun S ‚Ü¶ S.obj

end SmallCategoryOfSet

variable (C : Type u) [Category.{v} C]

structure CoreSmallCategoryOfSet where
  obj : Set Œ©
  hom (X Y : obj) : Set Œ©
  objEquiv : obj ‚âÉ C
  homEquiv {X Y : obj} : hom X Y ‚âÉ (objEquiv X ‚ü∂ objEquiv Y)

namespace CoreSmallCategoryOfSet

variable {Œ© C} (h : CoreSmallCategoryOfSet Œ© C)

/-- The `SmallCategoryOfSet` structure induced by a
`CoreSmallCategoryOfSet` structure. -/
@[simps]
def smallCategoryOfSet : SmallCategoryOfSet Œ© where
  obj := h.obj
  hom := h.hom
  id X := h.homEquiv.symm (ùüô _)
  comp f g := h.homEquiv.symm (h.homEquiv f ‚â´ h.homEquiv g)

@[simps!]
def functor : h.smallCategoryOfSet.obj ‚•§ C where
  obj := h.objEquiv
  map := h.homEquiv
  map_id _ := by
    rw [SmallCategoryOfSet.id_def]
    simp
  map_comp _ _ := by
    rw [SmallCategoryOfSet.comp_def]
    simp

def fullyFaithfulFunctor : h.functor.FullyFaithful where
  preimage := h.homEquiv.symm

instance : h.functor.Faithful := h.fullyFaithfulFunctor.faithful

instance : h.functor.Full := h.fullyFaithfulFunctor.full

instance : h.functor.EssSurj where
  mem_essImage Y := by
    obtain ‚ü®X, rfl‚ü© := h.objEquiv.surjective Y
    exact ‚ü®_, ‚ü®Iso.refl _‚ü©‚ü©

instance : h.functor.IsEquivalence where

noncomputable def equivalence : h.smallCategoryOfSet.obj ‚âå C :=
  h.functor.asEquivalence

end CoreSmallCategoryOfSet

namespace SmallCategoryOfSet

lemma exists_equivalence (C : Type w) [Category.{w} C]
    (h‚ÇÅ : Cardinal.mk C ‚â§ Cardinal.mk Œ©)
    (h‚ÇÇ : ‚àÄ (X Y : C), Cardinal.mk (X ‚ü∂ Y) ‚â§ Cardinal.mk Œ©) :
    ‚àÉ (h : SmallCategoryOfSet Œ©), Nonempty (categoryFamily Œ© h ‚âå C) := by
  obtain ‚ü®f‚ÇÅ, hf‚ÇÅ‚ü© := h‚ÇÅ
  let f‚ÇÇ (X Y) := (h‚ÇÇ X Y).some
  let e := Equiv.ofInjective f‚ÇÅ hf‚ÇÅ
  let h : CoreSmallCategoryOfSet Œ© C :=
    { obj := Set.range f‚ÇÅ
      hom X Y := Set.range (f‚ÇÇ (e.symm X) (e.symm Y))
      objEquiv := e.symm
      homEquiv {_ _} := by simpa using (Equiv.ofInjective _ ((f‚ÇÇ _ _).injective)).symm }
  exact ‚ü®h.smallCategoryOfSet, ‚ü®h.equivalence‚ü©‚ü©

end SmallCategoryOfSet

end CategoryTheory
