/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Homology.ShortComplex.ShortExact
public import Mathlib.CategoryTheory.Preadditive.Injective.LiftingProperties
public import Mathlib.CategoryTheory.MorphismProperty.Composition
public import Mathlib.CategoryTheory.MorphismProperty.Retract
public import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty

/-!
# Epimorphisms with an injective kernel

In this file, we define the class of morphisms `epiWithInjectiveKernel` in an
abelian category. We show that this property of morphisms is multiplicative.

This shall be used in the file `Mathlib/Algebra/Homology/Factorizations/Basic.lean` in
order to define morphisms of cochain complexes which satisfy this property
degreewise.

-/

@[expose] public section

namespace CategoryTheory

open Category Limits ZeroObject Preadditive

variable {C : Type*} [Category* C] [Abelian C]

namespace Abelian

/-- The class of morphisms in an abelian category that are epimorphisms
and have an injective kernel. -/
def epiWithInjectiveKernel : MorphismProperty C :=
  fun _ _ f => Epi f âˆ§ Injective (kernel f)

/-- The class of morphisms in an abelian category that are monomorphisms
and have a projective kernel. -/
def monoWithProjectiveCokernel : MorphismProperty C :=
  fun _ _ f => Mono f âˆ§ Projective (cokernel f)

lemma monoWithProjectiveCokernel_eq_unop :
    monoWithProjectiveCokernel (C := C) =
      epiWithInjectiveKernel.unop := by
  ext X Y f
  dsimp [monoWithProjectiveCokernel, epiWithInjectiveKernel]
  apply and_congr (by simp)
  rw [Injective.projective_iff_injective_op, Injective.iso_iff (kernelOpOp f).symm]

/-- A morphism `g : X âŸ¶ Y` is epi with an injective kernel iff there exists a morphism
`f : I âŸ¶ X` with `I` injective such that `f â‰« g = 0` and
the short complex `I âŸ¶ X âŸ¶ Y` has a splitting. -/
lemma epiWithInjectiveKernel_iff {X Y : C} (g : X âŸ¶ Y) :
    epiWithInjectiveKernel g â†” âˆƒ (I : C) (_ : Injective I) (f : I âŸ¶ X) (w : f â‰« g = 0),
      Nonempty (ShortComplex.mk f g w).Splitting := by
  constructor
  Â· rintro âŸ¨_, _âŸ©
    let S := ShortComplex.mk (kernel.Î¹ g) g (by simp)
    exact âŸ¨_, inferInstance, _, S.zero,
      âŸ¨ShortComplex.Splitting.ofExactOfRetraction S
        (S.exact_of_f_is_kernel (kernelIsKernel g)) (Injective.factorThru (ğŸ™ _) (kernel.Î¹ g))
        (by simp [S]) inferInstanceâŸ©âŸ©
  Â· rintro âŸ¨I, _, f, w, âŸ¨ÏƒâŸ©âŸ©
    have : IsSplitEpi g := âŸ¨Ïƒ.s, Ïƒ.s_gâŸ©
    let e : I â‰… kernel g :=
      IsLimit.conePointUniqueUpToIso Ïƒ.shortExact.fIsKernel (limit.isLimit _)
    exact âŸ¨inferInstance, Injective.of_iso e inferInstanceâŸ©

lemma epiWithInjectiveKernel_of_iso {X Y : C} (f : X âŸ¶ Y) [IsIso f] :
    epiWithInjectiveKernel f := by
  rw [epiWithInjectiveKernel_iff]
  exact âŸ¨0, inferInstance, 0, by simp,
    âŸ¨ShortComplex.Splitting.ofIsZeroOfIsIso _ (isZero_zero C) (by assumption)âŸ©âŸ©

instance : (epiWithInjectiveKernel : MorphismProperty C).IsMultiplicative where
  id_mem _ := epiWithInjectiveKernel_of_iso _
  comp_mem {X Y Z} gâ‚ gâ‚‚ hgâ‚ hgâ‚‚ := by
    rw [epiWithInjectiveKernel_iff] at hgâ‚ hgâ‚‚ âŠ¢
    obtain âŸ¨Iâ‚, _, fâ‚, wâ‚, âŸ¨Ïƒâ‚âŸ©âŸ© := hgâ‚
    obtain âŸ¨Iâ‚‚, _, fâ‚‚, wâ‚‚, âŸ¨Ïƒâ‚‚âŸ©âŸ© := hgâ‚‚
    refine âŸ¨Iâ‚ âŠ Iâ‚‚, inferInstance, biprod.fst â‰« fâ‚ + biprod.snd â‰« fâ‚‚ â‰« Ïƒâ‚.s, ?_, âŸ¨?_âŸ©âŸ©
    Â· ext
      Â· simp [reassoc_of% wâ‚]
      Â· simp [reassoc_of% Ïƒâ‚.s_g, wâ‚‚]
    Â· exact
        { r := Ïƒâ‚.r â‰« biprod.inl + gâ‚ â‰« Ïƒâ‚‚.r â‰« biprod.inr
          s := Ïƒâ‚‚.s â‰« Ïƒâ‚.s
          f_r := by
            ext
            Â· simp [Ïƒâ‚.f_r]
            Â· simp [reassoc_of% wâ‚]
            Â· simp
            Â· simp [reassoc_of% Ïƒâ‚.s_g, Ïƒâ‚‚.f_r]
          s_g := by simp [reassoc_of% Ïƒâ‚.s_g, Ïƒâ‚‚.s_g]
          id := by
            dsimp
            have h := gâ‚ â‰«= Ïƒâ‚‚.id =â‰« Ïƒâ‚.s
            simp only [add_comp, assoc, comp_add, id_comp] at h
            rw [â† Ïƒâ‚.id, â† h]
            simp only [comp_add, add_comp, assoc, BinaryBicone.inl_fst_assoc,
              BinaryBicone.inr_fst_assoc, zero_comp, comp_zero, add_zero,
              BinaryBicone.inl_snd_assoc, BinaryBicone.inr_snd_assoc, zero_add]
            abel }

instance : (epiWithInjectiveKernel (C := C)).IsStableUnderRetracts where
  of_retract := by
    rintro X' Y' X Y f' f r âŸ¨_, hfâŸ©
    have : Epi f' :=
      (MorphismProperty.epimorphisms C).of_retract r (.infer_property _)
    let r' : Retract (kernel f') (kernel f) :=
      { i := kernel.map _ _ r.left.i r.right.i (by simp)
        r := kernel.map _ _ r.left.r r.right.r (by simp) }
    exact âŸ¨inferInstance, r'.injectiveâŸ©

lemma epiWithInjectiveKernel.hasLiftingProperty
    {X Y : C} {p : X âŸ¶ Y} (hp : epiWithInjectiveKernel p)
    {A B : C} (i : A âŸ¶ B) [Mono i] :
    HasLiftingProperty i p := by
  suffices (MorphismProperty.monomorphisms C).rlp p from this _ (.infer_property _)
  rw [epiWithInjectiveKernel_iff] at hp
  obtain âŸ¨I, _, s, hs, âŸ¨ÏƒâŸ©âŸ© := hp
  have hI : (MorphismProperty.monomorphisms C).rlp (0 : I âŸ¶ 0) := by
    intro A B i (hi : Mono i)
    exact Injective.hasLiftingProperty_of_isZero _ _ (isZero_zero C)
  refine MorphismProperty.of_isPullback (f' := Ïƒ.r) (f := 0) âŸ¨by simp, âŸ¨?_âŸ©âŸ© hI
  refine PullbackCone.IsLimit.mk _ (fun t â†¦ t.fst â‰« s + t.snd â‰« Ïƒ.s)
    (fun t â†¦ ?_) (fun t â†¦ ?_) (fun t m hmâ‚ hmâ‚‚ â†¦ ?_)
  Â· have := Ïƒ.f_r
    dsimp at this âŠ¢
    simp [this]
  Â· have := Ïƒ.s_g
    dsimp
    simp [hs, this]
  Â· have := Ïƒ.id
    dsimp at this âŠ¢
    simp only [â† hmâ‚, â† hmâ‚‚, Category.assoc, â† Preadditive.comp_add, this, comp_id]

instance : (monoWithProjectiveCokernel : MorphismProperty C).IsMultiplicative := by
  rw [monoWithProjectiveCokernel_eq_unop]
  infer_instance

instance : (monoWithProjectiveCokernel : MorphismProperty C).IsStableUnderRetracts := by
  rw [monoWithProjectiveCokernel_eq_unop]
  infer_instance

end Abelian

end CategoryTheory
