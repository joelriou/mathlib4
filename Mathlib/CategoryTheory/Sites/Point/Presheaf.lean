/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Sites.Point.Conservative

/-!
# Points of presheaf toposes

Let `C` be a category. For the Grothendieck topology `‚ä•`, we know
that the category of sheaves with values in `A` identify to `C·µí·µñ ‚•§ A`
(see `sheafBotEquivalence` in the file `Mathlib/CategoryTheory/Sites/Sheaf.lean`).
In this file, we show that any `X : C` defines a point for this site, and that
these point form a conservative family of points.

## TODO
* show that the fiber functors identify the evaluation functors

-/

@[expose] public section

universe w v u

namespace CategoryTheory

open Opposite Limits

variable {C : Type u} [Category.{v} C] [LocallySmall.{w} C]

-- to be moved
/-- The object of the category of elements `shrinkYoneda.{w}.flip.obj (op X)`
corresponding to the identity of `X` is initial. -/
noncomputable def isInitialElementsMkShrinkYonedaObjObjEquivId (X : C) :
    IsInitial (Functor.elementsMk (shrinkYoneda.{w}.flip.obj (op X)) X
      (shrinkYonedaObjObjEquiv.symm (ùüô X))) :=
  IsInitial.ofUniqueHom (fun u ‚Ü¶ ‚ü®shrinkYonedaObjObjEquiv.{w} u.2, by
    dsimp
    rw [shrinkYoneda_map_app_shrinkYonedaObjObjEquiv_symm]
    simp‚ü©) (by
    rintro u ‚ü®m, hm‚ü©
    ext
    dsimp at hm ‚ä¢
    rw [‚Üê hm, shrinkYoneda_map_app_shrinkYonedaObjObjEquiv_symm]
    simp)

namespace GrothendieckTopology

instance (X : C) : HasInitial (shrinkYoneda.{w}.flip.obj (op X)).Elements :=
  (isInitialElementsMkShrinkYonedaObjObjEquivId X).hasInitial

/-- If `X` is an object of `C`, this is the point of the site `(C, ‚ä•)` (whose
sheaves are presheaves, see `sheafBotEquivalence`) corresponding to `X`. -/
@[simps]
noncomputable def pointBot (X : C) :
    Point.{w} (‚ä• : GrothendieckTopology C) where
  fiber := shrinkYoneda.flip.{w}.obj (op X)
  jointly_surjective {U} R hR x := by
    obtain rfl : R = ‚ä§ := by simpa using hR
    exact ‚ü®U, ùüô _, by simp, x, by simp‚ü©

variable (C) in
/-- The family of points on the site `(C, ‚ä•)` (whose
sheaves are presheaves, see `sheafBotEquivalence`) given by the objects of `X`. -/
noncomputable def pointsBot :
    ObjectProperty (Point.{w} (‚ä• : GrothendieckTopology C)) :=
  .ofObj pointBot

instance : (pointsBot.{w} C).IsConservativeFamilyOfPoints :=
  ObjectProperty.IsConservativeFamilyOfPoints.mk'.{w} (fun X S hS ‚Ü¶ by
    obtain ‚ü®Y, a, ha, b, hb‚ü© := hS ‚ü®_, ‚ü®X‚ü©‚ü© (shrinkYonedaObjObjEquiv.symm (ùüô X))
    obtain ‚ü®b, rfl‚ü© := shrinkYonedaObjObjEquiv.symm.surjective b
    dsimp at b hb
    have : b ‚â´ a = ùüô _ :=
      shrinkYonedaObjObjEquiv.symm.injective (by
        rw [‚Üê hb, shrinkYoneda_map_app_shrinkYonedaObjObjEquiv_symm])
    simpa only [bot_covering, ‚Üê Sieve.id_mem_iff_eq_top, this]
      using S.downward_closed ha b)

end GrothendieckTopology

end CategoryTheory
