/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Sites.Point.Category
public import Mathlib.CategoryTheory.Abelian.GrothendieckAxioms.Types
public import Mathlib.CategoryTheory.Functor.ReflectsIso.Jointly
public import Mathlib.CategoryTheory.Types.Epimorphisms

/-!
# Conservative families of points

Let `J` be a Grothendieck topology on a category `C`.
Let `P : ObjectProperty J.Point` be a family of points. We say that
`P` is a conservative family of points if the corresponding
fiber functors `Sheaf J (Type w) ‚•§ Type w` jointly reflect
isomorphisms. Under suitable assumptions on the coefficient
category `A`, this implies that the fiber functors
`Sheaf J A ‚•§ A` corresponding to the points in `P`
jointly reflect isomorphisms, epimorphisms and monomorphisms,
and they are also jointly faithful.

We provide a constructor `ObjectProperty.IsConservativeFamilyOfPoints.mk'`
which allows to verify that a family of points is conservative
using a condition involving covering sieves (SGA 4 IV 6.5 (a)).

## TODO
* Formalize SGA 4 IV 6.5 (a) which characterizes conservative families
of points.

-/

@[expose] public section

universe w w' v v' u u'

namespace CategoryTheory

open Limits Opposite

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  (P : ObjectProperty (GrothendieckTopology.Point.{w} J))

namespace ObjectProperty

/-- Let `P : ObjectProperty J.Point` a family of points of a
site `(C, J)`). We say that it is a conservative family of points
if the corresponding fiber functors `Sheaf J (Type w) ‚•§ Type w`
jointly reflect isomorphisms. -/
@[stacks 00YJ "(1)"]
class IsConservativeFamilyOfPoints : Prop where
  jointlyReflectIsomorphisms :
    JointlyReflectIsomorphisms
      (fun (Œ¶ : P.FullSubcategory) ‚Ü¶ Œ¶.obj.sheafFiber (A := Type w))

end ObjectProperty

namespace GrothendieckTopology.Point

variable [P.IsConservativeFamilyOfPoints]
  (A : Type u') [Category.{v'} A] [LocallySmall.{w} C]
  [HasColimitsOfSize.{w, w} A]
  {FC : A ‚Üí A ‚Üí Type*} {CC : A ‚Üí Type w}
  [‚àÄ (X Y : A), FunLike (FC X Y) (CC X) (CC Y)]
  [ConcreteCategory.{w} A FC]
  [PreservesFilteredColimitsOfSize.{w, w} (forget A)]

@[stacks 00YJ "(1)"]
lemma jointlyReflectIsomorphisms
    [J.HasSheafCompose (forget A)] [(forget A).ReflectsIsomorphisms] :
    JointlyReflectIsomorphisms
      (fun (Œ¶ : P.FullSubcategory) ‚Ü¶ Œ¶.obj.sheafFiber (A := A)) where
  isIso {K L} f _ := by
    rw [‚Üê isIso_iff_of_reflects_iso _ (sheafCompose J (forget A)),
      (ObjectProperty.IsConservativeFamilyOfPoints.jointlyReflectIsomorphisms
        (P := P)).isIso_iff]
    exact fun Œ¶ ‚Ü¶ ((MorphismProperty.isomorphisms _).arrow_mk_iso_iff
      (((Functor.mapArrowFunctor _ _).mapIso
        (Œ¶.obj.sheafFiberCompIso (forget A))).app (Arrow.mk f))).2
          (inferInstanceAs (IsIso ((forget A).map (Œ¶.obj.sheafFiber.map f))))

@[stacks 00YK "(1)"]
lemma jointlyReflectMonomorphisms [AB5OfSize.{w, w} A] [HasFiniteLimits A]
    [J.HasSheafCompose (forget A)] [(forget A).ReflectsIsomorphisms] :
    JointlyReflectMonomorphisms
      (fun (Œ¶ : P.FullSubcategory) ‚Ü¶ Œ¶.obj.sheafFiber (A := A)) :=
  (jointlyReflectIsomorphisms P A).jointlyReflectMonomorphisms

@[stacks 00YK "(2)"]
lemma jointlyReflectEpimorphisms
    [J.WEqualsLocallyBijective A] [HasSheafify J A]
    [J.HasSheafCompose (forget A)] [(forget A).ReflectsIsomorphisms] :
    JointlyReflectEpimorphisms
      (fun (Œ¶ : P.FullSubcategory) ‚Ü¶ Œ¶.obj.sheafFiber (A := A)) :=
  (jointlyReflectIsomorphisms P A).jointlyReflectEpimorphisms

@[stacks 00YK "(3)"]
lemma jointlyFaithful [AB5OfSize.{w, w} A] [HasFiniteLimits A]
    [J.HasSheafCompose (forget A)] [(forget A).ReflectsIsomorphisms] :
    JointlyFaithful
      (fun (Œ¶ : P.FullSubcategory) ‚Ü¶ Œ¶.obj.sheafFiber (A := A)) :=
  (jointlyReflectIsomorphisms P A).jointlyFaithful

variable {A} in
lemma jointly_reflect_isLocallySurjective
    [J.WEqualsLocallyBijective (Type w)] [HasSheafify J (Type w)]
    {X Y : C·µí·µñ ‚•§ A} (f : X ‚ü∂ Y)
    (hf : ‚àÄ (Œ¶ : P.FullSubcategory),
      Function.Surjective (Œ¶.obj.presheafFiber.map f)) :
    Presheaf.IsLocallySurjective J f := by
  simp only [‚Üê epi_iff_surjective] at hf
  rw [Presheaf.isLocallySurjective_iff_whisker_forget,
    ‚Üê Presheaf.isLocallySurjective_presheafToSheaf_map_iff,
    Sheaf.isLocallySurjective_iff_epi,
    (jointlyReflectEpimorphisms P (Type w)).epi_iff]
  exact fun Œ¶ ‚Ü¶ ((MorphismProperty.epimorphisms (Type w)).arrow_mk_iso_iff
    (((Functor.mapArrowFunctor _ _).mapIso
      ((Œ¶.obj.presheafFiberCompIso (forget A)).symm ‚â™‚â´
        Functor.isoWhiskerLeft _ (Œ¶.obj.presheafToSheafCompSheafFiber (Type w)).symm)).app
          (Arrow.mk f))).1 (hf Œ¶)

section

variable [HasSheafify J (Type w)] [J.WEqualsLocallyBijective (Type w)]
  {X : C} {Œπ : Type*} [Small.{w} Œπ] {U : Œπ ‚Üí C} (f : ‚àÄ i, U i ‚ü∂ X)

lemma jointly_reflect_ofArrows_mem :
    Sieve.ofArrows _ f ‚àà J X ‚Üî
      ‚àÄ (Œ¶ : P.FullSubcategory) (x : Œ¶.obj.fiber.obj X),
        ‚àÉ (i : Œπ) (y : Œ¶.obj.fiber.obj (U i)), Œ¶.obj.fiber.map (f i) y = x := by
  refine ‚ü®fun hf Œ¶ x ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®Z, _, ‚ü®_, p, _, ‚ü®i‚ü©, rfl‚ü©, z, rfl‚ü© := Œ¶.obj.jointly_surjective _ hf x
    exact ‚ü®i, Œ¶.obj.fiber.map p z, by simp‚ü©
  ¬∑ rw [ofArrows_mem_iff_isLocallySurjective]
    refine jointly_reflect_isLocallySurjective P _ (fun Œ¶ x ‚Ü¶ ?_)
    obtain ‚ü®x, rfl‚ü© := (Œ¶.obj.shrinkYonedaCompPresheafFiberIso.app X).toEquiv.symm.surjective x
    obtain ‚ü®i, y, rfl‚ü© := hf Œ¶ x
    refine ‚ü®Œ¶.obj.presheafFiber.map (Sigma.Œπ (fun i ‚Ü¶ shrinkYoneda.{w}.obj (U i)) i)
      (Œ¶.obj.shrinkYonedaCompPresheafFiberIso.inv.app _ y), ?_‚ü©
    have := congr_fun (Œ¶.obj.shrinkYonedaCompPresheafFiberIso.inv.naturality (f i)) y
    dsimp at this ‚ä¢
    rw [this, ‚Üê Sigma.Œπ_desc (fun i ‚Ü¶ shrinkYoneda.{w}.map (f i)) i, Functor.map_comp]
    rfl

end

end GrothendieckTopology.Point

namespace ObjectProperty.IsConservativeFamilyOfPoints

variable {P} [LocallySmall.{w} C]

private lemma mk'.isLocallySurjective
    (hP : ‚àÄ ‚¶ÉX : C‚¶Ñ (S : Sieve X) (_ : ‚àÄ (Œ¶ : P.FullSubcategory) (x : Œ¶.obj.fiber.obj X),
      ‚àÉ (Y : C) (g : Y ‚ü∂ X) (_ : S g) (y : Œ¶.obj.fiber.obj Y), Œ¶.obj.fiber.map g y = x),
        S ‚àà J X)
    {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type w} (f : F‚ÇÅ ‚ü∂ F‚ÇÇ) [Mono f]
    (hf : ‚àÄ (Œ¶ : P.FullSubcategory), Function.Surjective (Œ¶.obj.presheafFiber.map f)) :
    Presheaf.IsLocallySurjective J f := by
  wlog hF‚ÇÇ : ‚àÉ (U : C), F‚ÇÇ = shrinkYoneda.obj U generalizing F‚ÇÅ F‚ÇÇ
  ¬∑ refine ‚ü®fun {U} s ‚Ü¶ ?_‚ü©
    let f' := pullback.snd f (shrinkYonedaEquiv.{w}.symm s)
    have hf' (Œ¶ : P.FullSubcategory) :
        Function.Surjective (Œ¶.obj.presheafFiber.map f') := by
      replace hf := hf Œ¶
      rw [‚Üê CategoryTheory.epi_iff_surjective] at hf ‚ä¢
      exact (MorphismProperty.epimorphisms _).of_isPullback
        ((IsPullback.of_hasPullback f (shrinkYonedaEquiv.{w}.symm s)).map
          Œ¶.obj.presheafFiber) (.infer_property _)
    have := this f' hf' ‚ü®_, rfl‚ü©
    refine J.superset_covering ?_
      (Presheaf.imageSieve_mem J f' (shrinkYonedaObjObjEquiv.symm (ùüô U)))
    rintro V g ‚ü®v, hv‚ü©
    refine ‚ü®(pullback.fst f (shrinkYonedaEquiv.{w}.symm s)).app _ v, ?_‚ü©
    refine (congr_fun (NatTrans.congr_app (pullback.condition (f := f)) (op V)) _).trans ?_
    dsimp at hv ‚ä¢
    refine (congr_arg _ hv).trans ?_
    refine (congr_arg _ (shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm g.op (ùüô _))).trans ?_
    simpa using shrinkYonedaEquiv_symm_app_shrinkYonedaObjObjEquiv_symm s g
  obtain ‚ü®U, rfl‚ü© := hF‚ÇÇ
  suffices Presheaf.imageSieve f (shrinkYonedaObjObjEquiv.symm (ùüô U)) ‚àà J U from ‚ü®by
    intro V g
    obtain ‚ü®g, rfl‚ü© := shrinkYonedaObjObjEquiv.symm.surjective g
    replace this := J.pullback_stable g this
    rw [Presheaf.pullback_imageSieve] at this
    have hg := shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm g.op (ùüô _)
    simp only [Quiver.Hom.unop_op, Category.comp_id] at hg
    simpa [‚Üê hg]‚ü©
  refine hP _ (fun Œ¶ u ‚Ü¶ ?_)
  obtain ‚ü®x‚ÇÅ, hx‚ÇÅ‚ü© := hf Œ¶ (Œ¶.obj.shrinkYonedaCompPresheafFiberIso.inv.app _ u)
  obtain ‚ü®V, v, y, rfl‚ü© := Œ¶.obj.toPresheafFiber_jointly_surjective (A := Type w) x‚ÇÅ
  obtain ‚ü®t, ht‚ü© := shrinkYonedaObjObjEquiv.symm.surjective (f.app _ y)
  refine ‚ü®V, t, ‚ü®y, ht.symm.trans ?_‚ü©, v, ?_‚ü©
  ¬∑ simpa using (shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm t.op (ùüô _)).symm
  ¬∑ refine (Œ¶.obj.shrinkYonedaCompPresheafFiberIso.symm.app U).toEquiv.injective ?_
    dsimp
    trans (Œ¶.obj.toPresheafFiber V v (shrinkYoneda.{w}.obj U)) (shrinkYonedaObjObjEquiv.symm t)
    ¬∑ rw [‚Üê Œ¶.obj.presheafFiber_map_shrinkYoneda_map_shrinkYonedaCompPresheafFiberIso_inv_app]
      exact Œ¶.obj.shrinkYonedaCompPresheafFiberIso.inv.naturality_apply t v
    ¬∑ rw [‚Üê hx‚ÇÅ]
      refine Eq.trans (congr_arg _ ht)
        (Œ¶.obj.toPresheafFiber_naturality_apply f _ v y).symm

/- Let `P` be family of points of a site `(C, J)`, we show that `P` is a conservative
family of points if the following condition is satisfied:
for any sieve `S : Sieve X`, if the family of maps `Œ¶.map.fiber.map f`
for all morphisms `f` in the sieve `S` is jointly surjective for any `Œ¶` in `P`,
then `S` is a covering sieve for `J`. SGA 4 IV 6.5 (a) -/
lemma mk' [HasSheafify J (Type w)]
    (hP : ‚àÄ ‚¶ÉX : C‚¶Ñ (S : Sieve X) (_ : ‚àÄ (Œ¶ : P.FullSubcategory) (x : Œ¶.obj.fiber.obj X),
      ‚àÉ (Y : C) (g : Y ‚ü∂ X) (_ : S g) (y : Œ¶.obj.fiber.obj Y), Œ¶.obj.fiber.map g y = x),
        S ‚àà J X) :
    P.IsConservativeFamilyOfPoints where
  jointlyReflectIsomorphisms :=
    JointlyFaithful.jointlyReflectsIsomorphisms
      (JointlyFaithful.of_jointly_reflects_isIso_of_mono (fun F‚ÇÅ F‚ÇÇ f _ hf ‚Ü¶ by
        have : Epi f := by
          have : Mono f.val := inferInstanceAs (Mono ((sheafToPresheaf _ _).map f))
          rw [‚Üê Sheaf.isLocallySurjective_iff_epi]
          exact mk'.isLocallySurjective hP _
            (fun Œ¶ ‚Ü¶ ((isIso_iff_bijective _).1 (hf Œ¶)).2)
        exact Balanced.isIso_of_mono_of_epi f))

end ObjectProperty.IsConservativeFamilyOfPoints

end CategoryTheory
