/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Abelian.GrothendieckAxioms.Basic
public import Mathlib.CategoryTheory.Filtered.FinallySmall
public import Mathlib.CategoryTheory.Limits.Preserves.Filtered
public import Mathlib.CategoryTheory.Sites.CoverPreserving
public import Mathlib.CategoryTheory.Sites.LocallyBijective
public import Mathlib.CategoryTheory.Functor.Flat

/-!
# Points of a site

Let `C` be a category equipped with a Grothendieck topology `J`. In this file,
we define the notion of point of the site `(C, J)`, as a
structure `GrothendieckTopology.Point`. Such a `Œ¶ : J.Point` consists
in a functor `Œ¶.fiber : C ‚•§ Type w` such that the category `Œ¶.fiber.Elements`
is cofiltered (and initially small) and such that if `x : Œ¶.fiber.obj X`
and `R` is a covering sieve of `X`, then `x` belongs to the image
of some `y : Œ¶.fiber.obj Y` by a morphism `f : Y ‚ü∂ X` which belongs to `R`.
(This definition is essentially the definition of a fiber functor on a site
from SGA 4 IV 6.3.)

The fact that `Œ¶.fiber.Elements·µí·µñ` is filtered allows to define
`Œ¶.presheafFiber : (C·µí·µñ ‚•§ A) ‚•§ A` by taking the filtering colimit
of the evaluation functors at `op X` when `(X : C, x : F.obj X)` varies in
`Œ¶.fiber.Elements·µí·µñ`. We define `Œ¶.sheafFiber : Sheaf J A ‚•§ A` as the
restriction of `Œ¶.presheafFiber` to the full subcategory of sheaves.

Under certain assumptions, we show that if `A` is concrete and
`P ‚ü∂ Q` is a locally bijective morphism between presheaves,
then the induced morphism on fibers is a bijection. It follows
that not only `Œ¶.sheafFiber : Sheaf J A ‚•§ A` is the restriction of
`Œ¶.presheafFiber` but it may also be thought as a localization
of this functor with respect to the class of morphisms `J.W`.
In particular, the fiber of a presheaf identifies to the fiber of
its associated sheaf.

Under suitable assumptions on the target category `A`, we show that
both `Œ¶.presheafFiber` and `Œ¶.sheafFiber` commute with finite limits
and with arbitrary colimits.

-/

@[expose] public section

universe w' w v v' v'' u u' u''

namespace CategoryTheory

open Limits Opposite

variable {C : Type u} [Category.{v} C]

-- to be moved
namespace Functor.Elements

variable [LocallySmall.{w} C] (F : C ‚•§ Type w)

/-- If `F : C ‚•§ Type w` and `C` is locally `w`-small, then for any `X : C`,
this is the colimit cocone which identifies `F.obj X` to the colimit of
`(CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.obj X`. -/
@[simps]
noncomputable def coconeœÄOpCompShrinkYoneda (X : C) :
    Cocone ((CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.{w}.obj X) where
  pt := F.obj X
  Œπ.app u t := F.map (shrinkYonedaObjObjEquiv t) u.unop.snd
  Œπ.naturality u‚ÇÅ u‚ÇÇ g := by
    ext f
    obtain ‚ü®f, rfl‚ü© := shrinkYonedaObjObjEquiv.symm.surjective f
    dsimp at f ‚ä¢
    rw [shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm]
    simp

/-- If `F : C ‚•§ Type w` and `C` is locally `w`-small, then for any `X : C`,
`F.obj X` identifies to the colimit of
`(CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.obj X`. -/
noncomputable def isColimitCoconeœÄOpCompShrinkYoneda (X : C) :
    IsColimit (coconeœÄOpCompShrinkYoneda F X) := by
  refine Nonempty.some ((Types.isColimit_iff_coconeTypesIsColimit _).2
    ‚ü®?_, fun x ‚Ü¶ ?_‚ü©)
  ¬∑ let G := (CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.{w}.obj X
    let c := G.coconeTypesEquiv.symm (coconeœÄOpCompShrinkYoneda F X)
    have (u : G.ColimitType) (x : F.obj X) (h : G.descColimitType c u = x) :
        G.ŒπColimitType (op (elementsMk _ _ x))
          (shrinkYonedaObjObjEquiv.symm (ùüô X)) = u := by
      obtain ‚ü®‚ü®u‚ü©, v, rfl‚ü© := Functor.ŒπColimitType_jointly_surjective _ u
      obtain ‚ü®v, rfl‚ü© := shrinkYonedaObjObjEquiv.symm.surjective v
      dsimp [c] at v h
      simp only [Equiv.apply_symm_apply] at h
      rw [‚Üê G.ŒπColimitType_map (show u ‚ü∂ F.elementsMk _ x from ‚ü®v, h‚ü©).op]
      dsimp [G]
      rw [shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm]
      simp
    intro u‚ÇÅ u‚ÇÇ hu
    generalize hx‚ÇÅ : G.descColimitType c u‚ÇÅ = x
    have hx‚ÇÇ : G.descColimitType c u‚ÇÇ = x := by rw [‚Üê hx‚ÇÅ]; exact hu.symm
    rw [‚Üê this _ _ hx‚ÇÅ, ‚Üê this _ _ hx‚ÇÇ]
  ¬∑ exact ‚ü®Functor.ŒπColimitType _ (op (elementsMk _ _ x))
      (shrinkYonedaObjObjEquiv.symm (ùüô X)), by simp‚ü©

@[reassoc (attr := simp)]
lemma shrinkYoneda_map_app_coconeœÄOpCompShrinkYoneda_Œπ_app
    {X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (u : F.Elements) :
    (shrinkYoneda.{w}.map f).app (op u.fst) ‚â´
      (coconeœÄOpCompShrinkYoneda F X‚ÇÇ).Œπ.app (op u) =
      (coconeœÄOpCompShrinkYoneda F X‚ÇÅ).Œπ.app (op u) ‚â´ F.map f := by
  ext g
  obtain ‚ü®g, rfl‚ü© := shrinkYonedaObjObjEquiv.symm.surjective g
  dsimp
  simp only [Equiv.apply_symm_apply]
  rw [shrinkYoneda_map_app_shrinkYonedaObjObjEquiv_symm]
  simp

/-- If `F : C ‚•§ Type w` and `C` is locally `w`-small, then `F` identifies to the composition
`shrinkYoneda ‚ãô (Functor.whiskeringLeft _ _ _).obj (CategoryOfElements.œÄ F).op ‚ãô colim`. -/
noncomputable def shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso
    [HasColimitsOfShape F.Elements·µí·µñ (Type w)] :
    shrinkYoneda.{w} ‚ãô
      (Functor.whiskeringLeft _ _ _).obj (CategoryOfElements.œÄ F).op ‚ãô colim ‚âÖ F :=
  NatIso.ofComponents (fun X ‚Ü¶
    IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
      (isColimitCoconeœÄOpCompShrinkYoneda F X)) (fun {X‚ÇÅ X‚ÇÇ} f ‚Ü¶ colimit.hom_ext (by
        intro u
        simp [shrinkYoneda_map_app_coconeœÄOpCompShrinkYoneda_Œπ_app F f u.unop]))

lemma shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso_inv_app_apply
    [HasColimitsOfShape F.Elements·µí·µñ (Type w)] (u : F.Elements) :
      (shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso F).inv.app _ u.snd =
      (colimit.Œπ ((CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.{w}.obj u.fst) (op u)
        (shrinkYonedaObjObjEquiv.symm (ùüô _))) := by
  have :
      (coconeœÄOpCompShrinkYoneda F u.fst).Œπ.app (op u) ‚â´
        (shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso F).inv.app u.fst =
      colimit.Œπ ((CategoryOfElements.œÄ F).op ‚ãô shrinkYoneda.{w}.obj u.fst) (op u) :=
    IsColimit.comp_coconePointUniqueUpToIso_inv (colimit.isColimit _) _ (op u)
  simpa using congr_fun this (shrinkYonedaObjObjEquiv.symm (ùüô _))

end Functor.Elements

namespace GrothendieckTopology

variable (J : GrothendieckTopology C)

/-- Given `J` a Grothendieck topology on a category `C`, a point of the site `(C, J)`
consists of a functor `fiber : C ‚•§ Type w` such that the category `fiber.Elements`
is initially small (which allows defining the fiber functor on presheaves by
taking colimits) and cofiltered (so that the fiber functor on presheaves is exact),
and such that covering sieves induce jointly surjective maps on fibers (which
allows to show that the fibers of a presheaf and its associated sheaf are isomorphic). -/
structure Point where
  /-- the fiber functor on the underlying category of the site -/
  fiber : C ‚•§ Type w
  isCofiltered : IsCofiltered fiber.Elements := by infer_instance
  initiallySmall : InitiallySmall.{w} fiber.Elements := by infer_instance
  jointly_surjective {X : C} (R : Sieve X) (h : R ‚àà J X) (x : fiber.obj X) :
    ‚àÉ (Y : C) (f : Y ‚ü∂ X) (_ : R f) (y : fiber.obj Y), fiber.map f y = x

namespace Point

attribute [instance] initiallySmall isCofiltered

variable {J} (Œ¶ : Point.{w} J) {A : Type u'} [Category.{v'} A]
  {B : Type u''} [Category.{v''} B]
  [HasColimitsOfSize.{w, w} A] [HasColimitsOfSize.{w, w} B]

instance : HasColimitsOfShape Œ¶.fiber.Elements·µí·µñ A :=
  hasColimitsOfShape_of_finallySmall _ _

instance [LocallySmall.{w} C] [AB5OfSize.{w, w} A] [HasFiniteLimits A] :
    HasExactColimitsOfShape Œ¶.fiber.Elements·µí·µñ A :=
  hasExactColimitsOfShape_of_final _
    (FinallySmall.fromFilteredFinalModel Œ¶.fiber.Elements·µí·µñ)

/-- The fiber functor on categories of presheaves that is given by a point of a site. -/
noncomputable def presheafFiber : (C·µí·µñ ‚•§ A) ‚•§ A :=
  (Functor.whiskeringLeft _ _ _).obj (CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô colim

/-- Given a point `Œ¶` of a site `(C, J)`, `X : C` and `x : Œ¶.fiber.obj X`, this
is the canonical map `P.obj (op X) ‚ü∂ Œ¶.presheafFiber.obj P`. -/
noncomputable def toPresheafFiber (X : C) (x : Œ¶.fiber.obj X) (P : C·µí·µñ ‚•§ A) :
    P.obj (op X) ‚ü∂ Œ¶.presheafFiber.obj P :=
  colimit.Œπ ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P) (op ‚ü®X, x‚ü©)

@[ext]
lemma presheafFiber_hom_ext
    {P : C·µí·µñ ‚•§ A} {T : A} {f g : Œ¶.presheafFiber.obj P ‚ü∂ T}
    (h : ‚àÄ (X : C) (x : Œ¶.fiber.obj X), Œ¶.toPresheafFiber X x P ‚â´ f =
      Œ¶.toPresheafFiber X x P ‚â´ g) : f = g :=
  colimit.hom_ext (by rintro ‚ü®‚ü®X, x‚ü©‚ü©; exact h X x)

/-- Given a point `Œ¶` of a site `(C, J)`, `X : C` and `x : Œ¶.fiber.obj X`,
this is the map `P.obj (op X) ‚ü∂ Œ¶.presheafFiber.obj P` for any `P : C·µí·µñ ‚•§ A`
as a natural transformation. -/
@[simps]
noncomputable def toPresheafFiberNatTrans (X : C) (x : Œ¶.fiber.obj X) :
    (evaluation C·µí·µñ A).obj (op X) ‚ü∂ Œ¶.presheafFiber where
  app := Œ¶.toPresheafFiber X x
  naturality _ _ f := by simp [presheafFiber, toPresheafFiber]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma toPresheafFiber_w {X Y : C} (f : X ‚ü∂ Y) (x : Œ¶.fiber.obj X) (P : C·µí·µñ ‚•§ A) :
    P.map f.op ‚â´ Œ¶.toPresheafFiber X x P =
      Œ¶.toPresheafFiber Y (Œ¶.fiber.map f x) P :=
  colimit.w ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P)
      (CategoryOfElements.homMk ‚ü®X, x‚ü© ‚ü®Y, Œ¶.fiber.map f x‚ü© f rfl).op

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma toPresheafFiber_naturality {P Q : C·µí·µñ ‚•§ A} (g : P ‚ü∂ Q) (X : C) (x : Œ¶.fiber.obj X) :
    Œ¶.toPresheafFiber X x P ‚â´ Œ¶.presheafFiber.map g =
      g.app (op X) ‚â´ Œ¶.toPresheafFiber X x Q :=
  ((Œ¶.toPresheafFiberNatTrans X x).naturality g).symm

/-- The isomorphism `shrinkYoneda.{w} ‚ãô Œ¶.presheafFiber ‚âÖ Œ¶.fiber`. -/
noncomputable def shrinkYonedaCompPresheafFiberIso [LocallySmall.{w} C] :
    shrinkYoneda.{w} ‚ãô Œ¶.presheafFiber ‚âÖ Œ¶.fiber :=
  Functor.Elements.shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso _

lemma shrinkYonedaCompPresheafFiberIso_inv_app_toPresheafFiber
    [LocallySmall.{w} C] {X : C} (x : Œ¶.fiber.obj X) :
    Œ¶.shrinkYonedaCompPresheafFiberIso.inv.app X x =
      Œ¶.toPresheafFiber X x (shrinkYoneda.{w}.obj X)
        (shrinkYonedaObjObjEquiv.symm (ùüô X)) :=
  Functor.Elements.shrinkYonedaCompWhiskeringLeftObjœÄCompColimIso_inv_app_apply
    _ (Functor.elementsMk (Œ¶.fiber) _ x)

lemma presheafFiber_map_shrinkYoneda_map_shrinkYonedaCompPresheafFiberIso_inv_app
    [LocallySmall.{w} C] {X Y : C} (f : X ‚ü∂ Y) (x : Œ¶.fiber.obj X) :
      Œ¶.presheafFiber.map (shrinkYoneda.{w}.map f)
        (Œ¶.shrinkYonedaCompPresheafFiberIso.inv.app X x) =
      Œ¶.toPresheafFiber X x (shrinkYoneda.{w}.obj Y)
        (shrinkYonedaObjObjEquiv.symm f) := by
  rw [shrinkYonedaCompPresheafFiberIso_inv_app_toPresheafFiber]
  refine (Œ¶.toPresheafFiber_naturality_apply (shrinkYoneda.{w}.map f) _ x
    (shrinkYonedaObjObjEquiv.symm (ùüô X))).trans (congr_arg _ ?_)
  simpa using shrinkYoneda_map_app_shrinkYonedaObjObjEquiv_symm.{w} (ùüô _) f

section

variable {P : C·µí·µñ ‚•§ A} {T : A}
  (œÜ : ‚àÄ (X : C) (_ : Œ¶.fiber.obj X), P.obj (op X) ‚ü∂ T)
  (hœÜ : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (x : Œ¶.fiber.obj X),
    P.map f.op ‚â´ œÜ X x = œÜ Y (Œ¶.fiber.map f x) := by cat_disch)

set_option backward.privateInPublic true in
/-- Constructor for morphisms from the fiber of a presheaf. -/
noncomputable def presheafFiberDesc :
    Œ¶.presheafFiber.obj P ‚ü∂ T :=
  colimit.desc _ (Cocone.mk _ { app x := œÜ x.unop.1 x.unop.2 })

set_option backward.privateInPublic true in
@[reassoc (attr := simp)]
lemma toPresheafFiber_presheafFiberDesc (X : C) (x : Œ¶.fiber.obj X) :
    Œ¶.toPresheafFiber X x P ‚â´ Œ¶.presheafFiberDesc œÜ hœÜ = œÜ X x :=
  colimit.Œπ_desc _ _

end

variable {FC : A ‚Üí A ‚Üí Type*} {CC : A ‚Üí Type w'}
  [‚àÄ (X Y : A), FunLike (FC X Y) (CC X) (CC Y)]
  [ConcreteCategory.{w'} A FC]

section

variable {P Q : C·µí·µñ ‚•§ A}

variable [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]

instance : PreservesColimitsOfShape Œ¶.fiber.Elements·µí·µñ (forget A) :=
  Functor.Final.preservesColimitsOfShape_of_final (FinallySmall.fromFilteredFinalModel.{w} _) _

lemma toPresheafFiber_jointly_surjective (p : ToType (Œ¶.presheafFiber.obj P)) :
    ‚àÉ (X : C) (x : Œ¶.fiber.obj X) (z : ToType (P.obj (op X))),
      Œ¶.toPresheafFiber X x P z = p := by
  obtain ‚ü®‚ü®X, x‚ü©, z, rfl‚ü© := Types.jointly_surjective_of_isColimit
    (isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) p
  exact ‚ü®X, x, z, rfl‚ü©

lemma toPresheafFiber_jointly_surjective‚ÇÇ (p‚ÇÅ p‚ÇÇ : ToType (Œ¶.presheafFiber.obj P)) :
    ‚àÉ (X : C) (x : Œ¶.fiber.obj X) (z‚ÇÅ z‚ÇÇ : ToType (P.obj (op X))),
      Œ¶.toPresheafFiber X x P z‚ÇÅ = p‚ÇÅ ‚àß Œ¶.toPresheafFiber X x P z‚ÇÇ = p‚ÇÇ := by
  obtain ‚ü®‚ü®X, x‚ü©, z‚ÇÅ, z‚ÇÇ, rfl, rfl‚ü© := Types.FilteredColimit.jointly_surjective_of_isColimit‚ÇÇ
    (isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) p‚ÇÅ p‚ÇÇ
  exact ‚ü®X, x, z‚ÇÅ, z‚ÇÇ, rfl, rfl‚ü©

lemma toPresheafFiber_eq_iff' (X : C) (x : Œ¶.fiber.obj X) (z‚ÇÅ z‚ÇÇ : ToType (P.obj (op X))) :
    Œ¶.toPresheafFiber X x P z‚ÇÅ = Œ¶.toPresheafFiber X x P z‚ÇÇ ‚Üî
      ‚àÉ (Y : C) (f : Y ‚ü∂ X) (y : Œ¶.fiber.obj Y), Œ¶.fiber.map f y = x ‚àß
        P.map f.op z‚ÇÅ = P.map f.op z‚ÇÇ := by
  refine (Types.FilteredColimit.isColimit_eq_iff'
    (ht := isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) ..).trans ?_
  constructor
  ¬∑ rintro ‚ü®‚ü®Y, y‚ü©, ‚ü®f, hf‚ü©, hf'‚ü©
    exact ‚ü®Y, f, y, hf, hf'‚ü©
  ¬∑ rintro ‚ü®Y, f, y, hf, hf'‚ü©
    exact ‚ü®‚ü®Y, y‚ü©, ‚ü®f, hf‚ü©, hf'‚ü©

variable (f : P ‚ü∂ Q)

lemma toPresheafFiber_map_surjective [Presheaf.IsLocallySurjective J f] :
    Function.Surjective (Œ¶.presheafFiber.map f) := by
  intro p
  obtain ‚ü®X, x, z, rfl‚ü© := Œ¶.toPresheafFiber_jointly_surjective p
  obtain ‚ü®Y, g, ‚ü®t, ht‚ü©, y, rfl‚ü© := Œ¶.jointly_surjective _ (Presheaf.imageSieve_mem J f z) x
  exact ‚ü®Œ¶.toPresheafFiber Y y P t, by simp [‚Üê toPresheafFiber_w, ‚Üê ht]‚ü©

lemma toPresheafFiber_map_injective [Presheaf.IsLocallyInjective J f] :
    Function.Injective (Œ¶.presheafFiber.map f) := by
  suffices ‚àÄ (X : C) (x : Œ¶.fiber.obj X) (p‚ÇÅ p‚ÇÇ : ToType (P.obj (op X)))
      (hp : f.app _ p‚ÇÅ = f.app _ p‚ÇÇ), Œ¶.toPresheafFiber X x P p‚ÇÅ = Œ¶.toPresheafFiber X x P p‚ÇÇ by
    rintro q‚ÇÅ q‚ÇÇ h
    obtain ‚ü®X, x, p‚ÇÅ, p‚ÇÇ, rfl, rfl‚ü© := Œ¶.toPresheafFiber_jointly_surjective‚ÇÇ q‚ÇÅ q‚ÇÇ
    simp only [toPresheafFiber_naturality_apply, toPresheafFiber_eq_iff'] at h
    obtain ‚ü®Y, g, y, rfl, h‚ü© := h
    simp only [‚Üê NatTrans.naturality_apply] at h
    simpa using this _ y _ _ h
  intro X x p‚ÇÅ p‚ÇÇ h
  obtain ‚ü®Y, g, hg, y, rfl‚ü© := Œ¶.jointly_surjective _ (Presheaf.equalizerSieve_mem J f _ _ h) x
  simp_all [‚Üê toPresheafFiber_w_apply]

lemma toPresheafFiber_map_bijective
    [Presheaf.IsLocallyInjective J f] [Presheaf.IsLocallySurjective J f] :
    Function.Bijective (Œ¶.presheafFiber.map f) :=
  ‚ü®Œ¶.toPresheafFiber_map_injective f, Œ¶.toPresheafFiber_map_surjective f‚ü©

lemma W_isInvertedBy_presheafFiber
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    J.W.IsInvertedBy (Œ¶.presheafFiber (A := A)) := by
  intro P Q f hf
  obtain ‚ü®_, _‚ü© := (J.W_iff_isLocallyBijective f).1 hf
  rw [‚Üê isIso_iff_of_reflects_iso _ (forget A), isIso_iff_bijective]
  exact Œ¶.toPresheafFiber_map_bijective f

end

/-- The fiber functor on the category of sheaves that is given a by a point of a site. -/
noncomputable abbrev sheafFiber : Sheaf J A ‚•§ A :=
  sheafToPresheaf J A ‚ãô Œ¶.presheafFiber

instance (P : C·µí·µñ ‚•§ A) [HasWeakSheafify J A]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    IsIso (Œ¶.presheafFiber.map (CategoryTheory.toSheafify J P)) :=
  W_isInvertedBy_presheafFiber _ _ (W_toSheafify J P)

variable (A) in
/-- The fiber functor on sheaves is obtained from the fiber functor on presheaves
by localization with respect to the class of morphisms `J.W`. -/
noncomputable def presheafToSheafCompSheafFiber [HasWeakSheafify J A]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    presheafToSheaf J A ‚ãô Œ¶.sheafFiber ‚âÖ Œ¶.presheafFiber :=
  (NatIso.ofComponents
    (fun P ‚Ü¶ asIso ((Œ¶.presheafFiber (A := A)).map (CategoryTheory.toSheafify J P) :))
      (by simp [‚Üê Functor.map_comp])).symm

instance [LocallySmall.{w} C] [HasFiniteLimits A] [AB5OfSize.{w, w} A] :
    PreservesFiniteLimits (Œ¶.presheafFiber (A := A)) :=
  comp_preservesFiniteLimits _ _

instance [LocallySmall.{w} C] [HasFiniteLimits A] [AB5OfSize.{w, w} A] :
    PreservesFiniteLimits (Œ¶.sheafFiber (A := A)) :=
  comp_preservesFiniteLimits _ _

instance : PreservesColimitsOfSize.{w, w} (Œ¶.presheafFiber (A := A)) where
  preservesColimitsOfShape := by
    dsimp [presheafFiber]
    infer_instance

instance [HasSheafify J A] [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C] :
    PreservesColimitsOfSize.{w, w} (Œ¶.sheafFiber (A := A)) where
  preservesColimitsOfShape {K _} := ‚ü®fun {F} ‚Ü¶
    preservesColimit_of_preserves_colimit_cocone
      (Sheaf.isColimitSheafifyCocone _ (colimit.isColimit _))
        (IsColimit.ofIsoColimit (isColimitOfPreserves Œ¶.presheafFiber
          (colimit.isColimit (F ‚ãô sheafToPresheaf J A))) (by
            let G := colimit (F ‚ãô sheafToPresheaf J A)
            let œÜ := CategoryTheory.toSheafify J G
            have : IsIso (Œ¶.presheafFiber.map (CategoryTheory.toSheafify J G)) :=
              W_isInvertedBy_presheafFiber _ _ (W_toSheafify J _)
            refine Cocones.ext (asIso (Œ¶.presheafFiber.map (CategoryTheory.toSheafify J G)))
              (fun k ‚Ü¶ ?_)
            dsimp
            rw [‚Üê Functor.map_comp, Sheaf.sheafifyCocone_Œπ_app_val]
            dsimp))‚ü©

instance [HasSheafify J A] [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C] :
    PreservesFiniteColimits (Œ¶.sheafFiber (A := A)) :=
  PreservesColimitsOfSize.preservesFiniteColimits _

variable (F : A ‚•§ B) [LocallySmall.{w} C] [PreservesFilteredColimitsOfSize.{w, w} F]

/-- If `Œ¶` is a point of a site and `F : A ‚•§ B` is a functor which preserves
filtered colimits, then taking fibers of presheaves at `Œ¶` commutes with `F`. -/
noncomputable def presheafFiberCompIso :
    (Functor.whiskeringRight _ _ _).obj F ‚ãô Œ¶.presheafFiber ‚âÖ
      Œ¶.presheafFiber ‚ãô F :=
  haveI := Functor.Final.preservesColimitsOfShape_of_final
    (FinallySmall.fromFilteredFinalModel.{w} (Œ¶.fiber.Elements·µí·µñ)) F
  Functor.isoWhiskerLeft
    ((Functor.whiskeringLeft _ _ _).obj _) (preservesColimitNatIso F).symm

@[reassoc]
lemma toPresheafFiber_presheafFiberCompIso_hom_app
    (X : C) (x : Œ¶.fiber.obj X) (P : C·µí·µñ ‚•§ A) :
    Œ¶.toPresheafFiber X x (P ‚ãô F) ‚â´ (Œ¶.presheafFiberCompIso F).hom.app P =
      F.map (Œ¶.toPresheafFiber X x P) := by
  haveI := Functor.Final.preservesColimitsOfShape_of_final
    (FinallySmall.fromFilteredFinalModel.{w} (Œ¶.fiber.Elements·µí·µñ)) F
  simp only [presheafFiberCompIso]
  exact Œπ_preservesColimitIso_inv F ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P) _

/-- If `Œ¶` is a point of a site and `F : A ‚•§ B` is a functor which preserves
filtered colimits, then taking fibers of sheaves at `Œ¶` commutes with `F`. -/
@[simps!]
noncomputable def sheafFiberCompIso [J.HasSheafCompose F] :
    sheafCompose J F ‚ãô Œ¶.sheafFiber ‚âÖ Œ¶.sheafFiber ‚ãô F :=
  Functor.isoWhiskerLeft (sheafToPresheaf J A) (Œ¶.presheafFiberCompIso F) ‚â™‚â´
    (Functor.associator _ _ _).symm

section Comap

variable {C D : Type*} [Category* C] [Category* D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D}

/-- If `F : C ‚•§ D` is a representably flat and cover preserving functor between sites, then
any point on `D` induces a point on `C` by precomposing the fiber functor with `F`. -/
@[simps]
def comap (F : C ‚•§ D) [RepresentablyFlat F] (H : CoverPreserving J K F) (Œ¶ : Point.{w} K)
    [InitiallySmall (F ‚ãô Œ¶.fiber).Elements] :
    Point.{w} J where
  fiber := F ‚ãô Œ¶.fiber
  jointly_surjective {X} {R} hR x := by
    obtain ‚ü®Y, f, ‚ü®W, g, h, hg, rfl‚ü©, y, rfl‚ü© :=
      Œ¶.jointly_surjective (Sieve.functorPushforward F R) (H.1 hR) x
    use W, g, hg, Œ¶.fiber.map h y
    simp

end Comap

end Point

end GrothendieckTopology

end CategoryTheory
