/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Preserves.Bifunctor
public import Mathlib.CategoryTheory.Localization.Monoidal.Functor
public import Mathlib.CategoryTheory.Sites.Point.Basic
public import Mathlib.CategoryTheory.Sites.Monoidal

/-!
# Fiber functors are monoidal

Let `Œ¶` be a point of a site `(C, J)`. Let `A` be a monoidal category where
the tensor product commutes with filtered colimits in both variables.
We show that the fiber functors `Œ¶.presheafFiber : (C·µí·µñ ‚•§ A) ‚•§ A`
and `Œ¶.sheafFiber : Sheaf J A ‚•§ A` are monoidal.

-/

@[expose] public section

universe w w' v v' u u'

namespace CategoryTheory.GrothendieckTopology.Point

open Limits MonoidalCategory Functor

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C} (Œ¶ : Point.{w} J)
  {A : Type u'} [Category.{v'} A] [MonoidalCategory A] [HasColimitsOfSize.{w, w} A]

noncomputable instance :
    (Œ¶.presheafFiber (A := A)).OplaxMonoidal where
  Œ∑ := Œ¶.presheafFiberDesc (fun _ _ ‚Ü¶ ùüô _)
  Œ¥ P‚ÇÅ P‚ÇÇ :=
    Œ¶.presheafFiberDesc (fun X x ‚Ü¶ Œ¶.toPresheafFiber X x P‚ÇÅ ‚äó‚Çò Œ¶.toPresheafFiber X x P‚ÇÇ)
      (by simp [tensorHom_comp_tensorHom])
  Œ¥_natural_left _ _ := by
    ext
    simp [tensorHom_def', ‚Üê comp_whiskerRight, ‚Üê whisker_exchange_assoc]
  Œ¥_natural_right P f := by
    ext
    simp [tensorHom_def, ‚Üê MonoidalCategory.whiskerLeft_comp, whisker_exchange_assoc]
  oplax_associativity P‚ÇÅ P‚ÇÇ P‚ÇÉ := by
    ext X x
    dsimp
    conv_lhs =>
      simp only [toPresheafFiber_presheafFiberDesc_assoc, tensorHom_def'_assoc,
        ‚Üê comp_whiskerRight_assoc, toPresheafFiber_presheafFiberDesc]
      rw [‚Üê tensorHom_def'_assoc, associator_naturality]
    conv_rhs =>
      simp only [toPresheafFiber_naturality_assoc, toPresheafFiber_presheafFiberDesc_assoc,
        tensorHom_def_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp,
        toPresheafFiber_presheafFiberDesc]
      rw [‚Üê tensorHom_def]
      dsimp
  oplax_left_unitality _ := by
    ext
    simp [tensorHom_def', ‚Üê comp_whiskerRight]
  oplax_right_unitality _ := by
    ext
    simp [tensorHom_def, ‚Üê MonoidalCategory.whiskerLeft_comp]

@[reassoc (attr := simp)]
lemma toPresheafFiber_Œ∑ (X : C) (x : Œ¶.fiber.obj X) :
    Œ¶.toPresheafFiber X x (ùüô_ (C·µí·µñ ‚•§ A)) ‚â´ OplaxMonoidal.Œ∑ Œ¶.presheafFiber = ùüô (ùüô_ A) :=
  toPresheafFiber_presheafFiberDesc _ _ _ _ _

@[reassoc (attr := simp)]
lemma toPresheafFiber_Œ¥ (X : C) (x : Œ¶.fiber.obj X) (G‚ÇÅ G‚ÇÇ : C·µí·µñ ‚•§ A) :
    Œ¶.toPresheafFiber X x (G‚ÇÅ ‚äó G‚ÇÇ) ‚â´ OplaxMonoidal.Œ¥ Œ¶.presheafFiber G‚ÇÅ G‚ÇÇ =
      Œ¶.toPresheafFiber X x G‚ÇÅ ‚äó‚Çò Œ¶.toPresheafFiber X x G‚ÇÇ :=
  toPresheafFiber_presheafFiberDesc _ _ _ _ _

variable [LocallySmall.{w} C]
  [‚àÄ (X : A), PreservesFilteredColimitsOfSize.{w, w} (tensorLeft X)]
  [‚àÄ (X : A), PreservesFilteredColimitsOfSize.{w, w} (tensorRight X)]

instance (M : A) :
    PreservesColimitsOfShape Œ¶.fiber.Elements·µí·µñ ((curriedTensor A).flip.obj M) :=
  Final.preservesColimitsOfShape_of_final (FinallySmall.fromFilteredFinalModel.{w} _) _

instance (M : A) :
    PreservesColimitsOfShape Œ¶.fiber.Elements·µí·µñ ((curriedTensor A).obj M) :=
  Final.preservesColimitsOfShape_of_final (FinallySmall.fromFilteredFinalModel.{w} _) _

attribute [local instance] IsFiltered.isConnected in
instance : IsIso (OplaxMonoidal.Œ∑ (Œ¶.presheafFiber (A := A))) :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    (isColimitConstCocone _ (ùüô_ A))).isIso_hom

attribute [local simp] tensorHom_def toPresheafFiber OplaxMonoidal.Œ¥ presheafFiberDesc in
instance (P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ A) :
    IsIso (OplaxMonoidal.Œ¥ Œ¶.presheafFiber P‚ÇÅ P‚ÇÇ) := by
  let e := IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    ((Final.isColimitWhiskerEquiv (diag _) _ ).2
      (isColimitOfPreserves‚ÇÇ (curriedTensor A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P‚ÇÅ))
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P‚ÇÇ))))
  rw [show OplaxMonoidal.Œ¥ Œ¶.presheafFiber P‚ÇÅ P‚ÇÇ =
      colimMap { app _ := by exact ùüô _ } ‚â´ e.hom by cat_disch,
    isIso_comp_right_iff]
  apply +allowSynthFailures isIso_colimMap
  rw [NatTrans.isIso_iff_isIso_app]
  intro
  dsimp
  infer_instance

noncomputable instance : (Œ¶.presheafFiber (A := A)).Monoidal :=
  .ofOplaxMonoidal _

attribute [local instance] Sheaf.monoidalCategory

variable {FC : A ‚Üí A ‚Üí Type*} {CC : A ‚Üí Type w'}
  [‚àÄ (X Y : A), FunLike (FC X Y) (CC X) (CC Y)]
  [ConcreteCategory.{w'} A FC]
  [PreservesFilteredColimitsOfSize.{w, w} (forget A)]
  [(forget A).ReflectsIsomorphisms]
  [HasWeakSheafify J A] [J.WEqualsLocallyBijective A]
  [(J.W (A := A)).IsMonoidal]

noncomputable instance : (Œ¶.sheafFiber (A := A)).Monoidal :=
  Localization.Monoidal.functorMonoidalOfComp (presheafToSheaf J A) J.W
    Œ¶.sheafFiber Œ¶.presheafFiber

instance : NatTrans.IsMonoidal (Œ¶.presheafToSheafCompSheafFiber A).hom :=
  Localization.Monoidal.lifting_isMonoidal (presheafToSheaf J A) J.W
    Œ¶.sheafFiber Œ¶.presheafFiber

end CategoryTheory.GrothendieckTopology.Point
