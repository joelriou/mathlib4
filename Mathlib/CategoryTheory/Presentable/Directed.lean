/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Presentable.IsCardinalFiltered
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.MorphismProperty.Basic
import Mathlib.CategoryTheory.Filtered.Final
import Mathlib.CategoryTheory.Products.Unitor
import Mathlib.Data.Finite.Sigma

/-!
# `Œ∫`-filtered categories and `Œ∫`-directed poset

In this file, we formalize the proof by Deligne (SGA 4 I 8.1.6) that for
any (small) filtered category `J`, there exists a final functor `F : Œ± ‚•§ J`
where `Œ±` is a directed partially ordered set (`IsFiltered.exists_directed`).
The construction applies more generally to `Œ∫`-filtered categories and
`Œ∫`-directed posets (`IsCardinalFiltered.exists_cardinal_directed`).

Note: the argument by Deligne is reproduced (without reference) in the book
by Ad√°mek and Rosick√Ω (theorem 1.5), but with a mistake:
the construction by Deligne involves considering diagrams
(see `CategoryTheory.IsCardinalFiltered.exists_cardinal_directed.Diagram`)
which are not necessarily *subcategories* (the class of morphisms `W` does not
have to be multiplicative.)

## References
* [Alexander Grothendieck and Jean-Louis Verdier, *Expos√© I : Pr√©faisceaux*,
  SGA 4 I 8.1.6][sga-4-tome-1]
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

universe u v w

lemma CategoryTheory.MorphismProperty.toSet_iSup {C : Type*} [Category C]
    {Œπ : Type*} (W : Œπ ‚Üí MorphismProperty C) :
    (‚®Ü i , W i).toSet = ‚ãÉ i, (W i).toSet := by
  ext
  simp [MorphismProperty.toSet]

lemma CategoryTheory.MorphismProperty.toSet_max {C : Type*} [Category C]
    (W‚ÇÅ W‚ÇÇ : MorphismProperty C) :
    (W‚ÇÅ ‚äî W‚ÇÇ).toSet = W‚ÇÅ.toSet ‚à™ W‚ÇÇ.toSet := rfl

lemma hasCardinalLT_of_finite
    (X : Type*) [Finite X] (Œ∫ : Cardinal) (hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫) :
    HasCardinalLT X Œ∫ :=
  .of_le (by rwa [hasCardinalLT_aleph0_iff]) hŒ∫

lemma hasCardinalLT_punit (Œ∫ : Cardinal) (hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫) :
    HasCardinalLT PUnit Œ∫ :=
  hasCardinalLT_of_finite _ _ hŒ∫

lemma Cardinal.mk_surjective :
    Function.Surjective (Cardinal.mk : Type w ‚Üí Cardinal) := by
  rintro ‚ü®_‚ü©; exact ‚ü®_, rfl‚ü©

lemma hasCardinalLT_sigma' {Œπ : Type w} (Œ± : Œπ ‚Üí Type w) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    (hŒπ : HasCardinalLT Œπ Œ∫) (hŒ± : ‚àÄ i, HasCardinalLT (Œ± i) Œ∫) :
    HasCardinalLT (Œ£ i, Œ± i) Œ∫ := by
  have hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫ := Cardinal.IsRegular.aleph0_le Fact.out
  obtain hŒ∫ | rfl := hŒ∫.lt_or_eq
  ¬∑ obtain ‚ü®Œ∫‚ÇÄ, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, h‚ÇÑ‚ü© : ‚àÉ (Œ∫‚ÇÄ : Cardinal), Cardinal.aleph0 ‚â§ Œ∫‚ÇÄ ‚àß Œ∫‚ÇÄ < Œ∫ ‚àß
      Cardinal.mk Œπ ‚â§ Œ∫‚ÇÄ ‚àß ‚àÄ i, Cardinal.mk (Œ± i) ‚â§ Œ∫‚ÇÄ := sorry
    obtain ‚ü®X, rfl‚ü© := Œ∫‚ÇÄ.mk_surjective
    rw [hasCardinalLT_iff_cardinal_mk_lt]
    obtain ‚ü®œÜ‚ü© := h‚ÇÉ
    let œà (i : Œπ) : Œ± i ‚Ü™ X := (h‚ÇÑ i).some
    refine lt_of_le_of_lt ?_ h‚ÇÇ
    trans Cardinal.mk (X √ó X)
    ¬∑ refine ‚ü®‚ü®fun ‚ü®i, a‚ü© ‚Ü¶ ‚ü®œÜ i, œà i a‚ü©, fun ‚ü®i, a‚ü© ‚ü®j, b‚ü© h ‚Ü¶ ?_‚ü©‚ü©
      rw [Prod.ext_iff] at h
      obtain rfl : i = j := œÜ.injective h.1
      obtain rfl : a = b := (œà i).injective h.2
      rfl
    ¬∑ rw [‚Üê Cardinal.mul_def]
      exact (Cardinal.mul_le_max_of_aleph0_le_left h‚ÇÅ).trans (by simp)
  ¬∑ simp only [hasCardinalLT_aleph0_iff] at hŒπ hŒ± ‚ä¢
    infer_instance

@[simp]
lemma hasCardinalLT_lift_iff (X : Type v) (Œ∫ : Cardinal.{w}) :
    HasCardinalLT X (Cardinal.lift.{u} Œ∫) ‚Üî HasCardinalLT X Œ∫ := by
  simp [HasCardinalLT, ‚Üê (Cardinal.lift_strictMono.{max v w, max u}).lt_iff_lt]

@[simp]
lemma hasCardinalLT_ulift_iff (X : Type v) (Œ∫ : Cardinal.{w}) :
    HasCardinalLT (ULift.{u} X) Œ∫ ‚Üî HasCardinalLT X Œ∫ :=
  hasCardinalLT_iff_of_equiv Equiv.ulift Œ∫

lemma Cardinal.IsRegular.lift {Œ∫ : Cardinal.{w}} (hŒ∫ : Œ∫.IsRegular) :
    (Cardinal.lift.{u} Œ∫).IsRegular := by
  obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := hŒ∫
  constructor
  ¬∑ simpa
  ¬∑ rwa [‚Üê Cardinal.lift_ord, ‚Üê Ordinal.lift_cof, lift_le]

lemma hasCardinalLT_sigma {Œπ : Type u} (Œ± : Œπ ‚Üí Type v) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    (hŒπ : HasCardinalLT Œπ Œ∫) (hŒ± : ‚àÄ i, HasCardinalLT (Œ± i) Œ∫) :
    HasCardinalLT (Œ£ i, Œ± i) Œ∫ := by
  let Œπ' : Type max u v w := ULift.{max v w} Œπ
  let Œ±' (i : Œπ') : Type max u v w := ULift.{max u w} (Œ± (ULift.down i))
  let Œ∫' : Cardinal.{max u v w} := Cardinal.lift.{max u v} Œ∫
  have : Fact Œ∫'.IsRegular := ‚ü®Cardinal.IsRegular.lift Fact.out‚ü©
  have := hasCardinalLT_sigma' Œ±' Œ∫'
    (by rwa [hasCardinalLT_lift_iff, hasCardinalLT_ulift_iff]) (fun i ‚Ü¶ by
      rw [hasCardinalLT_lift_iff, hasCardinalLT_ulift_iff]
      exact hŒ± _)
  rw [hasCardinalLT_lift_iff] at this
  exact this.of_surjective (fun ‚ü®i, a‚ü© ‚Ü¶ ‚ü®ULift.down i, ULift.down a‚ü©)
    (fun ‚ü®i, a‚ü© ‚Ü¶ ‚ü®‚ü®ULift.up i, ULift.up a‚ü©, rfl‚ü©)

lemma hasCardinalLT_subtype_iSup
    {Œπ : Type*} {X : Type*} (P : Œπ ‚Üí X ‚Üí Prop) {Œ∫ : Cardinal} [Fact Œ∫.IsRegular]
    (hŒπ : HasCardinalLT Œπ Œ∫) (hP : ‚àÄ i, HasCardinalLT (Subtype (P i)) Œ∫) :
    HasCardinalLT (Subtype (‚®Ü i, P i)) Œ∫ :=
  (hasCardinalLT_sigma (fun i ‚Ü¶ Subtype (P i)) Œ∫ hŒπ hP).of_surjective
    (fun ‚ü®i, x, hx‚ü© ‚Ü¶ ‚ü®x, by simp only [iSup_apply, iSup_Prop_eq]; exact ‚ü®i, hx‚ü©‚ü©) (by
    rintro ‚ü®_, h‚ü©
    simp only [iSup_apply, iSup_Prop_eq] at h
    obtain ‚ü®i, hi‚ü© := h
    exact ‚ü®‚ü®i, _, hi‚ü©, rfl‚ü©)

lemma hasCardinalLT_iUnion
    {Œπ : Type*} {X : Type*} (S : Œπ ‚Üí Set X) {Œ∫ : Cardinal} [Fact Œ∫.IsRegular]
    (hŒπ : HasCardinalLT Œπ Œ∫) (hS : ‚àÄ i, HasCardinalLT (S i) Œ∫) :
    HasCardinalLT (‚ãÉ i, S i) Œ∫ := by
  convert hasCardinalLT_subtype_iSup S hŒπ hS using 2
  ext x
  change _ ‚Üî ((‚®Ü i, S i) : X ‚Üí Prop ) x
  aesop

lemma hasCardinalLT_subtype_max
    {X : Type*} {P‚ÇÅ P‚ÇÇ : X ‚Üí Prop} {Œ∫ : Cardinal} (hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫)
    (h‚ÇÅ : HasCardinalLT (Subtype P‚ÇÅ) Œ∫) (h‚ÇÇ : HasCardinalLT (Subtype P‚ÇÇ) Œ∫) :
    HasCardinalLT (Subtype (P‚ÇÅ ‚äî P‚ÇÇ)) Œ∫ := by
  have : HasCardinalLT (Subtype P‚ÇÅ ‚äï Subtype P‚ÇÇ) Œ∫ := by
    rw [hasCardinalLT_sum_iff _ _ _ hŒ∫]
    exact ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  refine this.of_surjective (Sum.elim (fun x ‚Ü¶ ‚ü®x.1, Or.inl x.2‚ü©)
    (fun x ‚Ü¶ ‚ü®x.1, Or.inr x.2‚ü©)) ?_
  rintro ‚ü®x, hx | hx‚ü©
  ¬∑ exact ‚ü®Sum.inl ‚ü®x, hx‚ü©, rfl‚ü©
  ¬∑ exact ‚ü®Sum.inr ‚ü®x, hx‚ü©, rfl‚ü©

lemma hasCardinalLT_union
    {X : Type*} {S‚ÇÅ S‚ÇÇ : Set X} {Œ∫ : Cardinal} [Fact Œ∫.IsRegular]
    (h‚ÇÅ : HasCardinalLT S‚ÇÅ Œ∫) (h‚ÇÇ : HasCardinalLT S‚ÇÇ Œ∫) :
    HasCardinalLT (S‚ÇÅ ‚à™ S‚ÇÇ : Set _) Œ∫ :=
  hasCardinalLT_subtype_max (Cardinal.IsRegular.aleph0_le Fact.out) h‚ÇÅ h‚ÇÇ

lemma hasCardinalLT_prod {T‚ÇÅ T‚ÇÇ : Type*} {Œ∫ : Cardinal} [Fact Œ∫.IsRegular]
    (h‚ÇÅ : HasCardinalLT T‚ÇÅ Œ∫) (h‚ÇÇ : HasCardinalLT T‚ÇÇ Œ∫) :
    HasCardinalLT (T‚ÇÅ √ó T‚ÇÇ) Œ∫ :=
  (hasCardinalLT_sigma (fun (t : T‚ÇÅ) ‚Ü¶ T‚ÇÇ) Œ∫ h‚ÇÅ (fun _ ‚Ü¶ h‚ÇÇ)).of_surjective
    (fun ‚ü®t‚ÇÅ, t‚ÇÇ‚ü© ‚Ü¶ (t‚ÇÅ, t‚ÇÇ)) (by rintro ‚ü®t‚ÇÅ, t‚ÇÇ‚ü©; exact ‚ü®‚ü®t‚ÇÅ, t‚ÇÇ‚ü©, rfl‚ü©)

section

open CategoryTheory Limits

lemma hasCardinalLT_toSet_morphismPropertyOfHoms {C : Type*} [Category C]
    {Œπ : Type*} {X Y : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ Y i)
    {Œ∫ : Cardinal}
    (h : HasCardinalLT Œπ Œ∫) : HasCardinalLT (MorphismProperty.ofHoms f).toSet Œ∫ :=
  h.of_surjective (fun i ‚Ü¶ ‚ü®Arrow.mk (f i), ‚ü®i‚ü©‚ü©) (by
    rintro ‚ü®f, hf‚ü©
    rw [MorphismProperty.mem_toSet_iff, MorphismProperty.ofHoms_iff] at hf
    obtain ‚ü®i, hf‚ü© := hf
    obtain rfl : f = _ := hf
    exact ‚ü®i, rfl‚ü©)

lemma hasCardinalLT_subtype_objectPropertyOfObj {C : Type*} [Category C]
    {Œπ : Type*} (X : Œπ ‚Üí C) {Œ∫ : Cardinal}
    (h : HasCardinalLT Œπ Œ∫) : HasCardinalLT (Subtype (ObjectProperty.ofObj X)) Œ∫ :=
  h.of_surjective (fun i ‚Ü¶ ‚ü®X i, by simp‚ü©) (by rintro ‚ü®_, ‚ü®i‚ü©‚ü©; exact ‚ü®i, rfl‚ü©)

lemma hasCardinalLT_arrow_walkingMultispan {h : MultispanShape} {Œ∫ : Cardinal}
    [Fact Œ∫.IsRegular]
    (h‚ÇÅ : HasCardinalLT h.L Œ∫) (h‚ÇÇ : HasCardinalLT h.R Œ∫) :
    HasCardinalLT (Arrow (WalkingMultispan h)) Œ∫ := by
  let T := WalkingMultispan h ‚äï h.L ‚äï h.L
  have hT‚ÇÄ : HasCardinalLT (WalkingMultispan h) Œ∫ := by
    have : HasCardinalLT (h.L ‚äï h.R) Œ∫ := by
      rw [hasCardinalLT_sum_iff _ _ _ (Cardinal.IsRegular.aleph0_le Fact.out)]
      exact ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    refine this.of_surjective (Sum.elim WalkingMultispan.left WalkingMultispan.right) ?_
    rintro (x | y)
    ¬∑ exact ‚ü®Sum.inl x, rfl‚ü©
    ¬∑ exact ‚ü®Sum.inr y, rfl‚ü©
  have hT : HasCardinalLT T Œ∫ := by
    rw [hasCardinalLT_sum_iff _ _ _ (Cardinal.IsRegular.aleph0_le Fact.out),
      hasCardinalLT_sum_iff _ _ _ (Cardinal.IsRegular.aleph0_le Fact.out)]
    exact ‚ü®hT‚ÇÄ, h‚ÇÅ, h‚ÇÅ‚ü©
  refine hT.of_surjective (fun t ‚Ü¶ match t with
    | Sum.inl x => Arrow.mk (ùüô x)
    | Sum.inr (Sum.inl z) => Arrow.mk (WalkingMultispan.Hom.fst z)
    | Sum.inr (Sum.inr z) => Arrow.mk (WalkingMultispan.Hom.snd z)) ?_
  intro f
  obtain ‚ü®x, y, f, rfl‚ü© := f.mk_surjective
  obtain (_ | z | z) := f
  ¬∑ exact ‚ü®Sum.inl x, rfl‚ü©
  ¬∑ exact ‚ü®Sum.inr (Sum.inl z), rfl‚ü©
  ¬∑ exact ‚ü®Sum.inr (Sum.inr z), rfl‚ü©

end

namespace CategoryTheory

open Limits

namespace Functor.Final

variable {C D : Type*} [Category C] [Category D]

instance [IsFiltered D] : (Prod.fst C D).Final := by
  let F : D ‚•§ Discrete PUnit.{1} := (Functor.const _).obj (Discrete.mk .unit)
  have hF : F.Final := Functor.final_of_isFiltered_of_pUnit _
  change (Functor.prod (ùü≠ C) F ‚ãô (prod.rightUnitorEquivalence.{0} C).functor).Final
  infer_instance

end Functor.Final

namespace IsCardinalFiltered

instance prod (J‚ÇÅ J‚ÇÇ : Type*) [Category J‚ÇÅ] [Category J‚ÇÇ]
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [IsCardinalFiltered J‚ÇÅ Œ∫] [IsCardinalFiltered J‚ÇÇ Œ∫] :
    IsCardinalFiltered (J‚ÇÅ √ó J‚ÇÇ) Œ∫ where
  nonempty_cocone {C _} F hC := ‚ü®by
    let c‚ÇÅ := cocone (F ‚ãô Prod.fst _ _) hC
    let c‚ÇÇ := cocone (F ‚ãô Prod.snd _ _) hC
    exact
      { pt := (c‚ÇÅ.pt, c‚ÇÇ.pt)
        Œπ.app i := (c‚ÇÅ.Œπ.app i, c‚ÇÇ.Œπ.app i)
        Œπ.naturality i j f := by
          ext
          ¬∑ simpa using c‚ÇÅ.w f
          ¬∑ simpa using c‚ÇÇ.w f}‚ü©

variable (J : Type w) [SmallCategory J] (Œ∫ : Cardinal.{w})

namespace exists_cardinal_directed

@[ext]
structure PreDiagram where
  W : MorphismProperty J
  P : ObjectProperty J
  src {i j : J} {f : i ‚ü∂ j} : W f ‚Üí P i
  tgt {i j : J} {f : i ‚ü∂ j} : W f ‚Üí P j
  hW : HasCardinalLT W.toSet Œ∫
  hP : HasCardinalLT (Subtype P) Œ∫

namespace PreDiagram

variable {J Œ∫}

structure Terminal (D : PreDiagram J Œ∫) (e : J) where
  prop_id : D.W (ùüô e)
  lift {j : J} (hj : D.P j) : j ‚ü∂ e
  hlift {j : J} (hj : D.P j) : D.W (lift hj)
  uniq {j : J} (hj : D.P j) {œÜ : j ‚ü∂ e} (hœÜ : D.W œÜ) : lift hj = œÜ
  comm {i j : J} (f : i ‚ü∂ j) (hf : D.W f) : f ‚â´ lift (D.tgt hf) = lift (D.src hf)

namespace Terminal

attribute [reassoc] Terminal.comm

variable {D : PreDiagram J Œ∫} {e : J}

lemma prop (h : D.Terminal e) : D.P e := D.src (h.prop_id)

@[simp]
lemma lift_self (h : D.Terminal e) : h.lift h.prop = ùüô e := h.uniq _ h.prop_id

instance : Subsingleton (D.Terminal e) where
  allEq h‚ÇÅ h‚ÇÇ := by
    have : @h‚ÇÅ.lift = @h‚ÇÇ.lift := by
      ext j hj
      exact h‚ÇÅ.uniq hj (h‚ÇÇ.hlift hj)
    cases h‚ÇÅ
    cases h‚ÇÇ
    aesop

noncomputable def ofExistsUnique (prop_id : D.W (ùüô e))
    (h‚ÇÅ : ‚àÄ ‚¶Éj : J‚¶Ñ (_ : D.P j), ‚àÉ (lift : j ‚ü∂ e), D.W lift)
    (h‚ÇÇ : ‚àÄ ‚¶Éj : J‚¶Ñ (_ : D.P j) (l‚ÇÅ l‚ÇÇ : j ‚ü∂ e), D.W l‚ÇÅ ‚Üí D.W l‚ÇÇ ‚Üí l‚ÇÅ = l‚ÇÇ)
    (h‚ÇÉ : ‚àÄ ‚¶Éi j : J‚¶Ñ (f : i ‚ü∂ j) (_ : D.W f), ‚àÉ (li : i ‚ü∂ e) (lj : j ‚ü∂ e),
      D.W li ‚àß D.W lj ‚àß f ‚â´ lj = li) :
    D.Terminal e where
  prop_id := prop_id
  lift hj := (h‚ÇÅ hj).choose
  hlift hj := (h‚ÇÅ hj).choose_spec
  uniq hj œÜ hœÜ := h‚ÇÇ hj (h‚ÇÅ hj).choose œÜ (h‚ÇÅ hj).choose_spec hœÜ
  comm _ hf := by
    obtain ‚ü®li, lj, hli, hlj, fac‚ü© := h‚ÇÉ _ hf
    rw [h‚ÇÇ (D.src hf) _ li (h‚ÇÅ (D.src hf)).choose_spec hli,
      h‚ÇÇ (D.tgt hf) _ lj (h‚ÇÅ (D.tgt hf)).choose_spec hlj, fac]

end Terminal

end PreDiagram

structure Diagram extends PreDiagram J Œ∫ where
  e : J
  terminal : toPreDiagram.Terminal e
  uniq_terminal (j : J) (hj : toPreDiagram.Terminal j) : j = e

@[ext]
lemma Diagram.ext {D‚ÇÅ D‚ÇÇ : Diagram J Œ∫} (hW : D‚ÇÅ.W = D‚ÇÇ.W) (hP : D‚ÇÅ.P = D‚ÇÇ.P) : D‚ÇÅ = D‚ÇÇ := by
  obtain ‚ü®D‚ÇÅ, e, h‚ÇÅ, h‚ÇÅ'‚ü© := D‚ÇÅ
  obtain ‚ü®D‚ÇÇ, e', h‚ÇÇ, h‚ÇÇ'‚ü© := D‚ÇÇ
  obtain rfl : D‚ÇÅ = D‚ÇÇ := by aesop
  obtain rfl : e = e' := h‚ÇÇ' _ h‚ÇÅ
  obtain rfl : h‚ÇÅ = h‚ÇÇ := by subsingleton
  rfl

instance : PartialOrder (Diagram J Œ∫) where
  le D‚ÇÅ D‚ÇÇ := D‚ÇÅ.W ‚â§ D‚ÇÇ.W ‚àß D‚ÇÅ.P ‚â§ D‚ÇÇ.P
  le_refl _ := ‚ü®by rfl, by rfl‚ü©
  le_trans _ _ _ h‚ÇÅ h‚ÇÇ := ‚ü®h‚ÇÅ.1.trans h‚ÇÇ.1, h‚ÇÅ.2.trans h‚ÇÇ.2‚ü©
  le_antisymm _ _ h‚ÇÅ h‚ÇÇ := by
    ext : 1
    ¬∑ exact le_antisymm h‚ÇÅ.1 h‚ÇÇ.1
    ¬∑ exact le_antisymm h‚ÇÅ.2 h‚ÇÇ.2

section

variable {J Œ∫}

def functorMap {D‚ÇÅ D‚ÇÇ : Diagram J Œ∫} (h : D‚ÇÅ ‚â§ D‚ÇÇ) : D‚ÇÅ.e ‚ü∂ D‚ÇÇ.e :=
  D‚ÇÇ.terminal.lift (h.2 _ D‚ÇÅ.terminal.prop)

@[simp]
lemma functorMap_id (D : Diagram J Œ∫) : functorMap (le_refl D) = ùüô D.e := by
  simp [functorMap]

@[reassoc (attr := simp)]
lemma functorMap_comp {D‚ÇÅ D‚ÇÇ D‚ÇÉ : Diagram J Œ∫} (h‚ÇÅ‚ÇÇ : D‚ÇÅ ‚â§ D‚ÇÇ) (h‚ÇÇ‚ÇÉ : D‚ÇÇ ‚â§ D‚ÇÉ) :
    functorMap h‚ÇÅ‚ÇÇ ‚â´ functorMap h‚ÇÇ‚ÇÉ = functorMap (h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ÇÉ) :=
  D‚ÇÉ.terminal.comm _ (h‚ÇÇ‚ÇÉ.1 _ (D‚ÇÇ.terminal.hlift _))

end

@[simps]
def functor : Diagram J Œ∫ ‚•§ J where
  obj D := D.e
  map h := functorMap (leOfHom h)

variable [Fact Œ∫.IsRegular]

variable {J Œ∫} in
@[simps]
def PreDiagram.single (j : J) : PreDiagram J Œ∫ where
  W := .ofHoms (fun (_ : Unit) ‚Ü¶ ùüô j)
  P := .ofObj (fun (_ : Unit) ‚Ü¶ j)
  src := by rintro _ _ _ ‚ü®‚ü©; exact ‚ü®‚ü®‚ü©‚ü©
  tgt := by rintro _ _ _ ‚ü®‚ü©; exact ‚ü®‚ü®‚ü©‚ü©
  hW :=
    (hasCardinalLT_punit Œ∫ (Cardinal.IsRegular.aleph0_le Fact.out)).of_surjective
        (fun (_ : Unit) ‚Ü¶ ‚ü®Arrow.mk (ùüô j), ‚ü®‚ü®‚ü©‚ü©‚ü©) (by
      rintro ‚ü®f, hf‚ü©
      refine ‚ü®‚ü®‚ü©, ?_‚ü©
      ext
      exact ((MorphismProperty.ofHoms_iff _ _).1
        ((MorphismProperty.arrow_mk_mem_toSet_iff _ _).1 hf)).choose_spec.symm)
  hP :=
    (hasCardinalLT_punit Œ∫ (Cardinal.IsRegular.aleph0_le Fact.out)).of_surjective
      (fun (_ : Unit) ‚Ü¶ ‚ü®j, by simp‚ü©) (fun ‚ü®k, hk‚ü© ‚Ü¶ ‚ü®‚ü®‚ü©, by aesop‚ü©)

instance (j : J) : Finite (Subtype (PreDiagram.single (Œ∫ := Œ∫) j).P) :=
  Finite.of_surjective (fun (_ : Unit) ‚Ü¶ ‚ü®j, by simp‚ü©)
    (by rintro ‚ü®_, ‚ü®‚ü©‚ü©; exact ‚ü®‚ü®‚ü©, rfl‚ü©)

variable {J Œ∫} in
def Diagram.single (j : J) : Diagram J Œ∫ where
  toPreDiagram := .single j
  e := j
  terminal :=
    { prop_id := ‚ü®‚ü®‚ü©‚ü©
      lift := by rintro j hj; simp at hj; subst hj; exact ùüô _
      hlift := by rintro j hj; simp at hj; subst hj; exact ‚ü®‚ü®‚ü©‚ü©
      uniq := by rintro j hj œÜ hœÜ; simp at hj; subst hj; obtain ‚ü®‚ü®‚ü©‚ü© := hœÜ; simp
      comm := by rintro _ _ f hf; obtain ‚ü®‚ü®‚ü©‚ü© := hf; simp }
  uniq_terminal := by rintro _ ‚ü®‚ü®‚ü©‚ü©; rfl

variable {J Œ∫} in
@[simps]
def PreDiagram.iSup {Œπ : Type*} (D : Œπ ‚Üí PreDiagram J Œ∫) (hŒπ : HasCardinalLT Œπ Œ∫) :
    PreDiagram J Œ∫ where
  W := ‚®Ü (i : Œπ), (D i).W
  P := ‚®Ü (i : Œπ), (D i).P
  src hf := by
    simp at hf ‚ä¢
    obtain ‚ü®i, hi‚ü© := hf
    exact ‚ü®i, (D i).src hi‚ü©
  tgt hf := by
    simp at hf ‚ä¢
    obtain ‚ü®i, hi‚ü© := hf
    exact ‚ü®i, (D i).tgt hi‚ü©
  hW := by
    rw [MorphismProperty.toSet_iSup]
    exact hasCardinalLT_iUnion _ hŒπ (fun i ‚Ü¶ (D i).hW)
  hP := hasCardinalLT_subtype_iSup _ hŒπ (fun i ‚Ü¶ (D i).hP)

variable {J Œ∫} in
@[simps]
def PreDiagram.max (D‚ÇÅ D‚ÇÇ : PreDiagram J Œ∫) :
    PreDiagram J Œ∫ where
  W := D‚ÇÅ.W ‚äî D‚ÇÇ.W
  P := D‚ÇÅ.P ‚äî D‚ÇÇ.P
  src := by
    rintro _ _ _ (h | h)
    ¬∑ exact Or.inl (D‚ÇÅ.src h)
    ¬∑ exact Or.inr (D‚ÇÇ.src h)
  tgt := by
    rintro _ _ _ (h | h)
    ¬∑ exact Or.inl (D‚ÇÅ.tgt h)
    ¬∑ exact Or.inr (D‚ÇÇ.tgt h)
  hW := hasCardinalLT_union D‚ÇÅ.hW D‚ÇÇ.hW
  hP := hasCardinalLT_union D‚ÇÅ.hP D‚ÇÇ.hP

variable [IsCardinalFiltered J Œ∫]
  (hJ : ‚àÄ (e : J), ‚àÉ (m : J) (_ : e ‚ü∂ m), IsEmpty (m ‚ü∂ e))

include hJ

lemma isCardinalFiltered : IsCardinalFiltered (Diagram J Œ∫) Œ∫ :=
  isCardinalFiltered_preorder _ _ (fun Œπ D hŒπ ‚Ü¶ by
    simp only [‚Üê hasCardinalLT_iff_cardinal_mk_lt] at hŒπ
    choose m‚ÇÄ t‚ÇÄ hm‚ÇÄ using fun i ‚Ü¶ hJ (D i).e
    let m‚ÇÅ := IsCardinalFiltered.max m‚ÇÄ hŒπ
    let t‚ÇÅ (i : Œπ) : m‚ÇÄ i ‚ü∂ m‚ÇÅ := IsCardinalFiltered.toMax m‚ÇÄ hŒπ i
    let u (i : Œπ) : (D i).e ‚ü∂ m‚ÇÅ := t‚ÇÄ i ‚â´ t‚ÇÅ i
    obtain ‚ü®m‚ÇÇ, t‚ÇÇ, hm‚ÇÇ‚ü© : ‚àÉ (m‚ÇÇ : J) (t‚ÇÇ : m‚ÇÅ ‚ü∂ m‚ÇÇ),
      ‚àÄ (i‚ÇÅ i‚ÇÇ : Œπ) (j : J) (hj‚ÇÅ : (D i‚ÇÅ).P j) (hj‚ÇÇ : (D i‚ÇÇ).P j),
        (D i‚ÇÅ).terminal.lift hj‚ÇÅ ‚â´ u i‚ÇÅ ‚â´ t‚ÇÇ = (D i‚ÇÇ).terminal.lift hj‚ÇÇ ‚â´ u i‚ÇÇ ‚â´ t‚ÇÇ := by
      let S := { x : Œπ √ó Œπ √ó J // (D x.1).P x.2.2 ‚àß (D x.2.1).P x.2.2 }
      let shape : MultispanShape.{w, w} :=
        { L := { x : Œπ √ó Œπ √ó J // (D x.1).P x.2.2 ‚àß (D x.2.1).P x.2.2 }
          R := PUnit
          fst _ := ‚ü®‚ü©
          snd _ := ‚ü®‚ü© }
      let index : MultispanIndex shape J :=
        { left x := x.1.2.2
          right _ := m‚ÇÅ
          fst x := (D x.1.1).terminal.lift x.2.1 ‚â´ u x.1.1
          snd x := (D x.1.2.1).terminal.lift x.2.2 ‚â´ u x.1.2.1 }
      have hshape : HasCardinalLT (Arrow (WalkingMultispan shape)) Œ∫ := by
        refine hasCardinalLT_arrow_walkingMultispan ?_
          (hasCardinalLT_of_finite _ _ (Cardinal.IsRegular.aleph0_le Fact.out))
        let T := Œ£ (i : Œπ), Subtype (D i).P
        have hT : HasCardinalLT (Œπ √ó T) Œ∫ :=
          hasCardinalLT_prod hŒπ (hasCardinalLT_sigma _ _ hŒπ (fun i ‚Ü¶ (D i).hP))
        refine hT.of_injective (fun ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ, j‚ü©, h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ‚ü®i‚ÇÅ, i‚ÇÇ, ‚ü®j, h‚ÇÇ‚ü©‚ü©) ?_
        rintro ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ, j‚ü©, _, _‚ü© ‚ü®‚ü®i‚ÇÅ', i‚ÇÇ', j'‚ü©, _, _‚ü© h
        rw [Prod.ext_iff, Sigma.ext_iff] at h
        dsimp at h
        obtain rfl : i‚ÇÅ = i‚ÇÅ' := h.1
        obtain rfl : i‚ÇÇ = i‚ÇÇ' := h.2.1
        obtain rfl : j = j' := by simpa using h
        rfl
      let c : Multicofork _ := IsCardinalFiltered.cocone index.multispan hshape
      exact ‚ü®c.pt, c.œÄ ‚ü®‚ü©, fun i‚ÇÅ i‚ÇÇ j h‚ÇÅ h‚ÇÇ ‚Ü¶ by
        simpa [index, shape] using c.condition ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ, j‚ü©, h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
    let œÜ (x : (Œ£ (i : Œπ), (Subtype (D i).P))) : x.2.1 ‚ü∂ m‚ÇÇ :=
      (D x.1).terminal.lift x.2.2 ‚â´ u x.1 ‚â´ t‚ÇÇ
    let D‚ÇÄ := PreDiagram.iSup (fun i ‚Ü¶ (D i).toPreDiagram) hŒπ
    have hD‚ÇÄ {i : Œπ} : ¬¨ (D i).P m‚ÇÇ := fun hi ‚Ü¶
      (hm‚ÇÄ i).false (t‚ÇÅ _ ‚â´ t‚ÇÇ ‚â´ (D i).terminal.lift hi)
    let D‚ÇÅ := D‚ÇÄ.max (.single m‚ÇÇ)
    let D‚ÇÇ : PreDiagram J Œ∫ :=
      { W := D‚ÇÅ.W ‚äî .ofHoms œÜ
        P := D‚ÇÅ.P
        src := by
          simp [D‚ÇÅ, D‚ÇÄ]
          rintro _ _ _ ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®i, j, hj‚ü©)
          ¬∑ simp at hf
            obtain ‚ü®i, hf‚ü© := hf
            exact Or.inl ‚ü®i, (D i).src hf‚ü©
          ¬∑ exact Or.inr rfl
          ¬∑ exact Or.inl ‚ü®i, hj‚ü©
        tgt := by
          simp [D‚ÇÅ, D‚ÇÄ]
          rintro _ _ _ ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®i, j, hj‚ü©)
          ¬∑ simp at hf
            obtain ‚ü®i, hf‚ü© := hf
            exact Or.inl ‚ü®i, (D i).tgt hf‚ü©
          ¬∑ exact Or.inr rfl
          ¬∑ exact Or.inr rfl
        hW := by
          rw [MorphismProperty.toSet_max]
          exact hasCardinalLT_union D‚ÇÅ.hW
            (hasCardinalLT_toSet_morphismPropertyOfHoms _
              (hasCardinalLT_sigma _ _ hŒπ (fun i ‚Ü¶ (D i).hP)))
        hP := hasCardinalLT_subtype_max (Cardinal.IsRegular.aleph0_le Fact.out)
                (hasCardinalLT_subtype_iSup _ hŒπ (fun i ‚Ü¶ (D i).hP))
                (hasCardinalLT_of_finite _ _ (Cardinal.IsRegular.aleph0_le Fact.out))
            }
    have hD‚ÇÇ {f : m‚ÇÇ ‚ü∂ m‚ÇÇ} (hf : D‚ÇÇ.W f) : f = ùüô _ := by
      simp [D‚ÇÇ, D‚ÇÅ, D‚ÇÄ] at hf
      obtain ((hf | ‚ü®‚ü®‚ü©‚ü©) | hf) := hf
      ¬∑ simp at hf
        obtain ‚ü®i, hi‚ü© := hf
        exact (hD‚ÇÄ ((D i).src hi)).elim
      ¬∑ rfl
      ¬∑ rw [MorphismProperty.ofHoms_iff] at hf
        obtain ‚ü®‚ü®i, j, hj‚ü©, hi‚ü© := hf
        obtain rfl : m‚ÇÇ = j := congr_arg Arrow.leftFunc.obj hi
        exact (hD‚ÇÄ hj).elim
    let he : D‚ÇÇ.Terminal m‚ÇÇ := by
      have H {i : Œπ} {j : J} (hj : (D i).P j) {f : j ‚ü∂ m‚ÇÇ} (hf : D‚ÇÇ.W f) :
          f = œÜ ‚ü®i, ‚ü®_, hj‚ü©‚ü© := by
        simp [D‚ÇÇ, D‚ÇÅ, D‚ÇÄ] at hf
        obtain ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®‚ü®i', j, hj'‚ü©‚ü©) := hf
        ¬∑ simp at hf
          obtain ‚ü®i, hf‚ü© := hf
          exact (hD‚ÇÄ ((D i).tgt hf)).elim
        ¬∑ exact (hD‚ÇÄ hj).elim
        ¬∑ apply hm‚ÇÇ
      refine .ofExistsUnique ?_ ?_ ?_ ?_
      ¬∑ exact Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©)
      ¬∑ simp [D‚ÇÇ, D‚ÇÅ, D‚ÇÄ]
        rintro j (‚ü®i, hi‚ü© | rfl)
        ¬∑ exact ‚ü®œÜ ‚ü®i, _, hi‚ü©, Or.inr (.mk _)‚ü©
        ¬∑ exact ‚ü®ùüô _, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©)‚ü©
      ¬∑ intro j hj l‚ÇÅ l‚ÇÇ hl‚ÇÅ hl‚ÇÇ
        simp [D‚ÇÇ, D‚ÇÅ, D‚ÇÄ] at hj
        obtain (‚ü®i, hj‚ü© | rfl) := hj
        ¬∑ obtain rfl := H hj hl‚ÇÅ
          obtain rfl := H hj hl‚ÇÇ
          rfl
        ¬∑ rw [hD‚ÇÇ hl‚ÇÅ, hD‚ÇÇ hl‚ÇÇ]
      ¬∑ rintro j k f ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®‚ü®i, j, hj‚ü©‚ü©)
        ¬∑ simp [D‚ÇÄ] at hf
          obtain ‚ü®i, hf‚ü© := hf
          exact ‚ü®œÜ ‚ü®i, j, (D i).src hf‚ü©, œÜ ‚ü®i, k, (D i).tgt hf‚ü©, Or.inr ‚ü®_‚ü©, Or.inr ‚ü®_‚ü©,
            by simp [œÜ, (D i).terminal.comm_assoc _ hf]‚ü©
        ¬∑ exact ‚ü®ùüô _, ùüô _, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), by simp‚ü©
        ¬∑ exact ‚ü®œÜ ‚ü®i, j, hj‚ü©, ùüô _, Or.inr ‚ü®_‚ü©, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), by simp‚ü©
    let D‚ÇÇ' : Diagram J Œ∫ :=
      { toPreDiagram := D‚ÇÇ
        e := _
        terminal := he
        uniq_terminal j hj := by
          have := hj.prop
          simp [D‚ÇÇ, D‚ÇÅ, D‚ÇÄ] at this
          obtain (‚ü®i, hi‚ü© | rfl) := this
          ¬∑ exfalso
            exact (hm‚ÇÄ i).false (t‚ÇÅ _ ‚â´ t‚ÇÇ ‚â´ hj.lift
              (by simp [D‚ÇÇ, D‚ÇÅ]) ‚â´ (D i).terminal.lift hi)
          ¬∑ rfl }
    refine ‚ü®D‚ÇÇ', fun i ‚Ü¶ ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ exact le_trans (le_trans (le_trans (by rfl) (le_iSup _ i))
        le_sup_left) le_sup_left
    ¬∑ exact le_trans (le_trans (by rfl) (le_iSup _ i)) le_sup_left)

lemma final_functor : (functor J Œ∫).Final := by
  have := isCardinalFiltered J Œ∫ hJ
  have := isFiltered_of_isCardinalFiltered J Œ∫
  have := isFiltered_of_isCardinalFiltered (Diagram J Œ∫) Œ∫
  rw [Functor.final_iff_of_isFiltered]
  refine ‚ü®fun j ‚Ü¶ ‚ü®.single j, ‚ü®ùüô _‚ü©‚ü©, fun {j D} (f‚ÇÅ f‚ÇÇ : j ‚ü∂ D.e) ‚Ü¶ ?_‚ü©
  obtain ‚ü®m‚ÇÄ, t, hm‚ÇÄ‚ü© := hJ D.e
  obtain ‚ü®m‚ÇÅ, u, hu‚ü© : ‚àÉ (m‚ÇÅ : J) (u : m‚ÇÄ ‚ü∂ m‚ÇÅ), f‚ÇÅ ‚â´ t ‚â´ u = f‚ÇÇ ‚â´ t ‚â´ u :=
    ‚ü®_, IsFiltered.coeqHom (f‚ÇÅ ‚â´ t) (f‚ÇÇ ‚â´ t),
      by simpa using IsFiltered.coeq_condition (f‚ÇÅ ‚â´ t) (f‚ÇÇ ‚â´ t)‚ü©
  have h‚ÇÅ : ¬¨ (D.P m‚ÇÅ) := fun h‚ÇÅ ‚Ü¶ hm‚ÇÄ.false (u ‚â´ D.terminal.lift h‚ÇÅ)
  let œÜ (x : Subtype D.P) : x.1 ‚ü∂ m‚ÇÅ := D.terminal.lift x.2 ‚â´ t ‚â´ u
  let D‚ÇÄ := D.toPreDiagram.max (.single m‚ÇÅ)
  let D‚ÇÅ : PreDiagram J Œ∫ :=
    { W := D‚ÇÄ.W ‚äî .ofHoms œÜ
      P := D‚ÇÄ.P
      src := by
        rintro i j f (hf | ‚ü®‚ü®j, hj‚ü©‚ü©)
        ¬∑ exact D‚ÇÄ.src hf
        ¬∑ exact Or.inl hj
      tgt := by
        rintro i j f (hf | ‚ü®‚ü®j, hj‚ü©‚ü©)
        ¬∑ exact D‚ÇÄ.tgt hf
        ¬∑ exact Or.inr ‚ü®‚ü®‚ü©‚ü©
      hW :=
        hasCardinalLT_union
          (hasCardinalLT_union D.hW
            (hasCardinalLT_toSet_morphismPropertyOfHoms _
            (hasCardinalLT_of_finite _ _ (Cardinal.IsRegular.aleph0_le Fact.out))))
          (hasCardinalLT_toSet_morphismPropertyOfHoms _ D.hP)
      hP :=
        hasCardinalLT_union D.hP
          (hasCardinalLT_subtype_objectPropertyOfObj _
            ((hasCardinalLT_of_finite _ _ (Cardinal.IsRegular.aleph0_le Fact.out)))) }
  have h‚ÇÇ {j : J} (hj : D.P j) {f : j ‚ü∂ m‚ÇÅ} (hf : D‚ÇÅ.W f) :
      f = œÜ ‚ü®_, hj‚ü© := by
    obtain ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®‚ü®‚ü©‚ü©) := hf
    ¬∑ exact (h‚ÇÅ (D.tgt hf)).elim
    ¬∑ exact (h‚ÇÅ hj).elim
    ¬∑ rfl
  have h‚ÇÉ {f : m‚ÇÅ ‚ü∂ m‚ÇÅ} (hf : D‚ÇÅ.W f) : f = ùüô _ := by
    obtain ((hf | ‚ü®‚ü®‚ü©‚ü©) | hf) := hf
    ¬∑ exact (h‚ÇÅ (D.src hf)).elim
    ¬∑ rfl
    ¬∑ rw [MorphismProperty.ofHoms_iff] at hf
      obtain ‚ü®‚ü®j, hj‚ü©, hf‚ü© := hf
      obtain rfl : m‚ÇÅ = j := congr_arg Arrow.leftFunc.obj hf
      exact (h‚ÇÅ hj).elim
  let hm‚ÇÅ : D‚ÇÅ.Terminal m‚ÇÅ :=
    .ofExistsUnique (Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©)) (by
        rintro j (hj | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©)
        ¬∑ exact ‚ü®œÜ ‚ü®_, hj‚ü©, Or.inr ‚ü®_‚ü©‚ü©
        ¬∑ exact ‚ü®ùüô _, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©)‚ü©) (by
        rintro j (hj | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) l‚ÇÅ l‚ÇÇ hl‚ÇÅ hl‚ÇÇ
        ¬∑ obtain rfl := h‚ÇÇ hj hl‚ÇÅ
          obtain rfl := h‚ÇÇ hj hl‚ÇÇ
          rfl
        ¬∑ rw [h‚ÇÉ hl‚ÇÅ, h‚ÇÉ hl‚ÇÇ]) (by
      rintro j k f ((hf | ‚ü®‚ü®‚ü©‚ü©) | ‚ü®‚ü®j, hj‚ü©‚ü©)
      ¬∑ exact ‚ü®œÜ ‚ü®_, D.src hf‚ü©, œÜ ‚ü®_, D.tgt hf‚ü©,
          Or.inr ‚ü®_‚ü©, Or.inr ‚ü®_‚ü©, D.terminal.comm_assoc _ hf _‚ü©
      ¬∑ exact ‚ü®ùüô _, ùüô _, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), by simp‚ü©
      ¬∑ exact ‚ü®œÜ ‚ü®_, hj‚ü©, ùüô _, Or.inr ‚ü®_‚ü©, Or.inl (Or.inr ‚ü®‚ü®‚ü©‚ü©), by simp‚ü©)
  have lift_eq (j : J) (hj : D.P j) : hm‚ÇÅ.lift (Or.inl hj) = œÜ ‚ü®_, hj‚ü© :=
    hm‚ÇÅ.uniq _ (Or.inr ‚ü®_‚ü©)
  let D‚ÇÅ' : Diagram J Œ∫ :=
    { toPreDiagram := D‚ÇÅ
      e := m‚ÇÅ
      terminal := hm‚ÇÅ
      uniq_terminal j hj := by
        obtain (hj' | ‚ü®‚ü®‚ü©‚ü©) := hj.prop
        ¬∑ exact hm‚ÇÄ.elim (u ‚â´ hj.lift (Or.inr ‚ü®‚ü®‚ü©‚ü©) ‚â´ D.terminal.lift hj')
        ¬∑ rfl}
  exact ‚ü®D‚ÇÅ', homOfLE ‚ü®le_sup_left.trans le_sup_left, le_sup_left‚ü©,
    by simpa [functorMap, D‚ÇÅ', lift_eq _ D.terminal.prop, œÜ]‚ü©

lemma aux :
    ‚àÉ (Œ± : Type w) (_ : PartialOrder Œ±) (_ : IsCardinalFiltered Œ± Œ∫)
      (F : Œ± ‚•§ J), F.Final :=
  ‚ü®_, _, isCardinalFiltered J Œ∫ hJ, functor J Œ∫, final_functor J Œ∫ hJ‚ü©

end exists_cardinal_directed

lemma exists_cardinal_directed [Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫] :
    ‚àÉ (Œ± : Type w) (_ : PartialOrder Œ±) (_ : IsCardinalFiltered Œ± Œ∫)
      (F : Œ± ‚•§ J), F.Final := by
  have := isFiltered_of_isCardinalFiltered Œ∫.ord.toType Œ∫
  obtain ‚ü®Œ±, _, _, F, _‚ü© :=
    exists_cardinal_directed.aux (J √ó Œ∫.ord.toType) Œ∫ (fun ‚ü®j, x‚ü© ‚Ü¶
      ‚ü®‚ü®j, Order.succ x‚ü©, (ùüô _, homOfLE (Order.le_succ x)), ‚ü®fun ‚ü®_, f‚ü© ‚Ü¶ by
        have : NoMaxOrder Œ∫.ord.toType :=
          Cardinal.noMaxOrder (Cardinal.IsRegular.aleph0_le Fact.out)
        exact not_isMax _ (Order.max_of_succ_le (leOfHom f))‚ü©‚ü©)
  exact ‚ü®_, _, inferInstance, F ‚ãô Prod.fst _ _, inferInstance‚ü©

end IsCardinalFiltered

lemma IsFiltered.isDirected (Œ± : Type w) [PartialOrder Œ±] [IsFiltered Œ±] :
    IsDirected Œ± (¬∑ ‚â§ ¬∑) where
  directed i j := ‚ü®max i j, leOfHom (leftToMax i j), leOfHom (rightToMax i j)‚ü©

attribute [local instance] Cardinal.fact_isRegular_aleph0 in
lemma IsFiltered.exists_directed
    (J : Type w) [SmallCategory J] [IsFiltered J] :
    ‚àÉ (Œ± : Type w) (_ : PartialOrder Œ±) (_ : IsDirected Œ± (¬∑ ‚â§ ¬∑)) (_ : Nonempty Œ±)
      (F : Œ± ‚•§ J), F.Final := by
  have := (isCardinalFiltered_aleph0_iff.{w} J).2 inferInstance
  obtain ‚ü®Œ±, _, _, F, _‚ü© := IsCardinalFiltered.exists_cardinal_directed J .aleph0
  have : IsFiltered Œ± := by rwa [‚Üê isCardinalFiltered_aleph0_iff.{w}]
  exact ‚ü®Œ±, _, IsFiltered.isDirected _, nonempty, F, inferInstance‚ü©

end CategoryTheory
